Use Windows.pkg     
Use seq_chnl.pkg    
Use cFileNameForm.pkg
Use vwin32fhW.pkg

Object oSaveCodeFiles_dg is a ModalPanel
    Set Size to 131 302
    Set Label to "Save Code To Files"
    Set piMinSize to 89 211
    Set Location to 2 2
    Set Border_Style to Border_Thick   
    Set Locate_Mode to CENTER_ON_PARENT                      
    
    Property String[] pasLegacyCode
    Property String[] pasRefactoredCode
    Property Boolean pbSaved False
    
    Object oOK_Btn is a Button
        Set Label to "Save"
        Set Location to 113 187
        Set peAnchors to anBottomRight
        Set Default_State to True

        Procedure OnClick                   
            String[] asCode  
            String sFileName
            
            Get Value of oLegacyCode_fm to sFileName
            Set psCodeFile of (phoEditorLegacy(ghoApplication)) to sFileName
            Get pasLegacyCode to asCode
            Send SaveData of oLegacyCode_fm asCode
            
            Get Value of oRefactoredCode_fm to sFileName
            Set psCodeFile of (phoEditorRefactored(ghoApplication)) to sFileName
            Get pasRefactoredCode to asCode
            Send SaveData of oRefactoredCode_fm asCode
            
            Set pbSaved to True
            Send Close_Panel
        End_Procedure

    End_Object

    Object oCancel_Btn is a Button
        Set Label    to "&Cancel"
        Set Location to 113 242
        Set peAnchors to anBottomRight

        Procedure OnClick
            Set pbSaved to False
            Send Close_Panel
        End_Procedure

    End_Object     

    Object oLegacyCode_fm is a cFileNameForm
        Set Size to 12 226
        Set Location to 30 64
        Set Label to "File Name for Legacy Code"
        Set Label_Col_Offset to 0
        Set Label_Justification_Mode to JMode_Top
    End_Object

    Object oRefactoredCode_fm is a cFileNameForm
        Set Size to 12 226
        Set Location to 57 64
        Set Label to "File Name for Refactored Code"
        Set Label_Col_Offset to 0
        Set Label_Justification_Mode to JMode_Top
    End_Object

    Object oInfo_tb is a TextBox
        Set Size to 10 177
        Set Location to 77 65
        Set Label to "NOTE: Files will get overwritten if they already exists"
        Set FontWeight to fw_Bold
    End_Object
    
    Procedure Activating
        Set psFileName of oLegacyCode_fm     to "LegacyCode.pkg"
        Set psFileName of oRefactoredCode_fm to "RefactoredCode.pkg"  
        Forward Send Activating
    End_Procedure

    On_Key Key_Ctrl+Key_S Send KeyAction of oOK_Btn
    On_Key Key_Escape     Send KeyAction of oCancel_Btn
End_Object

Function ShowSaveCodeFiles String[] asLegacyCode String[] asRefactoredCode Returns Boolean
    Handle ho                                                                             
    Boolean bSaved
    
    Move (oSaveCodeFiles_dg(Self)) to ho
    Set pasLegacyCode     of ho to asLegacyCode
    Set pasRefactoredCode of ho to asRefactoredCode
    Set pbSaved           of ho to False
    Send Popup of ho
    Get pbSaved           of ho to bSaved
    Function_Return bSaved
End_Function                                        

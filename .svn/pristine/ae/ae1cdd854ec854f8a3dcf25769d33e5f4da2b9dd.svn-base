Use vWin32fh.pkg

Class cSystemIO_Path is a cObject
    {Visibility=Private}
    Function ParseFileFirstName String sFileName Returns String
        Move (ParseFileName(sFileName)) to sFileName //Removes path
        String sExtension sFileFirstName
        Move (ParseFileExtension(sFileName)) to sExtension
        Move (Left(sFileName, Length(sFileName) - Length(sExtension) - 1)) to sFileFirstName
        Function_Return sFileFirstName
    End_Function
    {Visibility=Private}
    Function ANSIFileExists String sFile Returns Integer
        Integer iExists
        String sFileANSI
        Move (ToANSI(sFile)) to sFileANSI
        File_Exist sFileANSI iExists
        Function_Return iExists
    End_Function
    
    //If sFileName exists, return with a suffix that makes it unique
    {Visibility=Public}
    Function UniqueFileName String sFileName Returns String
        String sUniqueFileName

        Integer iPos iNumSuffix
        Boolean bFileExists
        String sFirstName sExtension sPath sNumSuffix sSuffixType
        Get ANSIFileExists sFileName to bFileExists
        If (not(bFileExists)) Function_Return sFileName
    
        Move (ParseFolderName(sFileName)) to sPath
        Get ParseFileFirstName sFileName to sFirstName
        Move (ParseFileExtension(sFileName)) to sExtension
        If (Pos("_", sFirstName) = 0) Begin
            Move (sFirstName+"_1") to sFirstName
            Function_Return (UniqueFileName(Self,sPath+sFirstName+"."+sExtension))
        End
    
        Move (Length(sFirstName)) to iPos
        While ((iPos > 1) and (Mid(sFirstName,1,iPos) <> "_"))
            Move (Mid(sFirstName,1,iPos)+sNumSuffix) to sNumSuffix
            Move (iPos-1) to iPos
        Loop
        If (IsPositiveOrZeroInteger(gcStringMethods, sNumSuffix)) Move (Integer(sNumSuffix)) to iNumSuffix
        Else Move 0 to iNumSuffix
        If (iNumSuffix = 0) Move (sFirstName+"_1") to sFirstName
        Else Begin
            Move (Left(sFirstName, iPos)+String(iNumSuffix+1)) to sFirstName
        End
        Function_Return (UniqueFileName(Self, sPath+sFirstName+"."+sExtension))
    End_Function
    
    {Visibility=Public}
    Function RemoveLastPartOfPath String sPath Returns String
        Integer iTeller
        For iTeller from 0 to (length(sPath)-1)
        If (Mid(sPath, 1, Length(sPath) - iTeller) <> "\") Loop
        If (iTeller < (Length(sPath) - 1)) Move (left(sPath, Length(sPath) - (iTeller + 1))) to sPath
        Else Move "" to sPath
        Function_Return sPath
    End_Function
End_Class

//Global variable serves as "static class identifier"
Global_Variable Handle gcSystemIO_Path
Object oSystemIO_Path is a cSystemIO_Path
    Move Self to gcSystemIO_Path
End_Object

Use DataDict.pkg
Use cDDOReStyler.pkg

Open SysFile


Enum_List
    Define eSplitUnknown              for 0 
    Define eSplitBySpaceSemiColumn    for 1
    Define eSplitBySemiColumn         for 2
    Define eSplitByBeginEnd           for 3
End_Enum_List

Define CS_SplitBySpaceSemiColumn    for "Split line space and semicolon"
Define CS_SplitBySemiColumn         for "Split line with semicolon"
Define CS_SplitByBeginEnd           for "Add a Begin/End block"

// ToDo: These doesn't feel right! Ask Wil about them
// Aren't modern DD classes named o<tablename>_DataDictionary ?
Enum_List
    Define eDDUnknown       for 0 // style unknown or not yet classified
    Define eDDOldStyle      for 1 // <tablename>_DD
    Define eDDStudioStyle   for 2 // o<tablename>_DD
    Define eDDNewStyle      for 3 // o<tablename>_dd
End_Enum_List

Define CS_DDOldStyle      for "<tablename>_DD"
Define CS_DDLegacyStyle   for "o<tablename>_DD"
Define CS_DDNewStyle      for "o<tablename>_dd"


Object oSplitInFile is a DescriptionValidationTable
    Procedure Fill_List
        Forward Send Fill_List
        Send Add_Table_Value eSplitBySpaceSemiColumn CS_SplitBySpaceSemiColumn
        Send Add_Table_Value eSplitBySemiColumn      CS_SplitBySemiColumn
        Send Add_Table_Value eSplitByBeginEnd        CS_SplitByBeginEnd
    End_Procedure
End_Object

Object oDDOStyler is a DescriptionValidationTable
    Procedure Fill_List
        Forward Send Fill_List
        Send Add_Table_Value eDDOldStyle    CS_DDOldStyle
        Send Add_Table_Value eDDStudioStyle CS_DDLegacyStyle
        Send Add_Table_Value eDDNewStyle    CS_DDNewStyle
    End_Procedure
End_Object

Class cSysFileDataDictionary is a DataDictionary
    
    Procedure Construct_Object
        Forward Send Construct_Object
        Set Main_File to SysFile.File_Number

        Set Foreign_Field_Option DD_KEYFIELD DD_NOPUT to True
        Set Foreign_Field_Option DD_KEYFIELD DD_FINDREQ to True
        Set Foreign_Field_Option DD_INDEXFIELD DD_NOPUT to True
        Set Foreign_Field_Option DD_DEFAULT DD_DISPLAYONLY to True

        Set Field_Label_Long Field SysFile.PathStarZen to "Path to Starzen tool"

        Set Field_Label_Long Field SysFile.PathSourceCompareTool to "Source Compare tool"
        Set Field_Label_Short Field SysFile.PathSourceCompareTool to "Source Compare tool"
        Set Status_Help Field SysFile.PathSourceCompareTool to "Path to a Source Compare tool"

        Set Field_Label_Long Field SysFile.FileExtensionFilter to "File Extension Filter"
        Set Field_Label_Short Field SysFile.FileExtensionFilter to "File Extension Filter"
        Set Field_Class_Name Field SysFile.FileExtensionFilter to "Combo"

        Set Field_Label_Long Field SysFile.bCountSourceLines to "Count number of source lines"
        Set Field_Label_Short Field SysFile.bCountSourceLines to "Count source lines"
        Set Field_Class_Name Field SysFile.bCountSourceLines to "Checkbox"
        Set Field_Checkbox_Values Field SysFile.bCountSourceLines to "1" "0"
        Set Status_Help Field SysFile.bCountSourceLines to "Note: This function must be run alone. All other functions will be ignored!"
        Set Field_Checkbox_Values Field SysFile.bEditorDropSelf to "1" "0"

    End_Procedure

    Procedure Field_Defaults
        Forward Send Field_Defaults
        Set Field_Changed_Value Field SysFile.bCountSourceLines to 0
        Set Field_Changed_Value Field SysFile.bEditorDropSelf to 0
    End_Procedure 
    
    Procedure Update
        Forward Send Update
    End_Procedure

End_Class
Use cTestFixture.pkg
Use cTest.pkg
Use cMock.pkg

Class cFixtureSetupCounter is a cMock
    Procedure SetupOneTime
        Send CalledProcedure (RefProc(SetupOneTime))
    End_Procedure
    Procedure SetupChildFixture1
        Send CalledProcedure (RefProc(SetupChildFixture1))
    End_Procedure
    Procedure SetupChildFixture2
        Send CalledProcedure (RefProc(SetupChildFixture2))
    End_Procedure
End_Class

Object Indented_test_fixtures_spec is a cTestFixture
    Object oSetupMock is a cFixtureSetupCounter
    End_Object
    
    Object SetupOneTime_in_a_fixture_runs_once_before_nested_fixtures is a cTestFixture
        Procedure SetupOneTime
            Send SetupOneTime to oSetupMock
        End_Procedure
        Object Fixture1 is a cTestFixture
            {Published=True}
            Procedure We_need_a_test_object_for_setup_to_run
            End_Procedure
            Procedure Setup
                Send SetupChildFixture1 to oSetupMock
            End_Procedure
        End_Object
        Object Fixture2 is a cTestFixture
            {Published=True}
            Procedure We_need_a_test_object_for_setup_to_run
            End_Procedure
            Procedure Setup
                Send SetupChildFixture2 to oSetupMock
            End_Procedure
        End_Object
        {Published=TRue}
        Procedure We_need_a_test_object_for_setup_to_run
        End_Procedure
        Procedure AfterTearDownOneTime
            Send AssertIAreEqual 1 (FirstTimeProcedureHasBeenCalled(oSetupMock, RefProc(SetupOneTime)))
            Send Assert (FirstTimeProcedureHasBeenCalled(oSetupMock, RefProc(SetupChildFixture1)) > 1)
        End_Procedure
    End_Object
End_Object

Use DataDict.pkg
Use cRDCAutoCreateNewID.pkg
Use RefactorConstants.h.pkg

// We need to create an instance of this class in the cRefactorEngine class.
// That class is instantiated by the cRefactorApplication class - in the End_Construct_Object.
// Hence, we can't have a "Use cFunctionsDataDictionary.pkg" at the top of the
// cRefactorEngine package, as that would render compilation errors in the cRefactorApplicatioon class,
// as that package has a "Use cRefactorEngie.pkg" at the top.
// As a workaround we instead open these files in Construct_Object.
Declare_Datafile SysFile
Declare_Datafile Functions
Declare_Datafile FunctionsA

Object oFunctionTypes is a DescriptionValidationTable
    Set Table_Title to "Function Type"
    Procedure Fill_List
        Forward Send Fill_List
        Send Add_Table_Value eStandardFunction   CS_StandardFunction
        Send Add_Table_Value eRemoveFunction     CS_RemoveFunction
        Send Add_Table_Value eEditorFunction     CS_EditorFunction
        Send Add_Table_Value eOtherFunction      CS_OtherFunction
        Send Add_Table_Value eOtherFunctionAll   CS_OtherFunctionAll
        Send Add_Table_Value eReportFunction     CS_ReportFunction
        Send Add_Table_Value eReportFunctionAll  CS_ReportFunctionAll
    End_Procedure
End_Object

Register_Object oFunctions_sl

Class cFunctionsDataDictionary is a DataDictionary

    Import_Class_Protocol cRDCAutoCreateNewIDMixin

    Procedure Construct_Object
        Forward Send Construct_Object

        Property Integer piFunctionType eAllFunctions

        Open SysFile
        Open Functions
        Open FunctionsA

        Set_Attribute DF_FILE_ALIAS of Functions.File_Number to DF_FILE_IS_MASTER
        Set_Attribute DF_FILE_ALIAS of FunctionsA.File_Number to DF_FILE_IS_ALIAS

        Set Main_File to Functions.File_Number
        Set Add_System_File to SysFile.File_Number DD_Lock_On_All

        Set Foreign_Field_Option DD_KEYFIELD DD_NOPUT to True
        Set Foreign_Field_Option DD_KEYFIELD DD_FINDREQ to True
        Set Foreign_Field_Option DD_INDEXFIELD DD_NOPUT to True
        Set Foreign_Field_Option DD_DEFAULT DD_DISPLAYONLY to True

        Set Key_Field_State Field Functions.ID to True
        Set Field_Prompt_Object Field Functions.ID to oFunctions_sl
        Set Status_Help Field Functions.ID to "Automatically generated ID number"

        Set Field_Prompt_Object Field Functions.Function_Name to oFunctions_sl
        Set Status_Help Field Functions.Function_Name to "the name entered must be exactly the same as the function name itself. Then the code must be added to the cRefactorDbView class (cRefactorDbView.pkg). See class code for standard function parameters."

        Set Status_Help Field Functions.Function_Help to "Longer explanation on what the function does. Shows when the mouse hovers above a function grid."

        Set Status_Help Field Functions.SummaryText to "Function summary text that appears after a refactoring has run."

        Set Field_Class_Name Field Functions.Selected to "Checkbox"
        Set Field_Checkbox_Values Field Functions.Selected to "1" "0"
        Set Status_Help Field Functions.Selected to "Selected function"

        Set Field_Value_Table Field Functions.Type to oFunctionTypes
        Set Status_Help Field Functions.Type to "The type of function. The type determines what data is send to the function: Line-by-line, as a String array, or all files as a string array."
        Set Field_Class_Name Field Functions.Type to "Combo"

        Set Field_Class_Name Field Functions.bHasParameter to "Checkbox"
        Set Field_Checkbox_Values Field Functions.bHasParameter to "1" "0"
        Set Status_Help Field Functions.bHasParameter to "This is a system function and may not be changed, because it is handled a bit differently."

        Set Field_Class_Name Field Functions.bPrivate to "Checkbox"
        Set Status_Help Field Functions.bPrivate to "If checked it indicates that it is a private function for your company that should not be shared (Exported/Imported) It is contained in its own package file Used inside the #IFDEF CS_Use_MyPackage construct."
        Set Field_Checkbox_Values Field Functions.bPrivate to "1" "0"
        Set Field_Label_Long Field Functions.bPrivate to "Private Function"
        Set Field_Label_Short Field Functions.bPrivate to "Private"

        Set Status_Help Field Functions.Counter to "This will initialize to 0 before each refactor run."
    End_Procedure

    Procedure Field_Defaults
        Forward Send Field_Defaults
        Set Field_Changed_Value Field Functions.Selected to 0
        Set Field_Changed_Value Field Functions.Type to 0
        Set Field_Changed_Value Field Functions.bHasParameter to 0
        Set Field_Changed_Value Field Functions.bPrivate to 0
    End_Procedure

    Procedure Creating
        Integer iID
        Forward Send Creating
        Get AutoCreateNewID File_Field Functions.ID True to iID
    End_Procedure

    Procedure Update
        Integer iIndex
        Boolean bChangeWriteProtected
        String sFunctionName sParam sValue
        String[] asValidations

        If (Functions.bHasParameter = True) Begin
            Move (Trim(Functions.Parameter)) to sParam
            Move (Trim(Functions.ParameterValidation)) to sValue
            If (sValue <> "") Begin
                Move (StrSplitToArray(sValue, ",")) to asValidations
                Move (SearchArray(sParam, asValidations)) to iIndex
                If (iIndex = -1) Begin  
                    Send Data_Set_Error -1 DFERR_OPERATOR ("Invalid value. Must be one of:" * String(sValue))
//                    Send UserError ("Invalid value. Must be one of:" * String(sValue))
                End
            End
        End
        Else Begin
            If (Trim(Functions.Parameter) <> "") Begin
                Send Data_Set_Error -1 DFERR_OPERATOR "This function doesn't use parameters and you are therefore not allowed to enter one."
//                Send UserError "This function doesn't use parameters and you are therefore not allowed to enter one."
            End
        End
        Move (Trim(Functions.Function_Help)) to Functions.Function_Help
        Send AdjustSelected (If(Functions.Selected, 1, 0)) Functions.Type
    End_Procedure

    Procedure Backout
        Send AdjustSelected (If(Functions.Selected, -1, 0)) Functions.Type
    End_Procedure

    Procedure AdjustSelected Integer iState Integer eType
        String sFunctionName
        Integer iTotFunctions

        Get TotalNoOfFunctions to iTotFunctions
        Move (Lowercase(Trim(Functions.Function_Name))) to sFunctionName
        If (sFunctionName = "") Begin
            Procedure_Return
        End
        // Handling of special function.
        Case Begin
            Case (sFunctionName = Lowercase(CS_CountSourceLines))
                Move Functions.Selected to SysFile.bCountSourceLines
                Case Break
        Case End

        If (SysFile.SelectedFunctionTotal <= iTotFunctions) Begin
            Add iState to SysFile.SelectedFunctionTotal
        End
        If (SysFile.SelectedFunctionTotal < 0) Begin
            Move 0 to SysFile.SelectedFunctionTotal
        End

        Case Begin
            Case (eType = eStandardFunction)
                Add iState to SysFile.SelectedeStandardFunction
                If (SysFile.SelectedeStandardFunction < 0) Begin
                    Move 0 to SysFile.SelectedeStandardFunction
                End
                Case Break
            Case (eType = eRemoveFunction)
                Add iState to SysFile.SelectedeRemoveFunction
                If (SysFile.SelectedeRemoveFunction < 0) Begin
                    Move 0 to SysFile.SelectedeRemoveFunction
                End
                Case Break
            Case (eType = eEditorFunction)
                Add iState to SysFile.SelectedeEditorFunction
                If (SysFile.SelectedeEditorFunction < 0) Begin
                    Move 0 to SysFile.SelectedeEditorFunction
                End
                Case Break
            Case (eType = eReportFunction)
                Add iState to SysFile.SelectedeReportFunction
                If (SysFile.SelectedeReportFunction < 0) Begin
                    Move 0 to SysFile.SelectedeReportFunction
                End
                Case Break
            Case (eType = eReportFunctionAll)
                Add iState to SysFile.SelectedeReportAllFunction
                If (SysFile.SelectedeReportAllFunction < 0) Begin
                    Move 0 to SysFile.SelectedeReportAllFunction
                End
                Case Break
            Case (eType = eOtherFunction)
                Add iState to SysFile.SelectedeOtherFunction
                If (SysFile.SelectedeOtherFunction < 0) Begin
                    Move 0 to SysFile.SelectedeOtherFunction
                End
                Case Break
            Case (eType = eOtherFunctionAll)
                Add iState to SysFile.SelectedeOtherAllFunction
                If (SysFile.SelectedeOtherAllFunction < 0) Begin
                    Move 0 to SysFile.SelectedeOtherAllFunction
                End
                Case Break
        Case End

        SaveRecord SysFile
    End_Procedure

    // Pass one of the pre-defined function types;
    // Returns number of selected functions for the passed type.
    { EnumList = "eStandardFunction, eEditorFunction, eReportFunction or eRemoveFunction" }
    Function SelectedTypeFunctions Integer eType Returns Integer
        Integer iItems

        Case Begin
            Case (eType = eStandardFunction)
                Move SysFile.SelectedeStandardFunction to iItems
                Case Break
            Case (eType = eEditorFunction)
                Move SysFile.SelectedeEditorFunction to iItems
                Case Break
            Case (eType = eReportFunction)
                Move SysFile.SelectedeReportFunction to iItems
                Case Break
            Case (eType = eRemoveFunction)
                Move SysFile.SelectedeRemoveFunction to iItems
                Case Break
            Case Else
                Move SysFile.SelectedFunctionTotal to iItems
        Case End

        Function_Return iItems
    End_Function

    // Finds the first selected Function record for the passed type.
    // Returns True if a match was found.
    // NOTE: It leaves the FunctionsA global record buffer after
    //       a successful find, to be used elsewhere.
    Function FindFirstSelectedFunction Integer eType Returns Boolean
        Clear FunctionsA
        Move True  to FunctionsA.Selected
        Move eType to FunctionsA.Type
        Find gt FunctionsA by Index.3
        Function_Return (Found and FunctionsA.Type = eType)
    End_Function

    // Finds the next selected function for the passed type
    // Returns True if a match was found.
    // It is mandatory to start a loop by first calling the
    // FindFirstSelectedFunction function.
    // NOTE: It leaves the FunctionsA global record buffer after
    //       a successful find, to be used elsewhere.
    Function FindNextSelectedFunction Integer eType Returns Boolean
        Move True  to FunctionsA.Selected
        Move eType to FunctionsA.Type
        Find gt FunctionsA by Index.3
        Function_Return (Found and FunctionsA.Type = eType)
    End_Function

    // Returns the total number of selected functions for all types.
    Function TotSelectedFunctions Returns Integer
        Function_Return (SysFile.SelectedFunctionTotal)
    End_Function

    // Returns the total number of functions in the database.
    // We only count Published functions.
    Function TotalNoOfFunctions Returns Integer
        Integer iItems
        
        Constraint_Set (Self +35) Clear
        Constrained_Clear eq FunctionsA by Index.3
        Constrain FunctionsA.bPublished eq 1
        Constrained_Find First FunctionsA by Index.3
        While (Found)
            Increment iItems
            Constrained_Find Next
        Loop
        Function_Return iItems
    End_Function

    // Returns the total number of selected functions.
    Function TotalNoSelected Returns Integer
        Integer iItems

        Constraint_Set (Self +5) Clear
        Constrained_Clear eq FunctionsA by Index.3
        Constrain FunctionsA.Selected eq 1
        Constrain FunctionsA.bPublished eq 1
        Constrained_Find First FunctionsA by Index.3
        While (Found)
            Increment iItems
            Constrained_Find Next
        Loop
        Function_Return iItems
    End_Function

    // Returns the total number of selected functions.
    Function TotalNoSelectedPerType Integer eType Returns Integer
        Integer iItems

        Constraint_Set (Self +6) Clear
        Constrained_Clear eq FunctionsA by Index.3
        Constrain FunctionsA.Selected eq 1
        Constrain FunctionsA.bPublished eq 1
        Constrain FunctionsA.Type eq eType
        Constrained_Find First FunctionsA by Index.3
        While (Found)
            Increment iItems
            Constrained_Find Next
        Loop
        Function_Return iItems
    End_Function

    Procedure ResetSysFile
        Reread SysFile
            Move 0 to SysFile.iCountNumberOfFiles
            Move 0 to SysFile.iCountNumberOfChangedFiles
            Move 0 to SysFile.iCountNumberOfLines
            Get TotalNoSelected to SysFile.SelectedFunctionTotal
            SaveRecord SysFile
        Unlock
        Send ResetAllSysFileSelectionCounters
    End_Procedure

    { EnumList = "eStandardFunction", "eRemoveFunction", "eEditorFunction", "eReportFunction", "eReportFunctionAll", "eOtherFunction", "eOtherFunctionAll"}
    Procedure ResetAllSysFileSelectionCounters
        Integer iCount
        Lock
            Get TotalNoSelectedPerType eStandardFunction to iCount
            Move iCount to SysFile.SelectedeStandardFunction
            Get TotalNoSelectedPerType eRemoveFunction to iCount
            Move iCount to SysFile.SelectedeRemoveFunction
            Get TotalNoSelectedPerType eEditorFunction to iCount
            Move iCount to SysFile.SelectedeEditorFunction
            Get TotalNoSelectedPerType eReportFunction to iCount
            Move iCount to SysFile.SelectedeReportFunction
            Get TotalNoSelectedPerType eReportFunctionAll to iCount
            Move iCount to SysFile.SelectedeReportAllFunction
            Get TotalNoSelectedPerType eOtherFunction to iCount
            Move iCount to SysFile.SelectedeOtherFunction
            Get TotalNoSelectedPerType eOtherFunctionAll to iCount
            Move iCount to SysFile.SelectedeOtherAllFunction
        Unlock
    End_Procedure

    // Zeroe's out all function counters
    Procedure ResetFunctionsCounters
        Send ResetSysFile

        Constraint_Set (Self  + 1) Clear
        Constrained_Clear eq FunctionsA by Index.1
        Constrained_Find First FunctionsA by Index.1
        While (Found)
            Reread FunctionsA
                Move 0 to FunctionsA.Counter
                SaveRecord FunctionsA
            Unlock
            Constrained_Find Next
        Loop
    End_Procedure

    Procedure UpdateDataFromMetaTags tFuncLib[] FuncLibArray
        Integer iSize iCount
        String sFunctionName
        Boolean bOK

        Send UnPublishedAllRecords
        Move True to bOK
        Move (SizeOfArray(FuncLibArray)) to iSize
        Decrement iSize
        For iCount from 0 to iSize
            Move FuncLibArray[iCount].sName to sFunctionName
            Clear FunctionsA
            Move sFunctionName to FunctionsA.Function_Name
            Find eq FunctionsA.Function_Name
            If (Found = True) Begin
                Get UpdateRecordFromMetaTag FuncLibArray[iCount] to bOK
                If (bOK = False) Begin
                    Send Data_Set_Error -1 DFERR_PROGRAM "Could not update Functions table record from meta-data! Process 'UpdateFromMetaTags' interrupted."
                    Procedure_Return
                End
            End
            Else Begin
                Get CreateNewRecordFromMetaTag FuncLibArray[iCount] to bOK
                If (bOK = False) Begin
                    Send Data_Set_Error -1 DFERR_PROGRAM "Could not update Functions table record from meta-data! Process 'UpdateFromMetaTags' interrupted."
                    Procedure_Return
                End
            End
        Loop

    End_Procedure

    // Sets all Functions records as unselected. (FunctionsA.Selected = 0)
    Procedure UnSelectAllRecords
        Constraint_Set (Self  + 11) Clear
        Constrained_Clear eq FunctionsA by Index.1
        Constrained_Find First FunctionsA by Index.1
        While (Found)
            Reread FunctionsA
                Move 0 to FunctionsA.Selected
                SaveRecord FunctionsA
            Unlock
            Constrained_Find Next
        Loop
    End_Procedure
    
    Procedure SelectRecordsFromStringArray String[] asFunctions
        Integer iCount iSize
        
        Move (SizeOfArray(asFunctions)) to iSize
        Decrement iSize
        For iCount from 0 to iSize
            Clear FunctionsA
            Move asFunctions[iCount] to FunctionsA.Function_Name
            Find eq FunctionsA by Index.2
            If (Found = True) Begin
                Reread FunctionsA
                    Move 1 to FunctionsA.Selected
                    SaveRecord FunctionsA
                Unlock
            End
        Loop
    End_Procedure
    
    // Sets all Functions records as unpublished. (FunctionsA.bPublished = 0)
    Procedure UnPublishedAllRecords
        Constraint_Set (Self  + 10) Clear
        Constrained_Clear eq FunctionsA by Index.1
        Constrained_Find First FunctionsA by Index.1
        While (Found)
            Reread FunctionsA
                Move 0 to FunctionsA.bPublished
                SaveRecord FunctionsA
            Unlock
            Constrained_Find Next
        Loop
    End_Procedure

    Function CreateNewRecordFromMetaTag tFuncLib FuncLib Returns Boolean
        Integer iID
        String[] asEnumList
        String sParamValidation

        Move False to Err
        Move FuncLib.FuncEnumList.asEnumList to asEnumList
        Get SanitizeStringArrayValues asEnumList to asEnumList
        Move (StrJoinFromArray(asEnumList, ","))   to sParamValidation

        Clear FunctionsA
        Get AutoCreateNewID File_Field FunctionsA.ID to iID
        Move 1                                   to FunctionsA.bPublished
        Move FuncLib.sName                       to FunctionsA.Function_Name
        Move FuncLib.sHelp                       to FunctionsA.Function_Help
        Move FuncLib.eFunctionType               to FunctionsA.Type
        Move FuncLib.sSummary                    to FunctionsA.SummaryText
        Move FuncLib.bFuncEnumList               to FunctionsA.bHasParameter
        Move FuncLib.FuncEnumList.sInitialValue  to FunctionsA.Parameter
        Move FuncLib.FuncEnumList.sParamHelp     to FunctionsA.ParameterHelp
        Move sParamValidation                    to FunctionsA.ParameterValidation
        SaveRecord FunctionsA
        Function_Return (not(Err))
    End_Function

    Function UpdateRecordFromMetaTag tFuncLib FuncLib Returns Boolean
        Integer iID
        String[] asEnumList
        String sParamValidation

        Move False to Err
        Move FuncLib.FuncEnumList.asEnumList to asEnumList
        Get SanitizeStringArrayValues asEnumList to asEnumList
        Move (StrJoinFromArray(asEnumList, ",")) to sParamValidation

        Reread
            Move 1 to FunctionsA.bPublished
            If (Trim(FuncLib.sHelp) <> Trim(FunctionsA.Function_Help)) Begin
                Move FuncLib.sHelp to FunctionsA.Function_Help
            End
            If (Trim(FuncLib.eFunctionType) <> Trim(FunctionsA.Type)) Begin
                Move FuncLib.eFunctionType to FunctionsA.Type
            End
            If (Trim(FuncLib.sSummary) <> Trim(FunctionsA.SummaryText)) Begin
                Move FuncLib.sSummary to FunctionsA.SummaryText
            End
            If (Trim(FuncLib.bFuncEnumList) <> Trim(FunctionsA.bHasParameter)) Begin
                Move FuncLib.bFuncEnumList to FunctionsA.bHasParameter
            End
            If (Trim(FuncLib.FuncEnumList.sInitialValue) <> Trim(FunctionsA.Parameter)) Begin
                Move FuncLib.FuncEnumList.sInitialValue to FunctionsA.Parameter
            End
            If (Trim(FuncLib.FuncEnumList.sParamHelp) <> Trim(FunctionsA.ParameterHelp)) Begin
                Move FuncLib.FuncEnumList.sParamHelp    to FunctionsA.ParameterHelp
            End
            If (Trim(sParamValidation) <> Trim(FunctionsA.ParameterValidation)) Begin
                Move sParamValidation to FunctionsA.ParameterValidation
            End
            SaveRecord FunctionsA
        Unlock
        Function_Return (not(Err))
    End_Function

    // Removes any spaces from array string values.
    // Also removes and quotes or double-quotes ("') characters.
    Function SanitizeStringArrayValues String[] asEnumList Returns String[]
        Integer iSize iCount
        Move (SizeOfArray(asEnumList)) to iSize
        Decrement iSize
        For iCount from 0 to iSize
            Move (Trim(asEnumList[iCount])) to asEnumList[iCount]
            Move (Replaces('"', asEnumList[iCount], "")) to asEnumList[iCount]
            Move (Replaces("'", asEnumList[iCount], "")) to asEnumList[iCount]
        Loop
        Function_Return asEnumList
    End_Function

End_Class

#IFDEF Is$WebApp
#ELSE
Use Functions.sl
#ENDIF

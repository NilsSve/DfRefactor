Use DataDict.pkg
Use vWin32fh.pkg

Open FolderSelHea
Open FolderSelDtl
Open FolderSelDtlA
Open FolderSelHeaA

Set_Attribute DF_FILE_ALIAS  of FolderSelDtlA.File_Number to DF_FILE_IS_ALIAS

Class cFolderSelDtlDataDictionary is a DataDictionary

    Procedure Construct_Object
        Forward Send Construct_Object
        Set Main_File to FolderSelDtl.File_Number

        Set Add_Server_File to FolderSelHea.File_Number

        Set ParentNoSwitchIfCommitted FolderSelHea.File_Number to True
        Set Foreign_Field_Option DD_KEYFIELD DD_FINDREQ to True
        Set Foreign_Field_Option DD_INDEXFIELD DD_NOPUT to True
        Set Foreign_Field_Option DD_DEFAULT DD_DISPLAYONLY to True

        Set Field_Auto_Increment Field FolderSelDtl.ID to File_Field FolderSelHea.ChildID

        Set Field_Label_Long Field FolderSelDtl.FolderSelHeaID to "Header table ID"

        Set Field_Label_Long Field FolderSelDtl.FolderName to "Folder Name"
        Set Field_Label_Short Field FolderSelDtl.FolderName to "Folder Name"

    End_Procedure

    Procedure Update
        Send AdjustSelected (If(FolderSelDtl.bSelected, 1, 0))
    End_Procedure

    Procedure Backout
        Send AdjustSelected (If(FolderSelDtl.bSelected, -1, 0))
    End_Procedure

    Procedure AdjustSelected Integer iSelected
        Add iSelected to FolderSelHea.TotalSelected
    End_Procedure

    Procedure Creating
        Increment FolderSelHea.TotalFolders
    End_Procedure 
    
    Procedure CmdLineSetSelectedFolders String[] asFolders
        Integer iSize iCount iParentID 
        String sHomePath sFolder
        Get Field_Current_Value Field FolderSelDtl.FolderSelHeaID to iParentID
        If (iParentID = 0 or SizeOfArray(asFolders) = 0) Begin
            Procedure_Return
        End
        
        Get File_Field_Current_Value File_Field FolderSelHea.WorkspaceHomeFolder to sHomePath
        Get vFolderFormat sHomePath to sHomePath
        Move (SizeOfArray(asFolders)) to iSize
        Decrement iSize
        
        For iCount from 0 to iSize
            Clear FolderSelDtlA
            Move iParentID         to FolderSelDtlA.FolderSelHeaID
            Move asFolders[iCount] to sFolder
            Move (sHomePath + String(sFolder)) to sFolder
            Move sFolder           to FolderSelDtlA.FolderName
            Find Eq FolderSelDtlA by Index.2
            
            // Update select state:
            If (Found and FolderSelDtlA.bSelected = False) Begin
                Reread FolderSelDtlA
                    Move True to FolderSelDtlA.bSelected
                    SaveRecord FolderSelDtlA
                Unlock
            End
            
            // Add new folder name:    
            Else Begin
                Lock
                    Move iParentID to FolderSelDtlA.FolderSelHeaID
                    Move sFolder   to FolderSelDtlA.FolderName
                    Move True      to FolderSelDtlA.bSelected
                    SaveRecord FolderSelDtlA
                Unlock
            End
        Loop
    End_Procedure

    Procedure Deleting
        If (FolderSelDtl.ID = FolderSelHea.ChildID) Begin
            Decrement FolderSelHea.ChildID
        End
        Decrement FolderSelHea.TotalFolders
    End_Procedure

    // Returns the total number of folders for the current FolderSelHea record.
    Function TotalNoOfFolders Returns Integer
        Integer iItems
        Move FolderSelHea.ID to FolderSelHeaA.ID
        Find eq FolderSelHeaA.ID

        Constraint_Set Self Clear
        Constrained_Clear eq FolderSelDtlA by Index.1
        Constrain FolderSelDtlA.FolderSelHeaID eq FolderSelHeaA.ID
        Constrained_Find First FolderSelDtlA by Index.1
        While (Found)
            Increment iItems
            Constrained_Find Next
        Loop
        Function_Return iItems
    End_Function

    // Returns all selected folders for the current FolderSelHea record as a string array.
    Function SelectedFolders Returns String[]
        Integer iItem
        String[] asFolders asEmpty

        If (FolderSelHea.ID = 0) Begin
            Function_Return asEmpty
        End
        Move FolderSelHea.ID to FolderSelHeaA.ID
        Find eq FolderSelHeaA.ID

        Constraint_Set (Self +10) Clear
        Constrained_Clear eq FolderSelDtlA by Index.1
        Constrain FolderSelDtlA.FolderSelHeaID eq FolderSelHeaA.ID
        Constrained_Find First FolderSelDtlA by Index.1
        Move 0 to iitem
        While (Found)
            If (FolderSelDtlA.bSelected = True) Begin
                Move (Trim(FolderSelDtlA.FolderName)) to asFolders[iItem]
                Increment iItem
            End
            Constrained_Find Next
        Loop
        Function_Return asFolders
    End_Function

End_Class

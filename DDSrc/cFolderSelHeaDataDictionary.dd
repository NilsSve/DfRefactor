Use cRefactorApplication.pkg
Use DataDict.pkg
Use cFolderSelDtlDataDictionary.dd

Open SysFile
Open FolderSelHeaA
Open FolderSelHea

Set_Attribute DF_FILE_ALIAS  of FolderSelHeaA.File_Number to DF_FILE_IS_ALIAS

Class cFolderSelHeaDataDictionary is a DataDictionary

    Procedure Construct_Object
        Forward Send Construct_Object

        Property Integer piLastErr 0
        Property Boolean pbErr False

        Set Main_File to FolderSelHea.File_Number

        Set Add_Client_File to FolderSelDtl.File_Number

        Set Add_System_File to SysFile.File_Number DD_Lock_On_All
        Set Foreign_Field_Option DD_KEYFIELD DD_FINDREQ to True
        Set Foreign_Field_Option DD_INDEXFIELD DD_NOPUT to True
        Set Foreign_Field_Option DD_DEFAULT DD_DISPLAYONLY to True

        Set Field_Auto_Increment Field FolderSelHea.ID to File_Field SysFile.Next_FolderSel_ID

        Set Field_Label_Long Field FolderSelHea.UserName to "User Name"
        Set Field_Label_Short Field FolderSelHea.UserName to "User Name"
        Set Field_Class_Name Field FolderSelHea.UserName to "Form"

        Set Field_Label_Long Field FolderSelHea.WorkspaceHomeFolder to "Home Folder"
        Set Field_Label_Short Field FolderSelHea.WorkspaceHomeFolder to "Home Folder"

    End_Procedure

    Procedure Creating
        Get psUserName of ghoApplication to FolderSelHea.UserName
    End_Procedure

    Procedure SaveErrorState
        Set piLastErr to LastErr
        If (Err = True) Begin
            Set pbErr to True
        End
        Else Begin
            Set pbErr to False
        End
        Move False to Err
        Move 0 to LastErr
    End_Procedure

    Procedure RestoreErrorState
        Get piLastErr to LastErr
        If (pbErr(Self)) Begin
            Move True to Err
        End
        Else Begin
            Move False to Err
        End
    End_Procedure

    Function TotalFolders Returns Integer
        Clear FolderSelHeaA
        Get psHomePath  of ghoApplication to FolderSelHeaA.WorkspaceHomeFolder
        Get  psUsername of ghoApplication to FolderSelHeaA.UserName
        Find eq FolderSelHeaA.WorkspaceHomeFolder
        If (Found = False) Begin
            Function_Return 0
        End
        Function_Return FolderSelHeaA.TotalFolders
    End_Function

    Function TotalSelectedFolders Returns Integer
        Clear FolderSelHeaA
        Get psHomePath  of ghoApplication to FolderSelHeaA.WorkspaceHomeFolder
        Get  psUsername of ghoApplication to FolderSelHeaA.UserName
        Find eq FolderSelHeaA.WorkspaceHomeFolder
        If (Found = False) Begin
            Function_Return 0
        End
        Function_Return FolderSelHeaA.TotalSelected
    End_Function

    // Return False if save went all right.
    Function SaveNewHeaderAndChildRecords String sHomeFolder Returns Boolean
        Boolean bFound bErr bResult
        Integer iSize iCount
        String sUserName
        String[] asStartFolder asFolderArray

        // Preserve the status of the Err system flag:
        Send SaveErrorState

        Get psUsername of ghoApplication to sUserName
        Clear FolderSelHea
        Move sUserName   to FolderSelHea.UserName
        Move sHomeFolder to FolderSelHea.WorkspaceHomeFolder
        Find eq FolderSelHea.WorkspaceHomeFolder
        // If we found a record, there is nothing more to do here:
        If (Found = True) Begin
            Function_Return False
        End

        Reread
            Increment SysFile.Next_FolderSel_ID
            SaveRecord SysFile
            Clear FolderSelHea
            Move SysFile.Next_FolderSel_ID to FolderSelHea.ID
            Move sUserName   to FolderSelHea.UserName
            Move sHomeFolder to FolderSelHea.WorkspaceHomeFolder
            SaveRecord FolderSelHea
        Unlock

        If (Err = False) Begin
            Move sHomeFolder to asStartFolder[0]
            Get AllSourceFolders of ghoApplication asStartFolder to asFolderArray
            Move (SizeOfArray(asFolderArray)) to iSize
            If (iSize = 0) Begin
                Function_Return False
            End
            Decrement iSize
            // Start on 1 because the first one is the "Home" folder.
            For iCount from 1 to iSize
                Get SaveDetailRecord FolderSelHea.ID asFolderArray[iCount] sUserName False to bErr
                If (bErr = True) Begin
                    Function_Return False
                End
            Loop
        End

        Move Err to bResult
        Send RestoreErrorState
        Function_Return bResult
    End_Function

    // Note: An active header record (FolderSelHea) must be present
    //       in the global record buffer prior calling this:
    // Returns True if there was an error saving records.
    Function SaveDetailRecord Integer iHeaderID String sFolderName String sUserName Boolean bSelected Returns Boolean
        Boolean bFound bResult

        // Preserve the status of the Err system flag:
        Send SaveErrorState
        Clear FolderSelDtl
        Move iHeaderID   to FolderSelDtl.FolderSelHeaID
        Move sFolderName to FolderSelDtl.FolderName
        Find eq FolderSelDtl.FolderName
        Move (Found) to bFound

        // It any of these are true we need to save, else do nothing.
        If (bFound = False or Trim(sFolderName) <> Trim(FolderSelDtl.FolderName)) Begin
            Reread //FolderSelHeaA FolderSelDtl
                If (bFound = False) Begin
                    Increment FolderSelHea.ChildID
                    Increment FolderSelHea.TotalFolders
                    SaveRecord FolderSelHea
                    Clear FolderSelDtl
                    Move FolderSelHea.ChildID to FolderSelDtl.ID
                    Move FolderSelHea.ID      to FolderSelDtl.FolderSelHeaID
                End
                Move sFolderName to FolderSelDtl.FolderName
                Move bSelected   to FolderSelDtl.bSelected
                SaveRecord FolderSelDtl
            Unlock
        End

        Move Err to bResult
        Send RestoreErrorState
        Function_Return bResult
    End_Function

    // Delets all existing FolderSelDtlA records for the passed FolderSelHeaA.ID
    Function RemoveDetailRecords Integer iHeaID Returns Boolean
        Boolean bErr bResult

        Send SaveErrorState
        Reread FolderSelHeaA
            Move 0 to FolderSelHeaA.ChildID
            SaveRecord FolderSelHeaA
        Unlock

        Constraint_Set Self Clear
        Constrained_Clear eq FolderSelDtlA by Index.2
        Constrain FolderSelDtlA.FolderSelHeaID eq iHeaID
        Constrained_Find First FolderSelDtlA by Index.2
        While (Found)
            Lock
                Delete FolderSelDtlA
            Unlock
            Constrained_Find Next
        Loop

        Move (not(Err)) to bResult
        Send RestoreErrorState
        Function_Return (bResult = False)
    End_Function

End_Class

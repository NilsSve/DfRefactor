Use cRefactorApplication.pkg
Use DataDict.pkg
Use cFolderSelDtlDataDictionary.dd

Open SysFile       
Open FolderSelHeaA
Open FolderSelHea

Set_Attribute DF_FILE_ALIAS  of FolderSelHeaA.File_Number to DF_FILE_IS_ALIAS

Class cFolderSelHeaDataDictionary is a DataDictionary
    
    Procedure Construct_Object
        Forward Send Construct_Object
        Set Main_File to FolderSelHea.File_Number

        Set Add_Client_File to FolderSelDtl.File_Number

        Set Add_System_File to SysFile.File_Number DD_Lock_On_All

        Set Foreign_Field_Option DD_KEYFIELD DD_NOPUT to True
        Set Foreign_Field_Option DD_KEYFIELD DD_FINDREQ to True
        Set Foreign_Field_Option DD_INDEXFIELD DD_NOPUT to True
        Set Foreign_Field_Option DD_DEFAULT DD_DISPLAYONLY to True

        Set Field_Auto_Increment Field FolderSelHea.ID to File_Field SysFile.Next_FolderSel_ID

        Set Field_Label_Long Field FolderSelHea.UserName to "User Name"
        Set Field_Label_Short Field FolderSelHea.UserName to "User Name"
        Set Field_Class_Name Field FolderSelHea.UserName to "Form"

        Set Field_Label_Long Field FolderSelHea.WorkspaceHomeFolder to "Home Folder"
        Set Field_Label_Short Field FolderSelHea.WorkspaceHomeFolder to "Home Folder"

    End_Procedure

    Procedure Creating
        Get psUserName of ghoApplication to FolderSelHea.UserName
    End_Procedure  
    
    Function IsSavedFolders String sHomeFolder Returns Boolean
        Clear FolderSelHeaA
        Move sHomeFolder to FolderSelHeaA.WorkspaceHomeFolder
        Get  psUsername of ghoApplication to FolderSelHeaA.UserName
        Find eq FolderSelHeaA.WorkspaceHomeFolder
        If (Found = False) Begin
            Function_Return False
        End                               
        Function_Return (FolderSelHeaA.ChildID > 0)
    End_Function
    
    // ToDo: Redo and return FolderSelDtlA records instead.
    Function FindSavedFolders String sHomeFolder Returns String[]
        String[] asSavedFolders
        Boolean bFound
        Integer iHeaID
        
        Clear FolderSelHeaA
        Move sHomeFolder to FolderSelHeaA.WorkspaceHomeFolder
        Get  psUsername of ghoApplication to FolderSelHeaA.UserName
        Find eq FolderSelHeaA.WorkspaceHomeFolder
        If (Found = False) Begin
            Function_Return asSavedFolders
        End                               
        Move Found to bFound
        Move FolderSelHeaA.ID to iHeaID

        Constraint_Set (Self + 1) Clear
        Constrained_Clear eq FolderSelDtlA by Index.2
        Constrain FolderSelDtlA.FolderSelHeaID eq iHeaID
        Constrained_Find First FolderSelDtlA by Index.2
        While (Found)   
            Move (Trim(FolderSelDtlA.FolderName)) to asSavedFolders[SizeOfArray(asSavedFolders)]
            Constrained_Find Next
        Loop
        
        Function_Return asSavedFolders
    End_Function   
    
    // Returns True if all saves went OK.
    Function SaveSelectedFolders String sHomeFolder String[] asSavedFolders Returns Boolean
        Boolean bErrSave bErr bResult
        Integer iSize iCount iMax              
        String sFolderName
        
        // Save state of system flag.
        Move Err to bErrSave
        Move (SizeOfArray(asSavedFolders)) to iSize
        Decrement iSize
        
        Begin_Transaction
            Reread
            // SaveHeaderRecord also finds the header reocrd:
            Get SaveHeaderRecord sHomeFolder to bErr
            If (bErr = True) Begin
                Send UserError "Error saving header folder record."
                Function_Return False
            End    
            Get RemoveDetailRecords FolderSelHeaA.ID to bErr
            
            If (bErr = False) Begin
                For iCount from 0 to iSize
                    Move asSavedFolders[iCount] to sFolderName
                    Get SaveDetailRecord FolderSelHeaA.ID sFolderName to bErr
                    If (bErr = True) Begin
                        Move iSize to iCount // End the loop.
                    End
                Loop
            End
            If (bErr = False) ;
                Unlock
            Else Begin
                Send UserError "Error saving folder selections."
                Abort_Transaction
            End
        End_Transaction 

        Move Err to bResult
        Move bErrSave to Err
        Function_Return (bResult = False)
    End_Function  

    // Delets all existing FolderSelDtlA records for the passed FolderSelHeaA.ID
    Function RemoveDetailRecords Integer iHeaID Returns Boolean
        Boolean bErrSave bErr bResult

        Move Err to bErrSave
        Reread FolderSelHeaA
            Move 0 to FolderSelHeaA.ChildID
            SaveRecord FolderSelHeaA
        Unlock
        
        Constraint_Set Self Clear
        Constrained_Clear eq FolderSelDtlA by Index.2
        Constrain FolderSelDtlA.FolderSelHeaID eq iHeaID
        Constrained_Find First FolderSelDtlA by Index.2
        While (Found)   
            Lock
                Delete FolderSelDtlA
            Unlock
            Constrained_Find Next
        Loop
        
        Move (not(Err)) to bResult
        Move bErrSave to Err
        Function_Return (bResult = False)
    End_Function
    
    // Return False if save went all right.
    Function SaveHeaderRecord String sHomeFolder Returns Boolean
        Boolean bFound bErr bResult
        String sUserName
        
        // Preserve the status of the Err system flag:
        Move Err to bErr
        Move False to Err

        Get psUsername of ghoApplication to sUserName
        Clear FolderSelHeaA
        Move sUserName   to FolderSelHeaA.UserName
        Move sHomeFolder to FolderSelHeaA.WorkspaceHomeFolder
        Find eq FolderSelHeaA.WorkspaceHomeFolder 
        // If we found a record, there is nothing more to do here:
        If (Found = True) Begin
            Function_Return False
        End                     
        
        Lock
            Increment SysFile.Next_FolderSel_ID
            SaveRecord SysFile
            Clear FolderSelHeaA
            Move SysFile.Next_FolderSel_ID to FolderSelHeaA.ID
            Move sUserName   to FolderSelHeaA.UserName
            Move sHomeFolder to FolderSelHeaA.WorkspaceHomeFolder
            SaveRecord FolderSelHeaA
        Unlock       
        
        Function_Return (FolderSelHeaA.ID = 0)
    End_Function

    // Note: An active header record (FolderSelHea) must be present
    //       in the global record buffer prior calling this:
    // Returns True if there was an error saving records.
    Function SaveDetailRecord Integer iHeaderID String sFolderName Returns Boolean
        Boolean bFound bErr bResult
        String sUserName
        
        // Preserve the status of the Err system flag:
        Move Err to bErr
        Move False to Err
        Get psUsername of ghoApplication to sUserName
        Clear FolderSelDtl
        Move iHeaderID   to FolderSelDtlA.FolderSelHeaID
        Move sFolderName to FolderSelDtlA.FolderName
        Find eq FolderSelDtlA.FolderName
        Move (Found) to bFound
        
        // It these are true we need to save, else do nothing.
        If (bFound = False or Trim(sFolderName) <> Trim(FolderSelDtlA.FolderName)) Begin
            Reread //FolderSelHeaA FolderSelDtlA
                If (bFound = False) Begin
                    Increment FolderSelHeaA.ChildID
                    SaveRecord FolderSelHeaA
                    Clear FolderSelDtlA 
                    Move FolderSelHeaA.ChildID to FolderSelDtlA.ID
                    Move FolderSelHeaA.ID      to FolderSelDtlA.FolderSelHeaID
                End
                Move sFolderName to FolderSelDtlA.FolderName
                SaveRecord FolderSelDtlA
            Unlock       
        End
        
        Move Err to bResult
        Move bErr to Err
        Function_Return bResult
    End_Function

End_Class
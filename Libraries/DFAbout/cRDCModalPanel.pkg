//****************************************************************************
// $Module type: Class
// $Module name: cRDCModalPanel
// $Author     : Nils G. Svedmyr, RDC Tools International
// Created     : 2014-03-17 @ 12:33
//
// Description :
//
// $Rev History:
//    2014-03-17  Module header created
// The code is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
// without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
// This is free software; you can redistribute it and/or modify it under the terms of the
// GNU Lesser General Public License - see the "GNU Lesser General Public License.txt"
// in the help folder for more details.
//
//****************************************************************************
Use Windows.pkg
Use cCJCommandBarSystem.pkg   
Use cCJSkinFramework.pkg

Class cRDCModalPanel is a ModalPanel

    Procedure Construct_Object
        Forward Send Construct_Object

        Set Maximize_Icon to True
        Set Minimize_Icon to False
        Set Border_Style to Border_Thick
        Set Locate_Mode to Center_On_Parent

        Property String Private_Icon
        Property Handle phoDialogCommandbar
    End_Procedure

    Procedure Set Icon String sIcon
        Forward Set Icon to sIcon
        Set Private_Icon to sIcon
    End_Procedure

    Function Icon Returns String
        String sIcon
        Get Private_Icon to sIcon
        Function_Return sIcon
    End_Function

    Procedure Page Integer iPageObject
        String sIcon
        Integer hWnd
        
        Forward Send Page iPageObject
        Get Private_Icon to sIcon
        If (sIcon <> "") Begin
            Set Icon to sIcon
        End

        Get Window_Handle to hWnd

        If ((ghoSkinFramework <> 0) and (hWnd <> 0)) Begin
            Send ComRemoveWindow to ghoSkinFramework hWnd
            Send ComApplyWindow  to ghoSkinFramework hWnd
        End
    End_Procedure

    // Put a status bar at the bottom of the panel, which makes
    // status_help work and puts a gripper in the lower right corner.
    Procedure End_Construct_Object
        Integer iStyle iSize iOffset

        Forward Send End_Construct_Object

        Get Border_Style to iStyle
        Move 8 to iOffset
        If (iStyle = Border_Thick) Begin
            Object oDialogCommandbar is a cCJCommandBarSystem
                Object oStatusBar is a cCJStatusBar
                    Set phoDialogCommandbar to Self
                    Object oStatusPane1 is a cCJStatusBarPane
                        Set piId to sbpIDIdlePane
                        Set pbStyleStretch to True
                    End_Object
                End_Object
            End_Object
            Get Size to iSize
            Set Size to (Hi(iSize) + iOffset) (Low(iSize))
            Set piMinSize to (Hi(iSize) + iOffset) (Low(iSize))
        End

    End_Procedure

    Procedure Set StatusText String sText
        Set psText of (oStatusPane1(phoDialogCommandbar(Self))) to sText
    End_Procedure                          

    Procedure Popup
        Set Statusbar_Id to (phoDialogCommandbar(Self))
        Forward Send Popup
    End_Procedure

End_Class

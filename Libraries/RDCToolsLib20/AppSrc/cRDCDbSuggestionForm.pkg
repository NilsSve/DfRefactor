Use dbSuggestionForm.pkg

Struct tdSuggestionExtra
    Integer iFile
    Integer iField
    String  sExpression
End_Struct

Class cRDCDbSuggestionForm is a DbSuggestionForm

    Procedure Construct_Object
        Forward Send Construct_Object

        Set Label_Justification_mode to JMode_Right
        Set Label_Col_Offset to 2
        Set Label_Row_Offset to 0

        Property tdSuggestionExtra[] ptaExtras

        On_Key Key_Ctrl+Key_S Send Request_Save
        On_Key kCancel Send None
        On_Key Key_F3  Send None
    End_Procedure

    Procedure End_Construct_Object
        Forward Send End_Construct_Object
    End_Procedure

    Procedure Add_FileField Integer iFile Integer iField
        Integer iSize
        tdSuggestionExtra[] taExtras
        Get ptaExtras to taExtras
        Move (SizeOfArray(taExtras)) to iSize
        Move iFile to taExtras[iSize].iFile
        Move iField to taExtras[iSize].iField
        Set ptaExtras to taExtras
    End_Procedure

    Procedure Add_Expression String sExpression
        Integer iSize
        tdSuggestionExtra[] taExtras
        Get ptaExtras to taExtras
        Move (SizeOfArray(taExtras)) to iSize
        Move sExpression to taExtras[iSize].sExpression
        Set ptaExtras to taExtras
    End_Procedure

    Procedure ShowSuggestion tSuggestion SuggestionData String sSearch
        Handle hoServer
        Integer iCount iMax iMainFile iCol
        String sValue
        tdSuggestionExtra[] taExtras

        Get Server to hoServer
        Get Main_File of hoServer to iMainFile
        Get ptaExtras to taExtras
        Move (SizeOfArray(taExtras) - 1) to iMax
        If (iMax >= 0) Begin
           Send ReadByRowId to hoServer iMainFile (DeserializeRowID(SuggestionData.sRowId))
        End
        For iCount from 0 to iMax
            Move iCount to iCol
            If (taExtras[iCount].iFile > 0) Begin
                Get_Field_Value taExtras[iCount].iFile taExtras[iCount].iField to sValue
                Move (If(iCol > 0,'-','') * Trim(sValue)) to SuggestionData.aValues[iCol + 2]
                Increment iCol
            End
            If (taExtras[iCount].sExpression <> '') Begin
                Move (Eval(taExtras[iCount].sExpression)) to sValue
                Move (If(iCol > 0,'-','') * Trim(sValue)) to SuggestionData.aValues[iCol + 2]
            End
        Loop
        Forward Send ShowSuggestion SuggestionData sSearch
    End_Procedure

End_Class

//****************************************************************************
// $Module type: Package
// $Module name: cDbFormShadowTxt.pkg
//
// $Author     : Nils Svedmyr, RDC Tools International.
// Copyright (c) 2014 RDC Tools International
// E-mail      : support@rdctools.com
// Web-site    : http://www.rdctools.com
//
// Created     : 2014-12-08 @ 19:04 (Military date format - Year-Month-Day)
//
// The code is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
// without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
// This is free software; you can redistribute it and/or modify it under the terms of the
// GNU Lesser General Public License - see the "GNU Lesser General Public License.txt"
// in the help folder for more details.
//
//****************************************************************************
Use cRDCDbForm.pkg

Class cRDCDbFormShadowTxt is a cRDCDbForm

    Procedure Construct_Object
        Forward Send Construct_Object

        Property Integer piNormalTextColor clBlack
        Property Integer piShadowTextColor clLtGray
        Property String  psShadowText      ""

    End_Procedure

    Procedure End_Construct_Object
        String sTooltip sStatus_Help
        Forward Send End_Construct_Object

#IF (Required_FMAC_Version > 15)
        Get psToolTip   to sTooltip
        Get Status_Help to sStatus_Help
        If (sTooltip = "" and sStatus_Help <> "") Begin
            Set psToolTip to sStatus_Help
        End
#ENDIF
    End_Procedure

    Procedure OnChange
        String sValue
        Get Value to sValue
        If (sValue = psShadowText(Self)) Begin
            Set TextColor to (piShadowTextColor(Self))
        End
        Else Begin
            Set TextColor to (piNormalTextColor(Self))
        End
    End_Procedure

    Procedure Refresh Integer iMode
        Boolean bChanged

        Forward Send Refresh iMode
        // We don't want to set the psShadowText if a Default_Value has been assigned to the field.
        // If Changed_State = True, a default value must have been set by the refresh message while clearing and we do nothing.
        Get File_Field_Changed_State of (Server(Self)) (Data_File(Self)) (Data_Field(Self)) to bChanged
        If (bChanged = False and psShadowText(Self) <> "" and (iMode = Mode_Clear or iMode = Mode_Clear_All)) Begin
            Set Local_Value item 0 to (psShadowText(Self))
        End
    End_Procedure

    Procedure OnEnterObject Handle hoFrom
        String sValue

        Get Value to sValue
        If (sValue = psShadowText(Self)) Begin
            Set Local_Value item 0 to ""
        End
    End_Procedure

    Procedure OnExitObject
        String sValue

        Get Value to sValue
        If (sValue = "") Begin
            Set Local_Value item 0 to (psShadowText(Self))
        End
    End_Procedure

End_Class

// THDialog.pkg
// By Sergey V. Natarov, Wil van Antwerpen
//
Use Windows.pkg
Use Dfspnfrm.pkg
Use Colr_dlg.pkg

Define FindUp   for 0
Define FindDown for 1

Define ReplaceSelection for 0
Define ReplaceBuffer    for 1

// *** Find & Replace
Struct tFindReplaceOptions
    String sSearchText
    String sReplaceText // +R
    Boolean bWordMatch
    Boolean bMatchCase
    Boolean bRegExp
    Boolean bPreserveCase // +R
    Integer eFindOptions  // *R = eDirection -> eFindOptions
    Boolean bDocumentStart
    Boolean bStartFind
    Boolean bMarkAll
    Boolean bReplace      // +R
    Boolean bReplaceAll   // +R
    Boolean bCancel
#IFDEF IS$WIN64
    Integer iMissingAlignment1
#ENDIF
End_Struct

Class cFindCheckBox is a CheckBox

  Procedure doToggle
    Boolean bState

    Send DispatchHotkey  // no ringing bell on pressing your shortcut key please, that drives me nuts
    Send Activate
    Get Checked_State to bState
    Set Checked_State to (not(bState))
  End_Procedure
End_Class

Class cFindForm is a Form
  Procedure Activate returns integer
    Integer iRetVal
    Send DispatchHotkey  // no ringing bell on pressing your shortcut key please, that drives me nuts
    forward Get msg_activate to iRetVal
    Procedure_Return iRetVal
  End_Procedure
End_Class

Class cFindComboForm Is a ComboForm
  Procedure Construct_Object
    Forward Send Construct_Object
    Set Combo_Sort_State To False
    Property String[] pSearchStrings
  End_Procedure

  Procedure Activate returns integer
    Integer iRetVal
    Send DispatchHotkey  // no ringing bell on pressing your shortcut key please, that drives me nuts
    forward Get msg_activate to iRetVal
    Procedure_Return iRetVal
  End_Procedure

  Procedure Combo_Fill_List
    Integer  iItem
    Integer  iCount
    String[] SearchStrings

    Get pSearchStrings To SearchStrings
    Move (SizeOfArray(SearchStrings)) To iCount
    If (iCount>0) Begin
      Send Combo_Delete_Data
      For iItem From 0 To (iCount-1)
         Send Combo_Add_Item SearchStrings[iItem]
      Loop
      Set Value To SearchStrings[0]
    End
  End_Procedure

  Procedure AddSearchString String sValue
    Integer  iIndex
    String[] SearchStrings

    If (sValue<>"") Begin
      Get pSearchStrings To SearchStrings
      Move (SearchArray(sValue,SearchStrings,Desktop,(RefFunc(DFSTRICMP)))) To iIndex
      If (iIndex>-1) Begin
        Move (RemoveFromArray(SearchStrings,iIndex)) To SearchStrings // it will be moved up to the first position
      End
      Move (InsertInArray(SearchStrings,0,sValue)) To SearchStrings
      Set pSearchStrings To SearchStrings
    End
  End_Procedure

  Procedure Set SearchValue String sValue

    If (sValue<>"") Begin
      Send AddSearchString sValue
      Send Combo_Fill_List
    End
  End_Procedure

End_Class


// *** TH Parameters

Register_Procedure  Request_SaveAllEntries
Register_Procedure  NotifyChange
Register_Object     oStatusHelp
Register_Object     oOpenDialog

Class cNotifierCheckBox is a CheckBox
    Procedure Construct_Object
        Forward Send Construct_Object
        On_Key key_F2 Send Request_SaveAllEntries
        On_Key kCancel Send Close_Panel
    End_Procedure
    Procedure onChange
        Delegate Send NotifyChange
    End_Procedure
    Procedure Set Status_Help String sHelp
        String sToolTipText
        Forward Set Status_Help to sHelp
        Get psToolTip to sToolTipText
        If (sToolTipText = "") Begin
            Set psToolTip to sHelp
        End
    End_Procedure
End_Class

Class cNotifierForm is a Form
    Procedure Construct_Object
        Forward Send Construct_Object
        Set Label_Col_Offset to 2
        Set Label_Justification_Mode to JMode_Right
        On_Key key_F2 Send Request_SaveAllEntries
        On_Key kCancel Send Close_Panel
    End_Procedure
    Procedure onChange
        Delegate Send NotifyChange
    End_Procedure
    Procedure Set Status_Help String sHelp
        String sToolTipText
        Forward Set Status_Help to sHelp
        Get psToolTip to sToolTipText
        If (sToolTipText = "") Begin
            Set psToolTip to sHelp
        End
    End_Procedure
End_Class

Class cNotifierSpinForm is a Spinform
    Procedure Construct_Object
        Forward Send Construct_Object
        Set Label_Col_Offset to 2
        Set Label_Justification_Mode to JMode_Right
        On_Key key_F2 Send Request_SaveAllEntries
        On_Key kCancel Send Close_Panel
    End_Procedure
    Procedure onChange
        Delegate Send NotifyChange
    End_Procedure
    Procedure Set Status_Help String sHelp
        String sToolTipText
        Forward Set Status_Help to sHelp
        Get psToolTip to sToolTipText
        If (sToolTipText = "") Begin
            Set psToolTip to sHelp
        End
    End_Procedure
End_Class

Class cNotifierComboForm is a ComboForm
    Procedure Construct_Object
        Forward Send Construct_Object
        Set Label_Col_Offset to 2
        Set Label_Justification_Mode to JMode_Right
        On_Key key_F2 Send Request_SaveAllEntries
        On_Key kCancel Send Close_Panel
    End_Procedure
    Procedure onChange
        Delegate Send NotifyChange
    End_Procedure
    Procedure Set Status_Help String sHelp
        String sToolTipText
        Forward Set Status_Help to sHelp
        Get psToolTip to sToolTipText
        If (sToolTipText = "") Begin
            Set psToolTip to sHelp
        End
    End_Procedure
End_Class

Class cColorPicker is a Container3D
    Procedure Construct_Object
        Forward Send Construct_Object
        Set Size to 10 90
        Set Location to 9 7
        Set Border_Style to Border_Normal
        Set Color to clWhite
    End_Procedure
    Procedure Mouse_Down Integer i1
        Handle  hoDlg
        Integer bOk iColor
        Get Color to iColor
        Object oColorDlg is a ColorDialog
            Set SelectedColor   to iColor
            Set Custom_Color 0  to iColor
            Get Object_Id       to hoDlg
        End_Object
        Get Show_Dialog of hoDlg to bOK
        If bOK Begin
            Get SelectedColor of hoDlg to iColor
            Set Color to iColor
            Delegate Send OnColorChange
        End
        Send Destroy to hoDlg
        Forward Send Mouse_Down i1
    End_Procedure
End_Class

Class cColorBox is a Container3d
    Procedure Construct_Object
        Forward Send Construct_Object
        Property Integer piColorerIndex
        Set Size to 22 105
        Set Border_Style to Border_Normal
        Set Color to clWhite
        Object oLabel is a TextBox
            Set Auto_Size_State to False
            Set Size to 15 106
            Set Location to 0 0
            Set Label to "Color"
            Set Justification_Mode to JMode_Center
            Set Transparent_State to True
        End_Object
        Object oColorPicker is a cColorPicker
        End_Object
    End_Procedure
    Procedure OnColorChange
    End_Procedure
    Procedure Set Label String sLabel
        Set Label of (oLabel(Self)) to sLabel
    End_Procedure
    Function Label Returns String
        Function_Return (Label(oLabel(Self)))
    End_Function
    Procedure Set SlectedColor Integer iRGBColor
        If (iRGBColor=-1) Begin
            Move clWhite to iRGBColor
        End
        Set Color of (oColorPicker(Self)) to iRGBColor
    End_Procedure
    Function SelectedColor Returns Integer
        Function_Return (Color(oColorPicker(Self)))
    End_Function
End_Class

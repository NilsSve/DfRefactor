Use Windows.pkg
Use MessageQueueHelpers.pkg
Use THDialog.pkg

Object oGotoDialog is a ModalPanel
    Set Size to 50 138
    Set Label to "Go To Line"
    Set piMinSize to 50 138
    Set Location to 44 47
    Set piMaxSize to 50 238
    Set peAnchors to anAll
    Set Border_Style to Border_Thick
    //Set Locate_Mode to POPUP_LOCATE -- locate mode isn't smart enough
    Property Boolean pbCancel


    Object oLineFm is a cFindForm
        Set Size to 13 30
        Set Location to 10 43
        Set Label to "&Line:"
        Set Label_Col_Offset to 2
        Set Label_Justification_Mode to JMode_Right
        Set Form_DataType To Mask_Numeric_Window
        Set Form_Mask To "Z*"
        Set peAnchors to anTopLeftRight
    End_Object

    Object oOKBtn is a Button
        Set Label to "&OK"
        Set Location to 10 80
        Set Default_State to True
        Set peAnchors to anTopRight

        Procedure OnClick
            Send Close_Panel
        End_Procedure
    End_Object

    Object oCancelBtn is a Button
        Set Label    to "Cancel"
        Set Location to 26 80
        Set peAnchors to anTopRight

        Procedure OnClick
            Set pbCancel to True
            Send Close_Panel
        End_Procedure

    End_Object

    On_Key Key_Alt+Key_L Send Activate  of oLineFm
    On_Key Key_Alt+Key_O Send KeyAction of oOKBtn
    On_Key kCancel       Send KeyAction of oCancelBtn

    Procedure RequestDetails Integer ByRef iLine
        Boolean bCancel
        Set pbCancel    to False
        Set Value       of oLineFm  to ""
        Send Popup
        Get pbCancel to bCancel
        If (not(bCancel)) Begin
            Get Value   of oLineFm  to iLine
            Decrement iLine
            If (iLine<0) Begin
                Move 0 to iLine
            End
        End
        Else Begin
            Move -1 to iLine
        End
    End_Procedure

#IFDEF TH_TRANSLATION
    Procedure Translate
        Set Label               to gILanguage[848]
        Set Label of oLineFm    to gILanguage[849]
        Set Label of oOKBtn     to gILanguage[850]
        Set Label of oCancelBtn to gILanguage[851]
    End_Procedure
#ENDIF

    Procedure Activating
        Forward Send Activating
#IFDEF TH_TRANSLATION
        Send Translate
#ENDIF
    End_Procedure

End_Object


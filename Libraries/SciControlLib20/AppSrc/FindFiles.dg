//TH-Header
//*****************************************************************************************
// Copyright (c) 2017 KURANT Project
// All rights reserved.
//
// $FileName    : FINDFILES.DG
// $ProjectName : The Hammer 2.0
// $Authors     : Sergey V. Natarov, Wil van Antwerpen, Michael Kurz, Bernhard Ponemayr
// $Created     : 01.25.2014  01:08
// $Type        : LGPL
//
// Contents: Find Text in Files
//
//*****************************************************************************************

Use mEditorMacros.pkg
Use dfClient.pkg
Use cComboForm.pkg
Use Windows.pkg

Activate_View Activate_oFindFiles For oFindFiles
Object oFindFiles Is a ToolPanel
    Property Integer piWasButton False
    Set pbSizeToClientArea  to False
    Set popup_state         to True
    Set scope_state         to True
    Set Border_Style        to Border_Dialog
    Set Label               to "Find in Files"
    Set Location            to 26 20
    Set Size                to 120 338
    Set Locate_Mode         to CENTER_ON_SCREEN

    // Hot Keys
    On_Key (key_alt+key_f)  Send KeyAction to oOk_bn
    On_Key (key_alt+key_c)  Send KeyAction to oCancel_bn
    On_Key (key_alt+key_n)  Send Activate to oFindText
    On_Key (key_alt+key_t)  Send Activate to oFileTypes
    On_Key (key_alt+key_o)  Send Activate to oPath
    On_Key (key_alt+Key_w)  Send ToggleAndActivate to oMatchWord
    On_Key (key_alt+Key_a)  Send ToggleAndActivate to oMatchCase
    On_Key (key_alt+Key_s)  Send ToggleAndActivate to oSearchSubFolders
    On_Key (key_alt+key_r)  Send ToggleAndActivate to oShowAllResults
    On_Key (key_alt+key_x)  Send ToggleAndActivate to oExcludeComments
    On_Key (key_alt+key_e)  Send ToggleAndActivate to oRegularExpression
    On_Key kCancel          Send Close_Panel

    Object oFindText is a cComboForm
        Set Label to "Fi&nd what:"
        Set Size to 13 195
        Set Location to 10 75
        Set Form_Border to 0
        Set Form_Margin to 100
        Set Combo_Sort_State to False
        Set Label_Col_Offset to 2
        Set Label_Justification_Mode to JMode_Right
        Procedure SaveValue
            String sVal
            Get value to sVal
            If ((Combo_Item_Matching(Self,sVal)) = -1) Begin
                Send Combo_Insert_Item 0 sVal
            End
        End_Procedure
    End_Object

    Object oFileTypes is a cComboForm
        Set Label to "In Files/file &types:"
        Set Size to 13 195
        Set Location to 26 75
        Set Form_Border to 0
        Set combo_sort_state to False
        Set form_margin item 0 to 100
        Set Label_Col_Offset to 2
        Set Label_Justification_Mode to JMode_Right
        
        Procedure LoadPredefinedValues
            String sTypes sPart sAllSource
            
            Send Combo_Delete_Data
            Get psFileTypes of ghoEditorProperties to sTypes
            Move ((Trim(sTypes)) + "|") to sTypes
            
            While sTypes Ne ""
                Move (Left(sTypes,(Pos("|",sTypes)))) to sPart
                Move (Replace(sPart,sTypes,"")) to sTypes
                Move (Left(sTypes,(Pos("|",sTypes)))) to sPart
                Move (Replace(sPart,sTypes,"")) to sTypes
                Move (Replace("|",sPart,"")) to sPart
                Send Combo_Add_Item sPart
                If (sPart <> "*.*") Begin
                    Move (sAllSource + sPart + ";") to sAllSource
                End
            End
            Move (Left(sAllSource,((Length(sAllSource))-1))) to sAllSource
            Send Combo_Insert_Item 0 sAllSource
            Set Value Item 0 to sAllSource
        End_Procedure
        
        Procedure SaveValue
            String sVal
            Get value to sVal
            If ((Combo_Item_Matching(Self,sVal)) = -1) Begin
                Send Combo_Insert_Item 0 sVal
            End
        End_Procedure
    End_Object 

    Object oPath is a cComboForm
        Set Label to "In f&older:"
        Set Size to 13 180
        Set Location to 42 75
        Set Form_Border to 0
        Set Form_Margin      to 100
        Set Combo_Sort_State to False
        Set Label_Col_Offset to 2
        Set Label_Justification_Mode to JMode_Right
        
        Procedure Combo_Add_Item_Ex String sPara0
            String sPart
            While (Pos(";",sPara0) <> 0)
                Move (Left(sPara0,(Pos(";",sPara0)))) to sPart
                Move (Replace(sPart,sPara0,"")) to sPara0
                Move (Replace(";",sPart,"")) to sPart
                If ((Combo_Item_Matching(Self,sPart)) = -1) Begin
                     Send Combo_Add_Item sPart
                End
            End
            If ((Combo_Item_Matching(Self,sPara0)) = -1) Begin
                Send combo_add_item sPara0
            End
        End_Procedure
        
        Procedure LoadPredefinedValues
            Send Combo_Delete_Data
            Send Combo_Add_Item (_T("Whole Workspace", 881))
            Send Combo_Add_Item_Ex (CurrentRuntimePkgPath(ghoWorkSpaceHandlerEx))
            Send Combo_Add_Item_Ex (CurrentAppSrcPath(ghoWorkSpaceHandlerEx))
            Send Combo_Add_Item_Ex (CurrentDDSrcPath(ghoWorkSpaceHandlerEx))
            Send Combo_Add_Item_Ex (CurrentProgramPath(ghoWorkspaceHandlerEx))
            Set Value to (_T("Whole Workspace", 881))
        End_Procedure
        
        Procedure SaveValue
            String sVal
            Get value to sVal
            If ((Combo_Item_Matching(Self,sVal)) = -1) Begin
                Send Combo_insert_item 1 sVal
            End
        End_Procedure
        
        Procedure SelectDirectory
            String sDir
            Send Activate to (Self)
            Get_Directory sDir
            Get SelectFolderWithInit (_T("Select Directory to search", 882)) sDir to sDir
            If (sDir <> "") Begin
                Set Value to sDir
            End
        End_Procedure
    End_Object

    Object oFolderSelectBN is a Button
        Set Size to 13 15
        Set Location to 42 255
        Set Label to "..."
        Set Focus_Mode to Pointer_Only
        Procedure OnClick
            Send SelectDirectory to oPath
        End_Procedure
    End_Object

    Object oMatchWord is a CheckBox
        Set Label to "Match &whole Word only"
        Set Size to 10 91
        Set Location to 59 75
        Procedure ToggleAndActivate
            Set Checked_State to (not(Checked_State(Self)))
            Send Activate to (Self)
        End_Procedure
    End_Object

    Object oMatchCase is a CheckBox
        Set Label to "Match c&ase"
        Set Size to 10 54
        Set Location to 72 75
        Procedure ToggleAndActivate
            Set Checked_State to (not(Checked_State(Self)))
            Send Activate to (Self)
        End_Procedure
    End_Object

    Object oRegularExpression is a CheckBox
        Set Label to "Regular &expression"
        Set Size to 10 77
        Set Location to 85 75
        Procedure ToggleAndActivate
            Set Checked_State to (not(Checked_State(Self)))
            Send Activate to (Self)
        End_Procedure
    End_Object

    Object oSearchSubFolders is a CheckBox
        Set Label to "Search &Subfolders"
        Set Size to 10 75
        Set Location to 59 183
        Procedure ToggleAndActivate
            Set Checked_State to (not(Checked_State(Self)))
            Send Activate to (Self)
        End_Procedure
    End_Object

    Object oShowAllResults is a CheckBox
        Set Label to "Show all &Results"
        Set Size to 10 69
        Set Location to 72 183
        Procedure ToggleAndActivate
            Set Checked_State to (not(Checked_State(Self)))
            Send Activate to (Self)
        End_Procedure
    End_Object

    Object oExcludeComments is a CheckBox
        Set Label to "E&xclude Comments"
        Set Size to 10 75
        Set Location to 85 183
        Procedure ToggleAndActivate
            Set Checked_State to (not(Checked_State(Self)))
            Send Activate to (Self)
        End_Procedure
    End_Object

    Object oOK_bn is a Button
        Set Size to 13 50
        Set Label to "&Find"
        Set Location to 10 277
        Set Default_State to True
        
        Procedure OnClick
            Boolean bSubFolders bMatchCase bMatchWord bShowAll bRegExp bExcludeComments
            String sFindText sFileTypes sPath sAppSrc sLastPath sProgram sWWs sTHPath
            Integer iLen iLastDeviderPos iFolder iFolders
            tTHWorkspace THWorkspace
            Get Value         of oFindText          to sFindText
            Get Value         of oFileTypes         to sFileTypes
            Get Value         of oPath              to sPath
            Get Checked_State of oSearchSubFolders  to bSubFolders
            Get Checked_State of oMatchCase         to bMatchCase
            Get Checked_State of oMatchWord         to bMatchWord
            Get Checked_State of oShowAllResults    to bShowAll
            Get Checked_State of oExcludeComments   to bExcludeComments
            Get Checked_State of oRegularExpression to bRegExp
            If ((Trim(sFindText)) = "") Begin
                Send Stop_Box (_T("No search Text", 890))
                Procedure_Return
            End
            If ((Trim(sFileTypes)) = "") Begin
                Move "*.*" to sFileTypes
            End
            If ((Trim(sPath)) = "") Begin
                Move "." to sPath
            End
            Else Begin
                Move (_T("Whole Workspace", 881)) to sWWs
                If (Trim(sPath)=sWWs) Begin
                    Move (CurrentAppSrcPath(ghoWorkSpaceHandlerEx)) to sAppSrc
                    If ((Pos(";",sAppSrc)) <> 0) Begin
                        Move (Left(sAppSrc,((Pos(";",sAppSrc))-1))) to sAppSrc  // only take the first path
                    End
                    Move (CurrentProgramPath(ghoWorkSpaceHandlerEx)) to sProgram
                    If ((Uppercase(Trim(sAppsrc))) = (Uppercase(Trim(sProgram)))) Begin // i asume not a all-on-one directory
                        Move sAppsrc to sPath
                    End
                    Else Begin
                        Move "" to sLastPath
                        Move (Length(sAppSrc)) to iLen
                        While (iLen > 0)
                            If ((Mid(sAppSrc,1,iLen)) = "\") Begin
                                Move iLen to iLastDeviderPos
                                Move 0 to iLen
                            End
                            Else Begin
                                Decrement iLen
                            End
                        End
                        If (iLastDeviderPos <> 0) Begin
                            Move (Left(sAppSrc,(iLastDeviderPos-1))) to sAppsrc
                        End
                        Move sAppSrc to sPath
                        Move 1 to bSubFolders
                    End
                    // Add TH Specific workspace folders to the search path
                    Get pTHWorkspace of ghoApplication to THWorkspace
                    Move (SizeOfArray(THWorkspace.saFolders)) to iFolders
                    For iFolder from 0 to (iFolders-1)
                        If (iFolder>0) Begin       // iFolder=0 == [current workspace] so skip that one
                            Move (RTrim(THWorkspace.saFolders[iFolder])) To sTHPath
                            If (Right(sTHPath,1)="\") Begin
                                Move (Left(sTHPath,Length(sTHPath)-1)) to sTHPath
                            End
                            Move (sPath+";"+sTHPath) To sPath
                        End
                    Loop
                End
            End
            Send SaveValues
            Send SaveSettings
            Send RequestStartFileFind to (oClientArea(oMain(Self))) sPath sFileTypes sFindText bSubFolders bMatchCase bMatchWord bShowAll bRegExp bExcludeComments
        End_Procedure
    End_Object

    Object oCancel_bn is a Button
        Set Size to 13 50
        Set Label to "&Cancel"
        Set Location to 26 277
        Procedure OnClick
            If (not(Enabled_State(oOk_bn(Self)))) Begin
                Set piWasButton to True
                Send RequestCancelFileFind to (oClientArea(oMain(Self)))
                Send Activate to oFindText
            End
            Else Begin
                Send Close_Panel
            End
        End_Procedure
    End_Object

    Procedure OnStartFind
        Set Enabled_State of oFindText          to False
        Set Enabled_State of oFileTypes         to False
        Set Enabled_State of oPath              to False
        Set Enabled_State of oOK_bn             to False
        Set Enabled_State of oMatchWord         to False
        Set Enabled_State of oMatchCase         to False
        Set Enabled_State of oSearchSubFolders  to False
        Set Enabled_State of oShowAllResults    to False
        Set Enabled_State of oExcludeComments   to False
        Set Enabled_State of oFolderSelectBN    to False
        Set Enabled_State of oRegularExpression to False
        Send Activate to oCancel_bn
    End_Procedure

    Procedure OnFinishFind
          Set Enabled_State of oFindText          to True
          Set Enabled_State of oFileTypes         to True
          Set Enabled_State of oPath              to True
          Set Enabled_State of oOK_bn             to True
          Set Enabled_State of oMatchWord         to True
          Set Enabled_State of oMatchCase         to True
          Set Enabled_State of oSearchSubFolders  to True
          Set Enabled_State of oShowAllResults    to True
          Set Enabled_State of oExcludeComments   to True
          Set Enabled_State of oRegularExpression to True
          Set Enabled_State of oFolderSelectBN    to True
          If (not(piWasButton(Self))) Send Close_Panel
          Set piWasButton to False
    End_Procedure

    Procedure SaveValues
        Broadcast Send SaveValue
    End_Procedure

    Procedure LoadComboValuesFromString Integer hoObj String sData
        String sPart sFirst
        Integer iFirst
        Move 1 to iFirst
        Send Combo_delete_data to hoObj
        While ((Pos("|",sData)) <> 0)
            Move (Left(sData,(Pos("|",sData)))) to sPart
            Move (Replace(sPart,sData,"")) to sData
            Move (Replace("|",sPart,"")) to sPart
            If (iFirst) Begin
                Move sPart to sFirst
                Move 0 to iFirst
            End
            Else If ((Trim(sPart)) <> "") Begin
                Send Combo_add_item to hoObj sPart
            End
        End
        If ((Trim(sFirst)) <> "") Begin
            Set value of hoObj to sFirst
        End
        Else Begin
            Set value of hoObj to ""
        End
    End_Procedure

    Function ComboValuesAsString Integer hoObj Returns String
        Integer iCou
        String sVal sRet
        Get Value of hoObj to sRet       // save the current value as first entry
        Move (sRet + "|") to sRet
        If ((Combo_Item_Count(hoObj)) = 0) Begin
            Procedure_Return sRet
        End
        For iCou from 0 to ((Combo_Item_Count(hoObj)) -1)
            Get Combo_Value of hoObj Item iCou to sVal
            Move (sRet + sVal + "|") to sRet
            If (iCou >= 30) Begin
                Move ((Combo_Item_Count(hoObj)) -1) to iCou    // i think 30 entries should be enough
            End
        Loop
        Function_Return sRet
    End_Function

    Procedure LoadSettings
        tTHWorkspace THWorkspace
        
        Set Value of oFindText to ""
        Send Combo_Delete_Data to oFindText
        Get pTHWorkspace of ghoApplication To THWorkspace
        Send LoadComboValuesFromString (oFindText(Self)) THWorkspace.sFFText
        //
        If (THWorkspace.sFFTypes="") Begin
            Send LoadPredefinedValues to oFileTypes
        End
        Else Begin
            Send LoadComboValuesFromString (oFileTypes(Self)) THWorkspace.sFFTypes
        End
        //
        If (THWorkspace.sFFPath="") Begin
            Send LoadPredefinedValues to oPath
        End
        Else Begin
            Send LoadComboValuesFromString (oPath(Self))  THWorkspace.sFFPath
        End
        //
        Set Checked_State of oMatchWord         to THWorkspace.iFFMatchWord
        Set Checked_State of oMatchCase         to THWorkspace.iFFMatchCase
        Set Checked_State of oSearchSubFolders  to THWorkspace.iFFSubFolders
        Set Checked_State of oShowAllResults    to THWorkspace.iFFShowAll
        Set Checked_State of oExcludeComments   to THWorkspace.iFFExcludeComments
        Set Checked_State of oRegularExpression to THWorkspace.iFFRegExp
    End_Procedure

    Procedure SaveSettings
        tTHWorkspace THWorkspace
        Get pTHWorkspace of ghoApplication To THWorkspace
        Get ComboValuesAsString    (oFindText(Self))            to THWorkspace.sFFText
        Get ComboValuesAsString    (oFileTypes(Self))           to THWorkspace.sFFTypes
        Get ComboValuesAsString    (oPath(Self))                to THWorkspace.sFFPath
        Get Checked_State       of oMatchWord                   to THWorkspace.iFFMatchWord
        Get Checked_State       of oMatchcase                   to THWorkspace.iFFMatchCase
        Get Checked_State       of oSearchSubfolders            to THWorkspace.iFFSubFolders
        Get Checked_State       of oShowAllResults              to THWorkspace.iFFShowAll
        Get Checked_State       of oExcludeComments             to THWorkspace.iFFExcludeComments
        Get Checked_State       of oRegularExpression           to THWorkspace.iFFRegExp
        Set pTHWorkspace of ghoApplication To THWorkspace

        Send THDoWriteWorkspace
    End_Procedure

    Procedure Close_Panel
      Send SaveSettings
      Forward Send Close_Panel
    End_Procedure

    #IFDEF TH_TRANSLATION
    Procedure Translate
        Set Label                       to gILanguage[877]
        Set Label of oFindText          to gILanguage[878]
        Set Label of oFileTypes         to gILanguage[879]
        Set Label of oPath              to gILanguage[880]
        Send LoadPredefinedValues       to oPath
        Set label of oFolderSelectBN    to gILanguage[883]
        Set Label of oMatchWord         to gILanguage[884]
        Set Label of oMatchCase         to gILanguage[885]
        Set Label of oRegularExpression to gILanguage[886]
        Set Label of oSearchSubFolders  to gILanguage[887]
        Set Label of oShowAllResults    to gILanguage[888]
        Set Label of oOK_bn             to gILanguage[889]
        Set Label of oCancel_bn         to gILanguage[891]
    End_Procedure
    #ENDIF

    Procedure Activating
        Forward Send Activating
        #IFDEF TH_TRANSLATION
        Send Translate
        #ENDIF
    End_Procedure

End_Object

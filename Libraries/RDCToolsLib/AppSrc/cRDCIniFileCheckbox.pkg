//****************************************************************************
// $Module type: Class
// $Module name: cRDCIniFileCheckbox.pkg
// $Author     : Nils G. Svedmyr, RDC Tools International, <support@rdctools.com>
// Created     : 2010-02-20 @ 12:46
//
// Description : Form class that reads/writes to an .ini file. An ini file object
//               needs to be declared at the .src files top level.
//
//               To read values from .ini file to a cIniFileForm object, simply
//               send ReadIniValue to the object - or to write the value back
//               to the .ini file; WriteIniValue.
//
//               The class automatically reads from the .ini file when the object
//               is created.
//
//               To write all cIniFileForm objects in a container (dbView) send;
//                  Broadcast recursive send WriteIniValue of (some_container_name(self))
//               Typically this is done in a Save button's OnClick event.
//
// $Rev History:
//    2010-02-20  Module header created
//****************************************************************************
Use cRDCSlideButton.pkg
Use cRDCProjectIniFile.pkg
Use cRDCCheckbox.pkg

#IFNDEF CS_IFCBTrue
Define CS_IFCBTrue  for "Yes"
#ENDIF
#IFNDEF CS_IFCBFalse
Define CS_IFCBFalse for "No"
#ENDIF

Class cRDCIniFileCheckbox is a CheckBox //cRDCCheckBox

    Procedure Construct_Object
        Forward Send Construct_Object

        Property Handle phoIniFileObject ghoProjectIniFile // Global handle defined in ProjectInifile.pkg
        Property String psSection        (psSectionName(ghoProjectIniFile))
        Property String psKey            ""
        Property String psDefault        ""
        Property Boolean pbReadState False

    End_Procedure

    Procedure End_Construct_Object
        Forward Send End_Construct_Object
        Send ReadIniValue
    End_Procedure

    Procedure ReadIniValue
        Handle ho
        String sSection sKey sDefault sValue
        Boolean bState

        Set pbReadState to True
        Get phoIniFileObject to ho
        Get psSection        to sSection
        Get psKey            to sKey
        Get psDefault        to sDefault

        Get IniFileValue of ho sSection sKey sDefault to sValue
        If (sValue = CS_IFCBTrue) Begin
            Move True to bState
        End
        Else If (sValue = CS_IFCBFalse) Begin
            Move False to bState
        End
        Set Checked_State to bState
        Set pbReadState to False
    End_Procedure

    Procedure WriteIniValue
        Handle ho
        String sSection sKey sValue
        Boolean bState

        Get phoIniFileObject to ho
        Get psSection        to sSection
        Get psKey            to sKey

        Get Checked_State    to bState
        If (bState = True) Begin
            Move CS_IFCBTrue to sValue
        End
        Else Begin
            Move CS_IFCBFalse to sValue
        End

        Set IniFileValue of ho sSection sKey to sValue
    End_Procedure

End_Class

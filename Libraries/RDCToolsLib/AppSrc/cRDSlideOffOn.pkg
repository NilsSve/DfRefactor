//****************************************************************************
// $Module type: Class
// $Module name: cRDCSlideOffOnx.pkg
// $Author     : Nils Svedmyr, RDC Tools International, <support@rdctools.com>
// Created     : 2019-10-20 @ 12:46
//
// Description : Like a checkbox but slides between "Off" and "On" mode.
//               Also capable of reading/writing the value to an ini-file.
//
// $Rev History:
//    2019-10-20  Module header created
//****************************************************************************
Use cCommandLinkButton.pkg
Use cRDCProjectIniFile.pkg

#IFNDEF CS_RDCOn
    Define CS_RDCOn  for "On"
#ENDIF
#IFNDEF CS_RDCOff
    Define CS_RDCOff for "Off"
#ENDIF

Class cRDCSliderOffOn is a cCommandLinkButton

    Procedure Construct_Object
        Forward Send Construct_Object

        Property String psOnValue  CS_RDCOn
        Property String psOffValue CS_RDCOff
        Property Boolean pbChecked False
        Property Boolean pbIniFileMode True

        Property Handle phoIniFileObject ghoProjectIniFile // Global handle defined in ProjectInifile.pkg
        Property String psSection        (psSectionName(ghoProjectIniFile))
        Property String psKey            ""
        Property String psDefault        ""
        Property Boolean pbReadState False

    End_Procedure

    Procedure End_Construct_Object
        Forward Send End_Construct_Object
    End_Procedure

    Procedure ReadIniValue
        Handle ho
        String sSection sKey sDefault sValue
        Boolean bState

        If (pbIniFileMode(Self) = False) Begin
            Procedure_Return
        End

        Set pbReadState to True
        Get phoIniFileObject to ho
        Get psSection        to sSection
        Get psKey            to sKey
        Get psDefault        to sDefault

        Get IniFileValue of ho sSection sKey sDefault to sValue
        If (sValue = CS_RDCOn) Begin
            Move True to bState
        End
        Else If (sValue = CS_RDCOff) Begin
            Move False to bState
        End
        Set Checked_State to bState
        Set pbReadState to False
    End_Procedure

    Procedure OnChange
        String sLabel sImage
        Boolean bOff

        Get Label to sLabel
        Move (sLabel = CS_RDCOff) to bOff
        Move (If(bOff, CS_RDCOn, CS_RDCOff)) to sLabel
        Move (If(bOff, "ToggleOn.ico", "ToggleOff.ico")) to sImage
        Set psImage to sImage
        Set Label to sLabel

    End_Procedure

    Procedure Checked_State Boolean bState
        Send OnChange
    End_Procedure

    Procedure Mouse_Up Integer iWindowNumber Integer iPosition
        Forward Send Mouse_Up iWindowNumber iPosition
        Send OnChange
    End_Procedure

    Procedure WriteIniValue
        Handle ho
        String sSection sKey sValue
        Boolean bState

        If (pbIniFileMode(Self) = False) Begin
            Procedure_Return
        End

        Get phoIniFileObject to ho
        Get psSection        to sSection
        Get psKey            to sKey

        Get Checked_State    to bState
        If (bState = True) Begin
            Move CS_IFCBTrue to sValue
        End
        Else Begin
            Move CS_IFCBFalse to sValue
        End

        Set IniFileValue of ho sSection sKey to sValue
    End_Procedure

End_Class

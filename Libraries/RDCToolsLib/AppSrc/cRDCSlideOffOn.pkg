//****************************************************************************
// $Module type: Class
// $Module name: cRDCSlideOffOnx.pkg
// $Author     : Nils Svedmyr, RDC Tools International, <support@rdctools.com>
// Created     : 2019-10-20 @ 12:46
//
// Description : Like a checkbox but slides between "Off" and "On" mode.
//               Also capable of reading/writing the value to an ini-file.
//
// $Rev History:
//    2019-10-20  Module header created
//****************************************************************************
Use cCommandLinkButton.pkg
Use cRDCProjectIniFile.pkg

#IFNDEF CS_IFCBTrue
    Define CS_IFCBTrue  for "Yes"
#ENDIF
#IFNDEF CS_IFCBFalse
    Define CS_IFCBFalse for "No"
#ENDIF
#IFNDEF CS_RDCOn
    Define CS_RDCOn  for "On"
#ENDIF
#IFNDEF CS_RDCOff
    Define CS_RDCOff for "Off"
#ENDIF
#IFNDEF CS_ToggleOffIco
    Define CS_ToggleOffIco for "ToggleOff.ico"
#ENDIF
#IFNDEF CS_ToggleOnIco
    Define CS_ToggleOnIco  for "ToggleOn.ico"
#ENDIF

Class cRDCSliderOffOn is a cCommandLinkButton

    Procedure Construct_Object
        Forward Send Construct_Object

        Property String psOnValue  CS_RDCOn
        Property String psOffValue CS_RDCOff
        Property Boolean pbIniFileMode True

        Property Handle phoIniFileObject ghoProjectIniFile // Global handle defined in ProjectInifile.pkg
        Property String psSection        (psSectionName(ghoProjectIniFile))
        Property String psKey            ""
        Property String psDefault        ""
        Property Boolean pbReadState False

        { Visibility=Private }
        Property Boolean private.Checked_State False

        Set Label to CS_RDCOff
        Set psImage to CS_ToggleOffIco
        Set piImageSize to 24

    End_Procedure

    Procedure End_Construct_Object
        Forward Send End_Construct_Object
        Send ReadIniValue
    End_Procedure

    Procedure Set Checked_State Boolean bState
        Set private.Checked_State to bState
        Send OnChange
    End_Procedure

    Procedure OnChange
        String sLabel sImage
        Boolean bState

        Get Checked_State to bState
        Move (If(bState, CS_RDCOn, CS_RDCOff)) to sLabel
        Move (If(bState, "ToggleOn.ico", "ToggleOff.ico")) to sImage
        Set psImage to sImage
        Set Label to sLabel
    End_Procedure

    Function Checked_State Returns Boolean
        Function_Return (private.Checked_State(Self))
    End_Function

    Procedure ReadIniValue
        Handle ho
        String sSection sKey sDefault sValue
        Boolean bState

        If (pbIniFileMode(Self) = False) Begin
            Procedure_Return
        End

        Set pbReadState to True
        Get phoIniFileObject to ho
        Get psSection        to sSection
        Get psKey            to sKey
        Get psDefault        to sDefault

        Get IniFileValue of ho sSection sKey sDefault to sValue
        If (sValue = CS_RDCOn or sValue = CS_IFCBTrue) Begin
            Move True to bState
        End
        Else If (sValue = CS_RDCOff or sValue = CS_IFCBFalse) Begin
            Move False to bState
        End
        Set Checked_State to bState
        Set pbReadState to False
    End_Procedure

    Procedure Mouse_Up Integer iWindowNumber Integer iPosition
        Boolean bState
        Forward Send Mouse_Up iWindowNumber iPosition
        Get private.Checked_State to bState
        Set private.Checked_State to (not(bState))
        Send OnChange
    End_Procedure

    Procedure WriteIniValue
        Handle ho
        String sSection sKey sValue
        Boolean bState

        If (pbIniFileMode(Self) = False) Begin
            Procedure_Return
        End

        Get phoIniFileObject to ho
        Get psSection        to sSection
        Get psKey            to sKey

        Get Checked_State    to bState
        If (bState = True) Begin
            Move CS_IFCBTrue to sValue
        End
        Else Begin
            Move CS_IFCBFalse to sValue
        End

        Set IniFileValue of ho sSection sKey to sValue
    End_Procedure

End_Class

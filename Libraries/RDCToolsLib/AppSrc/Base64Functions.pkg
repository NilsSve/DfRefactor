//> This package enables base64 encoding and decoding of strings:
//>
//>     Get EncodeString of ghoBase64Functions sValue to sValue
//>     Get DecodeString of ghoBase64Functions sValue to sValue
//>
//> Remember to set the "max argument size" if dealing with very large
//> strings.
//>
//> For 17.1 and later the functionality of this package is based on two functions (Base64Encode and
//> Base64Decode) that are built into the runtime.
//>
//> But for versions up to and including 17.0 it's based on the vdfBase64.dll file that was obtained
//> from DAE that have kindly allowed me to distribute it to other DF developers. Programs including
//> this package will give off an error on start-up if vdfBase64.dll is not found in the /bin folder
//> if the VDF runtime. You will find the DLL in the /programs folder of the StureApsPublicLib workspace

Use VdfBase.pkg

// Functions in vdfBase64.dll
External_Function xxBase64Encode "vdfbase64Encode" vdfBase64.dll Pointer pBuffer Returns Integer
External_Function xxBase64Decode "vdfbase64Decode" vdfBase64.dll Pointer pBuffer Returns Integer
External_Function xxBase64Free   "vdfbase64Free"   vdfBase64.dll Pointer pBuffer Returns Integer

// Structure
Struct txxfBase64Buffer
    Pointer pData
    Integer iLength
End_Struct

Global_Variable Integer ghoBase64Functions
Move 0 to ghoBase64Functions

Object _oBase64Functions is a cObject
    Move Self to ghoBase64Functions

    Function EncodeString String sValue Returns String
        Integer iGarbage
        String sReturn
        txxfBase64Buffer strBuffer
        Move (AddressOf(sValue)) to strBuffer.pData
        Move (Length(sValue)) to strBuffer.iLength
        If (strBuffer.iLength>0) Begin // Can't call with empty string
            Move (xxBase64Encode(AddressOf(strBuffer))) to iGarbage
            Move (Repeat(Character(0),strBuffer.iLength)) to sReturn
            Move (CopyMemory(AddressOf(sReturn),strBuffer.pData,strBuffer.iLength)) to iGarbage
            Move (xxBase64Free(AddressOf(strBuffer))) to iGarbage
        End
        Function_Return sReturn
    End_Function

    Function DecodeString String sValue Returns String
        Integer iGarbage
        String sReturn
        txxfBase64Buffer strBuffer
        Move (AddressOf(sValue)) to strBuffer.pData
        Move (Length(sValue)) to strBuffer.iLength
        If (strBuffer.iLength>0) Begin // Can't call with empty string
            Move (xxBase64Decode(AddressOf(strBuffer))) to iGarbage
            Move (Repeat(Character(0),strBuffer.iLength)) to sReturn
            Move (CopyMemory(AddressOf(sReturn),strBuffer.pData,strBuffer.iLength)) to iGarbage
            Move (xxBase64Free(AddressOf(strBuffer))) to iGarbage
        End
        Function_Return sReturn
    End_Function

End_Object

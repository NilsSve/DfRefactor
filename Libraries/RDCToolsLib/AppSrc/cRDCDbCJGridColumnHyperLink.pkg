// Written by Peter Bragg.
// Subclass for using hyperlinks in cDbCJGrid/cCJGrid column objects.
//
// Set pbHyperLink to True and it will call the Function "IsHyperLink" passing the Row Index.
// To be honest you don't really need this property at all - the function on its own would do.
//
// Anyway, code this function accordingly to return True or False If the row value for that column
// should be displayed as a hyperlink or not. (Within this Function you can call "Get RowValue"
// to Get the value for that column)
//
// All you then need to do is Add the following to your Grid Class:
//
//    Procedure OnComHyperlinkClick Variant llRow Variant llItem Integer llHyperlinkIndex
//        Handle  hoRow hoItem hoColumn
//        String  sLink
//        Integer iRow iColumn
//
//        Get phoReportRow to hoRow
//        Get phoReportRecordItem to hoItem
//
//        Set pvComObject of hoItem to llItem
//        Get ComIndex    of hoItem to iColumn
//        Set pvComObject of hoRow to llRow
//        Get ComIndex    of hoRow to iRow
//        Get ColumnObject iColumn to hoColumn
//        Get RowValue of hoColumn iRow to sLink
//
//        Send OnLinkClicked of hoColumn sLink iRow
//    End_Procedure


Use cDbCJGridColumn.pkg

Class cDbCJGridColumnHyperLink is a cDbCJGridColumn

    Procedure Construct_Object
        Forward Send Construct_Object

        {Category="Appearance"}
        Property Boolean pbHyperlink    False
    End_Procedure

    {Visibility=Public}
    Function IsHyperlink Integer iRow Returns Boolean
        Function_Return False
    End_Function

    {MethodType=Event}
    Procedure OnLinkClicked String sLink Integer iRow
    End_Procedure

    Procedure DrawCell Handle hoRow Handle hoItem Handle hoMetrics Integer iRow String sValue
        Boolean bIsHyperlink
        Variant vHyperLinks vHyperLink
        Handle  hoHyperLinks
        Integer iLinks

        Get pbHyperlink to bIsHyperlink
        If (bIsHyperlink) Begin
            Get IsHyperlink iRow to bIsHyperlink
        End

        Get ComHyperlinks of hoItem to vHyperLinks
        Get Create (RefClass(cCJReportHyperlinks)) to hoHyperLinks
        Set pvComObject of hoHyperLinks to vHyperLinks
        Get ComCount of hoHyperLinks to iLinks
        If (bIsHyperlink) Begin
            If (iLinks=0) Begin
                Get ComAddHyperlink of hoHyperLinks 0 (Length(sValue)) to vHyperLink
            End
        End
        Else If (iLinks>0) Begin
            Send ComRemoveAll of hoHyperLinks
        End
        Send Destroy of hoHyperLinks

        Forward Send DrawCell hoRow hoItem hoMetrics iRow sValue
    End_Procedure

    Procedure OnCreateColumn
        Variant vHyperLinkStyle
        Handle  hoPaintManager hoHyperLinkStyle

        Forward Send OnCreateColumn
        Get phoReportPaintManager to hoPaintManager
        Get ComHyperlinkStyle of hoPaintManager to vHyperLinkStyle
        Get Create (RefClass(cCJReportHyperlinkStyle)) to hoHyperLinkStyle
        Set pvComObject of hoHyperLinkStyle to vHyperLinkStyle
        Set ComTextDecoration of hoHyperLinkStyle to xtpReportTextDecorationUnderline
        Send Destroy of hoHyperLinkStyle
    End_Procedure

End_Class

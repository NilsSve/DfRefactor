Use Windows.pkg
Use Datadict.pkg

// Mixin class to auto-create a unique ID value - without the need to use a system file to
// autoincrement new ID's.
//
// Best is to include into your DataDictionanary subclass.
// Syntax:
//   Get AutoCreateNewID File_Field MyFile.MyField True/False to iRetval
//   Use it within Procedure Creating in a DD - after Forward Send Creating.
// Note:
//   A unique index for the field that is to be auto-incremented must exist,
//   and it must have been set as the main index for that field.
//   You may omit the third boolean parameter, but it is crucial that you include the
//   keyword "File_Field"!
// Sample:
//   Procedure Creating
//     Integer iID
//     Forward Send Creating
//     Get AutoCreateNewID File_Field Customer.Customer_No True to iID
//     Showln "The new ID is :" iID
//  End_Procedure
//
// Returns:
//   The new unique ID.
Class cRDCAutoCreateNewIDMixin is a Mixin
    Function AutoCreateNewID Integer iFile Integer iField Boolean bEntryUpdate Returns Integer
        Integer iID iIndex
        Boolean bEntryUpd

        If (num_arguments < 3) Begin
            Move False to bEntryUpd
        End
        Else Begin
            Move bEntryUpdate to bEntryUpd
        End

        Move 1 to iID
        Get_Attribute DF_FIELD_INDEX of iFile iField to iIndex
        Clear iFile
        Fill_Field iFile iField with DF_HIGH
        Vfind iFile iIndex LE
        If (Found) Begin
            Get_Field_Value iFile iField to iID
            Increment iID
        End
        Clear iFile
        If (bEntryUpd = True) Begin
            Send Request_Entry_Update iFile 3
        End
        Set_Field_Value iFile iField to iID
        Function_Return iID
    End_Function
End_Class

// This function is used when working with e.g. the global record buffer and _not_
// with datadictionaries - then use the above mixin class instead.
// The syntax for the AutoCreateNewID function is the same as in the
// above mixin class - a part from the function name.
// Example:
//    Get GlobalAutoCreateNewID File_Field MyFile.MyIDfield False to iRetval
// Note:
//    You may omit the third boolean parameter, but it is _crucial_ that you include the
//    keyword "File_Field"!
Function GlobalAutoCreateNewID Global Integer iFile Integer iField Boolean bEntryUpdate Returns Integer
    Handle ho
    Integer iRetval
    Boolean bEntryUpd

    If (num_arguments < 3) Begin
        Move False to bEntryUpd
    End
    Else Begin
        Move bEntryUpdate to bEntryUpd
    End

    Get Create (RefClass(cRDCAutoCreateNewIDMixin)) to ho
    If (ho > 0) Begin
        Get AutoCreateNewID of ho iFile iField bEntryUpd to iRetval
    End
    Send Destroy of ho

    Function_Return iRetval
End_Function

//Header
//*****************************************************************************************
// Copyright (c)  2023 RDC Tools International
// All rights reserved.
//
// $FileName    : cRDCRegKeyForm.pkg
// $ProjectName : DFRefactor
// $Authors     :
// $Created     : 2023-12-01  17:08 (Military data format)
//
// Contents: Form class for automated saving/retrieval to/from the registry.
//           Values are saved under the current program's "Preferences"
//           tag under HKEY_CURRENT_USER
//
//*****************************************************************************************
// RevisionStart
// RevisionEnd
//
Use Windows.pkg
Use cRegistry.pkg

#IFNDEF CS_Preferences
    Define CS_Preferences for "Preferences"
#ENDIF

Class cRDCRegKeyForm is a Form

    Procedure Construct_Object
        Forward Send Construct_Object
        
        Property String psRegKey
        // Default value for "ReadString" if no registry value exists.
        Property String psRegKeyDefaultVal

        Set Label_Justification_Mode to JMode_Right
        Set Label_Col_Offset to 2
    End_Procedure

    Procedure End_Construct_Object
        String sRegKey sVal sDefault
        
        Forward Send End_Construct_Object
        Get psRegKey to sRegKey
        If (sRegKey = "") Begin
            Procedure_Return
        End
        Get psRegKeyDefaultVal to sDefault
        Get ReadString of ghoApplication CS_Preferences sRegKey sDefault to sVal
        Set Value to sVal
    End_Procedure
    
    Procedure Request_Save
        String sVal sRegKey
        Get Value to sVal 
        Get psRegKey to sRegKey
        Send WriteString of ghoApplication CS_Preferences sRegKey sVal
    End_Procedure
    
End_Class

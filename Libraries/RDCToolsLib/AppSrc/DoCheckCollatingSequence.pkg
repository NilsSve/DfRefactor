Use AutoReindex.dg

// English:
Define CS_DbErrorDetected for "The database needs to be repaired/reindexed. This is probably caused by the usage of a different DF_Collate.cfg (collating sequence) file."
Define CS_StartRepairNow  for "OK?"
Define CS_ElseQuitProgram for "If you press No, the program will be closed."

// Swedish:
//Define CS_DbErrorDetected for "Ett databasfel har uppt„ckts och databasen beh”ver omsorteras/repareras."
//Define CS_StartRepairNow  for "Starta bearbetning av databasen nu?"
//Define CS_ElseQuitProgram for "Om du v„ljer Nej, kommer programmet att avslutas."

// Test to check that the 'correct' df_collate.cfg is used,
// else reindex all tables prior to starting the program.
//
// The message DoCheckCollatingSequence needs to be send from cApplication object's
// predefined DoOpenWorkspace procedure.
//    Pass a filenumber and fieldnumber (needs to be an ascii field).
//    Procedure DoOpenWorkspace String sWorkspace               // cApplication class procedure.
//        Forward Send DoOpenWorkspace sWorkspace
//        // Needs to be send right after the forward send...
//        Send DoCheckCollatingSequence TimeMast.File_Number 1  // Pass filenumber and an ASCII fieldnumber that you know always contains some data.
//    End_Procedure // DoOpenWorkspace
//
// You must use this package above the cApplication object's DoOpenWorkspace procedure.
// Syntax:
//   Use DoCheckCollatingSequence.pkg
//
Procedure DoCheckCollatingSequence Desktop Integer iFile Integer iField
    Integer iRetval iIndex iRecords
    Boolean bOpen
    String  sProduct

    Get_Attribute DF_FILE_OPENED of iFile to bOpen
    If (bOpen = False) Begin
        Open iFile                              // Pathing has been established but tables has not yet been opened.
    End
    Get_Attribute DF_FILE_RECORDS_USED of iFile to iRecords
    If (iRecords = 0) Begin
        Procedure_Return // Then nothing to do.
    End

    Get_Attribute DF_FIELD_INDEX of iFile iField to iIndex
    Move False to Found

    Clear iFile
    Fill_Field iFile iField with DF_HIGH

    Vfind iFile iIndex LE                       // This table must always contain at least one record.
    If (Found = True) Begin
        Vfind iFile iIndex EQ     // This will always fail if df_collate.cfg is a different one then what was used to enter records (in any table).
    End
    Close iFile

    If (Found = False) Begin
        Move MBR_No to iRetval
        Get psProduct of ghoApplication to sProduct
        Get YesNo_Box (CS_DbErrorDetected * "\n\n" + CS_StartRepairNow * CS_ElseQuitProgram) sProduct to iRetval
        If (iRetval = MBR_No) Begin
            Send Exit_Application
        End
        Send DoRepairFiles
    End
End_Procedure

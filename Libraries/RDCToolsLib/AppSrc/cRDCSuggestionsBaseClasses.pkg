Use Windows.pkg
Use cDbCJGridColumnSuggestion.pkg
Use cRDCDbSuggestionForm.pkg

// 2024-12-18 NOTE: This class doesn't seem to be used anywhere!
Class cShowSuggestions is a Mixin
    Procedure ShowSuggestions
        Integer isize ilen iLines ipoints iFIXED_PITCH ilen1 i
        String sTypeface sSearch
        tSuggestion[] aResults
        tWinTextMetric twtm

        //Forward Send ShowSuggestions
        Get pSearchResults to aResults
        Get Value to sSearch
        Send Delete_Data of ghoSuggestionList
        Move (SizeOfArray(aResults) -1) to iLines
        For i from 0 to iLines
            Send ShowSuggestion aResults[i] sSearch
            Get LineLength of ghoSuggestionList -1 to ilen1
            If (ilen1 > ilen) Begin
                Move ilen1 to ilen
            End
            If ((iLines - 1) <> i) Begin
                Send AppendTextLn of ghoSuggestionList ""
            End
        Loop
        Send AdjustSuggestionListSize iLines
        If (iLines) Begin
            Get GuiSize of ghoSuggestionList to isize
            Get psTypeFace of ghoSuggestionList to sTypeface
            Get FontPointHeight of ghoSuggestionList to ipoints
            Get TypeFaceWinTextMetric sTypeface ipoints to twtm
            Move (1 - (twtm.tmPitchAndFamily iand 1)) to iFIXED_PITCH
            If (iFIXED_PITCH) Begin
               Set GuiSize of ghoSuggestionList to (Hi(isize)) ((twtm.tmAveCharWidth * ilen + 20) max (Low(isize)))
            End
            Else Begin
               Set GuiSize of ghoSuggestionList to (Hi(isize)) (((twtm.tmAveCharWidth + 0.5) * ilen + 20) max (Low(isize)))
            End
        End
    End_Procedure

    // This is a copy of DF Function PointSizeToPhysicalFontSize, but returning all information of the typeface
    Function TypeFaceWinTextMetric WString wTypeFace Integer iPointSize Returns tWinTextMetric
        Integer iFontSize iLogPixelsY
        Number nFontSize
        Boolean bVoid bOK
        Handle hDC hFont hOldFont
        Pointer lpLogFont lptm
        tWinTextMetric tm
        tWinLogFont ALogFont

        // Convert Font point size to logical pixels....
        Move (GetDC(0)) to hDC
        Move (GetDeviceCaps(hDC, LOGPIXELSY)) to iLogPixelsY
        Move ((iPointSize * iLogPixelsY / 72.0) + 0.5) to nFontSize
        Move nFontSize to iFontSize    // nFontSize is rounded up, then we truncate it into iFontSize - this reduces rounding error due to integer truncation

        // Convert the TypeFace name from WString into a Short Array (WChar).
        Move (MemCopy(AddressOf(ALogFont.lfFaceName), AddressOf(wTypeFace), SizeOfWString(wTypeFace)*2+2)) to bOK

        // Convert Logical Pixels into physical pixels....
        Move (0-iFontSize) to ALogFont.lfHeight  // use -ve value to instruct system to scale font to device
        Move (AddressOf(ALogFont)) to lpLogFont

        Move (CreateFontIndirect(lpLogFont)) to hFont   // Here we create the Font
        Move (SelectObject(hDC, hFont)) to hOldFont     // Now we select it to the device context
        Move (AddressOf(tm)) to lptm
        Move (GetTextMetrics(hDC, lptm)) to bVoid       // Now we can get the font's text metrics

        Move (tm.tmHeight + tm.tmExternalLeading) to iFontSize   // Here is the font's physical size for this device

        Move (SelectObject(hDC, holdFont)) to hOldFont
        Move (DeleteObject(hFont)) to bVoid
        Move (ReleaseDC(0, hDC)) to hDC

        Function_Return tm
    End_Function
End_Class

Class cShowSuggestion is a Mixin
    Procedure Define_cShowSuggestion_Mixin
        Property String psTypeFace "Lucida Console"
    End_Procedure

    Procedure ShowSuggestion tSuggestion SuggestionData String sSearch
        Integer ilen ipos
        Send FillData (&SuggestionData) sSearch
        Forward Send ShowSuggestion SuggestionData sSearch
        Get FirstCharInLinePos of ghoSuggestionList -1 to ipos
        Get LineLength of ghoSuggestionList -1 to ilen
        Send SetSel of ghoSuggestionList ipos (ipos+ilen)
        Set psTypeFace of ghoSuggestionList to (psTypeFace(Self))
        Send End_of_Line of ghoSuggestionList
    End_Procedure

    // If you want to see columns, fill each field with the same length for each column
    Procedure FillData tSuggestion ByRef SuggestionData String sSearch
    End_Procedure
End_Class

Class cDbCJGridColumnSuggestionEditX is a cDbCJGridColumnSuggestionEdit
    Import_Class_Protocol cShowSuggestions
End_Class

Class cDbCJGridColumnSuggestionNew is a cDbCJGridColumnSuggestion
    Procedure Construct_Object
        Forward Send Construct_Object
        Send Define_cShowSuggestion_Mixin
        Set phcEditClass to (RefClass(cDbCJGridColumnSuggestionEditX))
    End_Procedure
    Import_Class_Protocol cShowSuggestion
End_Class

Class cRDCDbSuggestionFormNew is a DbSuggestionForm
    Procedure Construct_Object
        Forward Send Construct_Object
        Send Define_cShowSuggestion_Mixin
    End_Procedure
    Import_Class_Protocol cShowSuggestions
    Import_Class_Protocol cShowSuggestion
End_Class

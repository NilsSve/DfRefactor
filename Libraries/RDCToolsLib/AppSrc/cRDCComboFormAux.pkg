//****************************************************************************
// $Module type: Class
// $Module name: cComboFormAux
// $Author     : Nils G. Svedmyr, RDC Tools International, <mailto:support@rdctools.com>
// Web-site    : http://www.rdctools.com
// Created     : 2015-07-29 @ 19:04 (Military date format: YY-MM-DD)
//
// Description :
//
// $Rev History:
//    2015-07-29  Module header created
//****************************************************************************
Use Windows.pkg

Class cComboFormAux is a ComboForm

    Procedure Construct_Object
        Forward Send Construct_Object
        Property Integer[] piAuxValues

        // Property settings:
        Set Entry_State to False
        Set Combo_Sort_State to False  // Never sort the combo list or it will fail...
    End_Procedure

    // Private
    Procedure Set Aux_Value Integer iItem Integer iValue
        Integer[] iAuxValues
        Get piAuxValues to iAuxValues
        Move iValue to iAuxValues[iItem]
        Set piAuxValues to iAuxValues
    End_Procedure

    Function Aux_Value Integer iItem Returns Integer
        Integer[] iAuxValues
        Integer iRetval

        If (iItem < 0) Begin
            Function_Return iItem
        End

        Get piAuxValues to iAuxValues
        Move iAuxValues[iItem] to iRetval
        Function_Return iRetval
    End_Function

    // Augmented Combo_Add_Item:
    Procedure Combo_Add_Item String sValue Integer iAuxValue
        Integer iAux iItem
        Forward Send Combo_Add_Item sValue
        If (Num_Arguments > 1) Begin
            Move iAuxValue to iAux
        End
        Get Combo_Item_Count to iItem
        Set Aux_Value item (iItem - 1) to iAux
    End_Procedure

    // Returns the aux value of the currently selected value:
    Function Combo_Current_Aux_Value Returns Integer
        Integer iItem iRetval
        String sValue
        Get Value to sValue
        Get Combo_Item_Matching sValue to iItem
        Get Aux_Value iItem to iRetval
        Function_Return iRetval
    End_Function

    Function FindAuxValue Integer iAuxValue Returns Integer
        Integer[] iAuxValues
        Integer iRetval
        Get piAuxValues to iAuxValues
        Move (SearchArray(iAuxValue, iAuxValues)) to iRetval
        Function_Return iRetval
    End_Function

    // Augmented Combo_Delete_Data
    Procedure Combo_Delete_Data
        Integer[] iAuxValues
        Forward Send Combo_Delete_Data
        Set piAuxValues to iAuxValues
    End_Procedure

    // Augmented Value function.
    // We need to use Wincombo_x messages. Probably due to sync reasons.
    // When e.g. OnCloseUp is fired the data_value still holds the old value.
    Function Value Integer iItem Returns String
        String sValue
        Get WinCombo_Current_Item      to iItem
        Get WinCombo_Value item iItem  to sValue
        Function_Return sValue
    End_Function

End_Class

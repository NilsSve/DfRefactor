Use Windows.pkg

External_Function SelectFolderDialog 'SelectFolderDialog' DfUtils.dll ;
                                   Pointer lpwTitle ;
                                   Pointer lpwInitialFolderName ;
                                   Handle  hOwnerWindow ;
                                   Returns Pointer

// Provides an interface to the Windows folder selection dialog.
{ ClassLibrary=Windows }
Class cSelectFolderDialog is a cObject

    Procedure Construct_Object
        Forward Send Construct_Object
        { Visibility=Private }
        Property String  psTitle
        { Visibility=Private }
        Property String  psSelectedFolder
        { Visibility=Private }
        Property String  psInitialFolder
        { Visibility=Private }
        Property Boolean pbFolderIsSelected
        { Visibility=Private }
        Property Handle  phwParent        // needed to define the window to be modal
    End_Procedure
    
    // Opens the Windows Select folder dialog and returns the selected folder
    Function SelectFolder String sTitle String sInitialFolder Returns String
        String sSelectedFolder
        Boolean bFolderSelected
        
        Set psTitle         to sTitle
        Set psInitialFolder to sInitialFolder
        
        // Execute the Select Folder Dialog
        Get IsFolderSelected to bFolderSelected
        
        If bFolderSelected ;
            Get psSelectedFolder to sSelectedFolder
        
        Function_Return sSelectedFolder
    End_Function
    
    { Visibility=Private }
    Function IsFolderSelected Returns Boolean
        Handle  hParent
        Integer iResult
        String  szFolderName sTitle sInitialFolderName
        Pointer lpwFolderName lpwInitialFolderName lpwTitle
        Boolean bRetVal
        WString wFolderName wInitialFolderName wTitle
        
        // Initialize the FolderName string.
        Get psInitialFolder to sInitialFolderName
        Move (Trim(sInitialFolderName))  to wInitialFolderName
        Move (AddressOf(wInitialFolderName)) to lpwInitialFolderName
		
        // initialize the FolderName string.
        Get psInitialFolder        to szFolderName
        Move (Trim(szFolderName))  to szFolderName
        Move (szFolderName + Repeat(Character(0), 500 - Length(szFolderName))) to wFolderName    // pad the folder name up to 500.
        Move (AddressOf(wFolderName)) to lpwFolderName    // get address of this string

        // Set the Title        
        Get psTitle to sTitle
        Move (Trim(sTitle))  to sTitle
        If (sTitle = "") ;
            Move "Select a folder" to sTitle
        Move sTitle to wTitle
        Move (AddressOf(wTitle)) to lpwTitle
        
        // Set the parent (or actually owner window) to make dialog modal. Set to NULL if it should not be modal.
        Get phwParent to hParent
        
        Move (SelectFolderDialog(lpwTitle, lpwInitialFolderName, hParent)) to lpwFolderName
        Move (PointerToWString(lpwFolderName)) to szFolderName

        // Assign the selected folder name.
        Set psSelectedFolder to (CString(szFolderName))
        Set pbFolderIsSelected to (szFolderName <> "")
        
        Function_Return (szFolderName <> "")
    End_Function
	
End_Class

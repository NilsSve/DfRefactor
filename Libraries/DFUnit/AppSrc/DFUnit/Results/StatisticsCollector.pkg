Use VDFBase.pkg

Class cDFUnitStatisticsCollector_Mixin is a Mixin
    
    { Visibility=Private }
    Procedure Define_cDFUnitStatisticsCollector_Mixin
        { Visibility=Private }
        Property UBigInt pulAssertsSucceeded 0
        { Visibility=Private }
        Property UBigInt pulAssertsFailed 0
        { Visibility=Private }
        Property UBigInt pulErrors 0
        
        { Visibility=Private }
        Property UBigInt pulTotalTests 0
        { Visibility=Private }
        Property UBigInt pulTotalSucceededTests 0
    End_Procedure
    
    { Visibility=Private }
    Procedure ClearTestCounters
        Set pulAssertsSucceeded to 0
        Set pulAssertsFailed to 0
        Set pulErrors to 0
        
        Set pulTotalTests to 0
        Set pulTotalSucceededTests to 0
    End_Procedure
    
    { Visibility=Private }
    Procedure IncreaseTotalTests
        UBigInt ulTotalTests
        Get pulTotalTests to ulTotalTests
        Increment ulTotalTests
        Set pulTotalTests to ulTotalTests
    End_Procedure
    
    { Visibility=Private }
    Procedure IncreaseTotalSucceededTests
        UBigInt ulTotalSucceededTests
        Get pulTotalSucceededTests to ulTotalSucceededTests
        Increment ulTotalSucceededTests
        Set pulTotalSucceededTests to ulTotalSucceededTests
    End_Procedure
    
    { Visibility=Private }
    Function TotalTests Returns UBigInt
        Function_Return (pulTotalTests(Self))
    End_Function
    
    { Visibility=Private }
    Function TotalSucceededTests Returns UBigInt
        Function_Return (pulTotalSucceededTests(Self))
    End_Function
    
    { Visibility=Private }
    Function TotalFailedTests Returns UBigInt
        Function_Return (TotalTests(Self) - TotalSucceededTests(Self))
    End_Function
    
    { Visibility=Private }
    Function TotalFailedAssertions Returns UBigInt
        Function_Return (pulAssertsFailed(Self))
    End_Function
    
    { Visibility=Private }
    Function TotalSucceededAssertions Returns UBigInt
        Function_Return (pulAssertsSucceeded(Self))
    End_Function
    
    { Visibility=Private }
    Function TotalAssertions Returns UBigInt
        Function_Return (TotalFailedAssertions(Self) + TotalSucceededAssertions(Self))
    End_Function
    
    { Visibility=Private }
    Function TotalErrors Returns UBigInt
        Function_Return (pulErrors(Self))
    End_Function
    
    { Visibility=Private }
    Function TestsSucceeded Returns Boolean
        Function_Return (TotalErrors(Self) = 0 and TotalFailedAssertions(Self) = 0)
    End_Function
    
    { Visibility=Private }
    Function TestsFailed Returns Boolean
        Function_Return (TestsSucceeded(Self) = False)
    End_Function

End_Class
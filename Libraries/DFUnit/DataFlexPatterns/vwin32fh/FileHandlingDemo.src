// This code is part of «VDF GUIdance»
// Visit us @ http://www.vdf-guidance.com
// e-Mail us @ info@vdf-guidance.com
// VDF GUIdance is a mutual project of
// Frank Vandervelpen - Vandervelpen Systems and
// Wil van Antwerpen  - Antwise Solutions
// All software source code should be used «AS IS» without any warranty.

// Author: Wil van Antwerpen
//
// (c) 1999-2018, Antwise Solutions
// ** 11-11-2002 WvA Updated code to work with the latest version of the
//                   vWin32fh package.
// ** 06-06-2018 WvA Updated code to have no warnings on DF19.1

Use  dfPanel.pkg
Use  dfbase.pkg

Use vWin32fh.pkg

// This is a small demoprogram on how to use the API functions.
// It doesn't show all the functionality of the vWin32fh package, but you will
// get the basic idea.

register_object oShowIt

Object Main is a Panel
  Set Label To "Windows File-handling Demo"
  Set Size To 165 200
  Property String psSelectedFile    ""
  Property String psSelectedFolder  ""
  Property String psOperation       ""

  Object oHuppeldepup Is A modalpanel
    Set Size To 95 250
    Object oSelectFile Is A form
      Set Location To 10 70
      Set Size to 13 170
      Set Label To "Select file"
      Set Prompt_Button_Mode To PB_PromptOn
      Set form_Button_Bitmap Item 0  To "ABNew.bmp"

      Procedure Prompt
        String sValue
        Send DoSelectFile
        Get psSelectedFile to sValue
        Set Value Item 0 To sValue
      End_Procedure
    End_Object // oSelectFile

    Object oSelectFolder Is A form
      Set Location To 30 70
      Set Size to 13 170
      Set Label To "Select folder"
      Set Prompt_Button_Mode To PB_PromptOn
      Set form_Button_Bitmap Item 0  To "OpenFold.bmp"

      Procedure Prompt
        String sValue
        Send DoSelectFolder
        Get psSelectedFolder To sValue
        Set Value Item 0 To sValue
      End_Procedure
    End_Object // oSelectFolder

    Object oDone_bn Is A Button
      Set Location To 50 190
      Procedure Activating
        String sOperation
        Forward Send Activating
        Get psOperation To sOperation
        Set Label To sOperation
      End_Procedure

      Procedure OnClick
        Send DoFileOperation
        Send Close_panel
      End_Procedure
    End_Object // oDone_bn

    Object oCancel_bn Is A Button
      Set Location To 50 135
      Set Label To "Cancel"

      Procedure OnClick
        Send Close_panel
      End_Procedure
    End_Object // oCancel_bn

    Procedure DoFileOperation
      String  sSource
      String  sDestination
      String  sOperation
      Integer iRetVal
      Get psOperation To sOperation
      Get psSelectedFile To sSource
      Get psSelectedFolder To sDestination
      If (sOperation = "Copy") Begin
        Move (vCopyFile(sSource,sDestination)) To iRetVal
        If (iRetVal = 0) ;
          Send DoEditWriteln To oShowIt "File copied succesfully"
      End
      Else If (sOperation = "Move") Begin
        Move (vMoveFile(sSource,sDestination)) To iRetVal
        If (iRetVal = 0) ;
          Send DoEditWriteln To oShowIt "File moved succesfully"
      End
    End_Procedure // DoFileOperation

    Procedure DoSelectFile
      String sSelectedFile
      String sMsg
      Move (vSelect_File("Text files|*.txt|Batch files|*.bat|System files|*.sys|All files|*.*","Select a file","C:\")) To sSelectedFile
      If (sSelectedFile <> "") ;
        Move ("The file"*sSelectedFile*"is Selected.") To sMsg
      Else ;
        Move "No file is selected." To sMsg
      Set psSelectedFile To sSelectedFile
      Send DoEditWriteln To oShowIt sMsg
    End_Procedure // DoSelectFile

    Procedure DoSelectFolder
      String sSelectedFolder
      String sMsg
      Move (vSHBrowseForFolder("Select a folder")) To sSelectedFolder
      If (sSelectedFolder <> "") ;
        Move ("The folder"*sSelectedFolder*"is Selected.") To sMsg
      Else ;
        Move "No file is selected." To sMsg
      Set psSelectedFolder To sSelectedFolder
      Send DoEditWriteln To oShowIt sMsg
    End_Procedure // DoSelectFolder

    Procedure Activating
      String sOperation
      Forward Send Activating
      Get psOperation To sOperation
      Append sOperation "-file demonstration with Windows 32 API functions"
      Set Label To sOperation
    End_Procedure
  End_Object // oHuppeldepup

  Object oCopy_bn Is A Button
    Set Location To 10 10
    Set label To "Copy File"
    Procedure OnClick
      Set psOperation To "Copy"
      Send Popup To oHuppeldepup
    End_Procedure
  End_Object // oCopy_bn

  Object oMove_bn Is A Button
    Set Location To 10 65
    Set label To "Move File"
    Procedure OnClick
      Set psOperation To "Move"
      Send Popup To oHuppeldepup
    End_Procedure // OnClick
  End_Object // oMove_bn

  Object oRename_bn Is A Button
    Set Location To 25 10
    Set label To "Rename File"
    Procedure OnClick
      String sMsg
      Move "not implemented in demo, API functions are there" To sMsg
      Send DoEditWriteln To oShowIt sMsg
    End_Procedure // OnClick
  End_Object // oRename_bn

  Object oDelete_bn Is A Button
    Set Location To 25 65
    Set label To "Delete File"
    Procedure OnClick
      String sMsg
      Move "not implemented in demo, API functions are there" To sMsg
      Send DoEditWriteln To oShowIt sMsg
    End_Procedure // OnClick
  End_Object // oDelete_bn

  Object oDDE_bn Is A Button
    Set Location To 25 120
    Set label To "Dance"
    Procedure OnClick
      String sMsg
      Move "http:/" To sMsg
      Append sMsg "/www.vdf-guidance.com"
      Send vShellExecute "OPEN" sMsg "" ""
      Send DoEditWriteln To oShowIt sMsg
    End_Procedure // OnClick
  End_Object // oDDE_bn

  Object oShowit Is A Edit
    Set Location To 45 10
    Set Size To 70 175
    Set Enabled_State To False

    Procedure doEditWriteln String sText
      Send append_text (sText + (character(13)) + (character(10)) )
    End_Procedure
  End_Object // oShowit

End_Object

Send Popup To (Main(Self))
Start_UI
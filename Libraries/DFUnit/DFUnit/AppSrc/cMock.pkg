//This class puts the message reference in an array every time Procedure1 and
//Procedure2 is called.
//It is possible to query the first occurrence of the message reference.
Class cMock is a cObject
    
    {Visibility = Private}
    Procedure Construct_Object
        Forward Send Construct_Object
        
        {Visibility = Private}
        Property Handle[] pCalledProcedures
    End_Procedure
    
    {Visibility = Private}
    Procedure CalledProcedure Handle hProcedure
        Handle[] CalledProcedures
        Get pCalledProcedures to CalledProcedures
        Move hProcedure to CalledProcedures[SizeOfArray(CalledProcedures)]
        Set pCalledProcedures to CalledProcedures
    End_Procedure
    
    {Visibility = Private}
    Function FirstTimeProcedureHasBeenCalled Handle hProcedure Returns Integer
        Handle[] CalledProcedures
        Get pCalledProcedures to CalledProcedures
        Function_Return (SearchArray(hProcedure, CalledProcedures) + 1)
    End_Function
    
    {Visibility = Public}
    Procedure Procedure1
        Send CalledProcedure (RefProc(Procedure1))
    End_Procedure
    
    {Visibility = Public}
    Procedure Procedure2
        Send CalledProcedure (RefProc(Procedure2))
    End_Procedure
    
    {Visibility = Public}
    Function FirstTimeProcedure1HasBeenCalled Returns Integer
        Function_Return (FirstTimeProcedureHasBeenCalled(Self, RefProc(Procedure1)))
    End_Function
    
    {Visibility = Public}
    Function FirstTimeProcedure2HasBeenCalled Returns Integer
        Function_Return (FirstTimeProcedureHasBeenCalled(Self, RefProc(Procedure2)))
    End_Function

End_Class
Use cTestFixtureCatalog.pkg
Use cTestFixtureRunner.pkg
Use cTestCatalog.pkg
Use cObjectNameMixin.pkg
Use cAssertMixin.pkg
Use cErrorState.pkg
Use cTestFixtureResult.pkg

Class cTestFixture is a cObject
    Import_Class_Protocol cObjectNameMixin
    Import_Class_Protocol cAssertMixin
    
    Procedure Construct_Object
        Forward Send Construct_Object
        
        Send Define_cObjectNameMixin
        Send Define_cAssertMixin
        
        // DFRefactor property:
        // Each TstFixture object must set this to the _exact_ samt name as
        // the function it is supposed to test.
        // This is being used by the Export/Import logic of the TEST-BENCH program.
        Property String psFunctionName
        
        Property String psTestFixtureName
        Property Handle phTestCatalog
        Property Boolean pbAbortTestFixture False
        Property Boolean pbFailureOccured False
        Property Handle phCurrentlyRunningTest
        Property DateTime pStartTime
        Property DateTime pEndTime
        Property Integer piNumberOfTests
        
        Object oTestFixtureCatalog is a cTestFixtureCatalog
        End_Object
        Set phTestFixtureNeighborhood of oTestFixtureCatalog to Self
        
        Object oTestFixtureRunner is a cTestFixtureRunner
        End_Object
        Object oTestCatalog is a cTestCatalog
        End_Object
        Object oTestFixtureResult is a cTestFixtureResult
        End_Object
        Object oErrorState is a cErrorState
        End_Object
        Set phTestCatalog to (oTestCatalog(Self))
    End_Procedure
    
    Function TestFixtureId Returns Handle
        Function_Return Self
    End_Function
    
    Procedure ReportTests
        Handle hTestCatalog   
        String sFunctionName
        Get psFunctionName to sFunctionName
        If (sFunctionName = "") Begin
            Error DFERR_PROGRAM ("psFunctionName undefined for" * Object_Label(Self))
        End
        Get phTestCatalog to hTestCatalog
        If (hTestCatalog = 0) Begin
            Error DFERR_PROGRAM ("phTestCatalog undefined for" * Name(Self))
        End
        Else Begin
            Send InitTestCatalog to hTestCatalog
            Set piNumberOfTests to (piNumberOfTests(hTestCatalog))
        End
    End_Procedure
    
    Function TotalNumberOfTests Returns Integer
        Integer iTests
        Get NumberOfTests of oTestFixtureCatalog to iTests
        Move (piNumberOfTests(Self) + iTests) to iTests
        Function_Return iTests
    End_Function
    
    Procedure ReportTestFixture Handle hTestFixtureCatalog
        Send AddTestFixtureToCatalog to hTestFixtureCatalog Self
        Send ReportTests
    End_Procedure
    
    Function ObjectName Returns String
        String sTestFixtureName
        Get psTestFixtureName to sTestFixtureName
        If (sTestFixtureName <> "") Begin
            Function_Return sTestFixtureName
        End
        
        Get LastPartOfObjectName Self to sTestFixtureName
        Function_Return sTestFixtureName
    End_Function
    
    Procedure FailedAssert String sFailureMessage
        Handle hAssertAppliesTo
        Get phCurrentlyRunningTest to hAssertAppliesTo
        If (hAssertAppliesTo = 0) Begin
            Move Self to hAssertAppliesTo
        End
        Send DoFailedAssert to hAssertAppliesTo sFailureMessage
    End_Procedure
    
    Procedure RunChildTestFixtures
        Set phTestFixtureCatalog of oTestFixtureRunner to oTestFixtureCatalog
        Send RunTestFixtures to oTestFixtureRunner
        If (FailureOccured(oTestFixtureRunner(Self))) Begin
            Set pbFailureOccured to True
        End
    End_Procedure
    
    Procedure CatalogTestFixture
        Send CatalogTestFixtures to oTestFixtureCatalog
    End_Procedure
    
    Procedure AddTestProcedure Integer iMsg String sMsgName
        Handle hTest
        Get Create (RefClass(cTest)) to hTest
        Set piTestMsg of hTest to iMsg
        If (Left(sMsgName, 4) = "msg_") Begin
            Move (Replace("msg_", sMsgName, "")) to sMsgName
        End
        Set psTestName of hTest to sMsgName
        Send AddTestToCatalog to (phTestCatalog(Self)) hTest
    End_Procedure
    
    //Registers the "published" test methods
    Procedure RegisterInterface Integer iMsg String sMsgName String sParams String sComment
        Send AddTestProcedure iMsg sMsgName
    End_Procedure
    
    Procedure SetTestFixtureResult
        Handle hResult
        Move oTestFixtureResult to hResult
        Send Reset to hResult
        Set psName of hResult to (ObjectName(Self))
        If (pbAbortTestFixture(Self)) Begin
            Set psExecuted of hResult to "False"
        End
        Else Begin
            Set psExecuted of hResult to "True"
        End
        Set psResult of hResult to "Success"
        Set psSuccess of hResult to "True"
        Set pTime of hResult to (pEndTime(Self) - pStartTime(Self))
    End_Procedure
    
    Function TestFixtureFailed Returns Boolean
        Function_Return (pbFailureOccured(Self))
    End_Function
    
    Procedure InitTestFixture
        Send ResetAsserts
        Set pbFailureOccured to False
        Set pStartTime to (CurrentDateTime())
        Set pbAbortTestFixture to False
    End_Procedure
    
    Procedure FinishTestFixture
        Set pEndTime to (CurrentDateTime())
    End_Procedure
    
    Procedure RunTestFixture
        Send InitTestFixture
        Send BeforeSetupOneTime
        If (not(pbAbortTestFixture(Self))) Begin
            Send SetupOneTime
        End
        
        Send RunChildTestFixtures
        
        If (not(pbAbortTestFixture(Self))) Begin
            Send RunTests
        End
        Else Begin
            Error DFERR_PROGRAM "Test Fixture aborted"
        End
        
        If (not(pbAbortTestFixture(Self))) Begin
            Send TearDownOneTime
        End
        If (not(pbAbortTestFixture(Self))) Begin
            Send AfterTearDownOneTime
        End
        Send FinishTestFixture
        Send SetTestFixtureResult
    End_Procedure
    
    Procedure AbortTestFixture
        Set pbAbortTestFixture to True
    End_Procedure
    
    Procedure BeforeSetupOneTime
    End_Procedure
    
    Procedure BeforeSetup
        Set pbAbortTestFixture to False
    End_Procedure
    
    Function ParentFixtureId Returns Handle
        Send SaveErrorState to oErrorState
        Send Ignore_Error to Error_Object_Id DFERR_BAD_MESSAGE
        Handle hTestFixtureIdParent
        Get TestFixtureId of (Parent(Self)) to hTestFixtureIdParent
        Send Trap_Error to Error_Object_Id DFERR_BAD_MESSAGE
        Send RestoreErrorState to oErrorState
        Function_Return hTestFixtureIdParent
    End_Function
    
    Procedure SetupParentFixture
        Handle hTestFixtureIdParent
        Get ParentFixtureId to hTestFixtureIdParent
        If (hTestFixtureIdParent = Parent(Self)) Begin
            Send SetupParentFixture to hTestFixtureIdParent
            Send BeforeSetup to hTestFixtureIdParent
            Send Setup to hTestFixtureIdParent
        End
    End_Procedure
    
    Procedure SetupOneTime
    End_Procedure
    
    Procedure Setup
    End_Procedure
    
    Procedure RunTests
        Handle hTestCatalog hTest
        Get phTestCatalog to hTestCatalog
        Send IteratorReset to hTestCatalog
        While (IteratorMoveNext(hTestCatalog))
            Get CurrentTest of hTestCatalog to hTest
            Send InitTest to hTest
            
            Send BeforeSetup
            Send SetupParentFixture
            Send Setup
            
            Set phCurrentlyRunningTest to hTest
            Send RunTest to hTest
            Set phCurrentlyRunningTest to 0
            
            Send TearDown
            Send TearDownParentFixture
            Send AfterTearDown
            
            Send FinishTest to hTest
            Send SetTestResult to hTest
            If (TestFailed(hTest)) Begin
                Set pbFailureOccured to True
            End
        Loop
    End_Procedure
    
    Procedure TearDownOneTime
    End_Procedure
    
    Procedure AfterTearDownOneTime
    End_Procedure
    
    Procedure TearDown
    End_Procedure
    
    Procedure AfterTearDown
    End_Procedure
    
    Procedure TearDownParentFixture
        Send SaveErrorState to oErrorState
        Send Ignore_Error to Error_Object_Id DFERR_BAD_MESSAGE
        Handle hTestFixtureIdParent
        Get TestFixtureId of (Parent(Self)) to hTestFixtureIdParent
        Send Trap_Error to Error_Object_Id DFERR_BAD_MESSAGE
        Send RestoreErrorState to oErrorState
        If (hTestFixtureIdParent = Parent(Self)) Begin
            Send TearDown to hTestFixtureIdParent
            Send AfterTearDown to hTestFixtureIdParent
            Send TearDownParentFixture to hTestFixtureIdParent
        End
    End_Procedure
    
End_Class

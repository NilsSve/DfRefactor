Use cTestFixture.pkg
Use cTest.pkg

Object oTestRunCounter is a cObject
    Property Integer Number_of_times_test_has_been_run
    Property Integer Number_of_times_test_has_been_run_in_nested_fixture
    
    Procedure IncreaseCounter
        Set Number_of_times_test_has_been_run to (Number_of_times_test_has_been_run(Self) + 1)
    End_Procedure
    Procedure IncreaseCounterNested
        Set Number_of_times_test_has_been_run_in_nested_fixture to (Number_of_times_test_has_been_run_in_nested_fixture(Self) + 1)
    End_Procedure
    Procedure ResetCounters
        Set Number_of_times_test_has_been_run to 0
        Set Number_of_times_test_has_been_run_in_nested_fixture to 0
    End_Procedure
End_Object

Object All_tests_in_a_fixture_are_run_once is a cTestFixture
    {Published=True}
    Procedure With_no_nesting_of_fixtures
        Send IncreaseCounter to oTestRunCounter
        Send AssertIAreEqual 1 (Number_of_times_test_has_been_run(oTestRunCounter(Self)))
    End_Procedure
    
    Object With_nested_fixtures is a cTestFixture
        {Published=True}
        Procedure tests_are_still_run_once
            Send IncreaseCounterNested to oTestRunCounter
            Send AssertIAreEqual 1 (Number_of_times_test_has_been_run_in_nested_fixture(oTestRunCounter(Self)))
        End_Procedure
    End_Object
    
    Procedure TearDownOneTime
        Send ResetCounters to oTestRunCounter
    End_Procedure
    
    Object Test_Object_Makes_Sure_Test_Procedures_Not_In_Objects_Are_Run is a cTest
        Procedure Test
            Send AssertIAreEqual 1 (Number_of_times_test_has_been_run(oTestRunCounter(Self)))
            Send AssertIAreEqual 1 (Number_of_times_test_has_been_run_in_nested_fixture(oTestRunCounter(Self)))
        End_Procedure
    End_Object
End_Object

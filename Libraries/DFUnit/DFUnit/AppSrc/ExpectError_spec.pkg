Use DFUnit.pkg

Object ExpectError_spec is a cTestFixture
    Object Fail_if_expected_error_not_thrown is a cTestFixture
        Procedure Setup
            Send Ignore_Error to Error_Object_Id DFERR_PROGRAM
        End_Procedure
        
        Object Fails_if_expected_error_not_thrown is a cTest
            Procedure Test
                Send ExpectError DFERR_OPERATOR
            End_Procedure
        
            Procedure FinishTest
                Send Trap_Error to Error_Object_Id DFERR_PROGRAM
                Boolean bExpectedErrorOccured
                Move ((Err) and (LastErr = DFERR_PROGRAM)) to bExpectedErrorOccured
                If bExpectedErrorOccured Begin
                    //Expected error did not occur, so dferr_program was thrown
                    //This is correct behaviour, so reset Err indicator and failed asserts
                    Move False to Err
                    Move 0 to LastErr
                    Send ResetAsserts
                End
                Send Assert bExpectedErrorOccured "ExpectError does not work"
                Forward Send FinishTest
            End_Procedure
        End_Object
    End_Object
    
    Object Err_not_reset_inadvertently is a cTestFixture
        
        Object Err_not_reset_after_succesful_expect_error is a cTest
            Property Boolean pbErrWasSet
            Procedure InitTest
                If (Err) Set pbErrWasSet to True
                Else Set pbErrWasSet to False
                Move True to Err
                Forward Send InitTest
            End_Procedure
            
            Procedure Test
                Send ExpectError DFERR_OPERATOR
                Send Ignore_Error to Error_Object_Id DFERR_OPERATOR
                Error DFERR_OPERATOR
            End_Procedure
            
            Procedure FinishTest
                Forward Send FinishTest
                Boolean bErr
                Move Err to bErr
                Send Assert bErr
                Set pbSuccess to bErr
                If (pbErrWasSet(Self)) Move True to Err
                Else Move (not(pbSuccess(Self))) to Err
            End_Procedure
        End_Object
        
    End_Object
End_Object

Use cTestCatalog.pkg
Use cObjectNameMixin.pkg
Use cAssertMixin.pkg
Use cErrorState.pkg
Use cTestFixtureResult.pkg

Class cTest is a cObject
    Import_Class_Protocol cObjectNameMixin
    Import_Class_Protocol cAssertMixin
    
    Procedure Construct_Object
        Forward Send Construct_Object
        
        Send Define_cObjectNameMixin
        Send Define_cAssertMixin
        
        Property String psTestName
        Property Integer piTestMsg
        Property Boolean pbSuccess
        Property Integer piExpectedError
        Property Handle phErrorState
        Property DateTime pStartTime
        Property DateTime pEndTime
        
        Object oRememberErrorState is a cErrorState
            Set phErrorState to Self
        End_Object
        Object oTestResult is a cTestResult
        End_Object
    End_Procedure
    
    Procedure ReportTest Handle hTestFixtureCatalog
        Send AddTestToCatalog to hTestFixtureCatalog Self
    End_Procedure
    
    Function ObjectName Returns String
        String sTestName
        Get psTestName to sTestName
        If (sTestName <> "") Function_Return sTestName
        
        Get LastPartOfObjectName Self to sTestName
        Function_Return sTestName
    End_Function
    
    Procedure ExpectError Integer iErrorNumber
        Set piExpectedError to iErrorNumber
        Send Ignore_Error to Error_Object_Id iErrorNumber
        Move 0 to LastErr
    End_Procedure
    
    Procedure CheckIfExpectedError
        Integer iExpectedError
        Get piExpectedError to iExpectedError
        Boolean bExpectedErrorOccured
        If (iExpectedError <> 0) Begin
            Move (LastErr = iExpectedError) to bExpectedErrorOccured
            Send Trap_Error to Error_Object_Id iExpectedError
            Send Assert bExpectedErrorOccured ("Expected error number" * String(iExpectedError) * "did not occur.")
            If bExpectedErrorOccured Begin
                Move False to Err
                Move 0 to LastErr
            End
            Else Begin
                Move True to Err
                Move DFERR_PROGRAM to LastErr
            End
        End
    End_Procedure
    
    Procedure UnexpectError
        If (piExpectedError(Self) <> 0) Begin
            Set piExpectedError to 0
        End
    End_Procedure
    
    Procedure SuppressErrorDialog
        Send SuppressErrorDialog to Error_Object_Id
    End_Procedure
    
    Procedure ResetErrorDialog
        Send ResetErrorDialog to Error_Object_Id
    End_Procedure
    
    Procedure SetTestResult
        Handle hResult
        Move oTestResult to hResult
        Set psName of hResult to (ObjectName(Self))
        Set psExecuted of hResult to "True"
        If (pbSuccess(Self)) Begin
            Set psResult of hResult to "Success"
            Set psSuccess of hResult to "True"
        End
        Else Begin
            Set psResult of hResult to "Failure"
            Set psSuccess of hResult to "False"
            String sErrorMessage
            Get psLastErrorMessage of Error_Object_Id to sErrorMessage
            Set psErrorMessage of oTestResult to sErrorMessage
        End
        Set pTime of hResult to (pEndTime(Self) - pStartTime(Self))
    End_Procedure
    
    Procedure InitTest
        Send ResetAsserts
        Send Reset to oTestResult
        Set pStartTime to (CurrentDateTime())
        Send SaveErrorState to (phErrorState(Self))
        Move False to Err
        Move 0 to LastErr
    End_Procedure
    
    Function TestFailed Returns Boolean
        Function_Return (pbAssertFailureOccured(Self))
    End_Function
    
    Procedure FinishTest
        If (TestFailed(Self)) Begin
            Send SaveErrorState to (phErrorState(Self))
            Set pbSuccess to False
        End
        Else begin
            Send RestoreErrorState to (phErrorState(Self))
            Set pbSuccess to True
        End
        Set pEndTime to (CurrentDateTime())
    End_Procedure
    
    Procedure RunTest
        Integer iTestMsg
        Get piTestMsg to iTestMsg
        If (iTestMsg = 0) Begin
            Send Test
        End
        Else Begin
            Send iTestMsg
        End
        Send CheckIfExpectedError
        Send UnexpectError
        String sErrorMessage
    End_Procedure
    
    Procedure Test
    End_Procedure
    
    Procedure Setup Handle hoTestFixture
        Send Setup to hoTestFixture
    End_Procedure
    
    Procedure TearDown Handle hoTestFixture
        Send TearDown to hoTestFixture
    End_Procedure
End_Class

Use Windows.pkg

// Used by the "Page" message below to show
// a rectangle around ComboForm and Checkbox objects.
#IFNDEF WM_CHANGEUISTATE
    Define WM_CHANGEUISTATE for |CI$0127
    Define WM_UPDATEUISTATE for |CI$0128
    Define WM_QUERYUISTATE  for |CI$0129
    Define UIS_SET          for 1
    Define UIS_CLEAR        for 2
    Define UIS_INITIALIZE   for 3
    Define UISF_HIDEFOCUS   for |CI$1
    Define UISF_HIDEACCEL   for |CI$2
    Define UISF_ACTIVE      for |CI$4
#ENDIF

// Note: Can't use cRDCCheckbox as then the OnChange event isn't called (Don't know why)
Class cSQLCheckBox is a CheckBox
    Procedure Construct_Object
        Forward Send Construct_Object
        // The piItem should be set to the object's corresponding tSQLConnection member (One based)
        Property Integer piItem
    End_Procedure

    Procedure DbTypeUpdate Integer iDbType String sDriverID
    End_Procedure

    Procedure ReadConnectionData String[] sConnectionData
        Integer iItem
        Get piItem to iItem
        Set Checked_State to (sConnectionData[iItem] = "1")
    End_Procedure

    Procedure WriteConnectionData String[] ByRef sConnectionData
        Boolean bState
        Integer iItem
        Get piItem to iItem
        Get Checked_State to bState
        Move bState to sConnectionData[iItem]
    End_Procedure

    Procedure Set psToolTip String sToolTip
        String sStatusHelp

        Get Status_Help to sStatusHelp
        If (sStatusHelp = "") Begin
            Set Status_Help to sToolTip
        End

        Forward Set psToolTip to sToolTip
    End_Procedure

    // Fix for a bug when using manifest files & running Vista and above.
    // The bug being that the focus rectangle doesn't show.
    Procedure Page Integer iPageObject
        Handle hWnd
        Integer iRet iState

        Forward Send Page iPageObject

        If (iPageObject) Begin
            Get Window_Handle to hWnd
            If (hWnd) Begin
                Move (SendMessage(hWnd, WM_QUERYUISTATE, 0, 0)) to iRet
                If (iRet iand UISF_HIDEFOCUS) Begin
                    Move (UIS_CLEAR + (UISF_HIDEFOCUS * 65536)) to iState
                    Move (SendMessage(hWnd, WM_UPDATEUISTATE, iState, 0)) to iRet
                End
            End
        End
    End_Procedure

End_Class

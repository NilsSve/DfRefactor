Use Windows.pkg
Use cRDCModalPanel.pkg
Use cRDCForm.pkg 
Use cRDCComboForm.pkg
Use cRDCSuggestionIniForm.pkg
Use cDbUpdateFunctionLibrary.pkg
Use cRDCHeaderGroup.pkg
Use cCommandLinkButton.pkg

Object oCreateDatabase_dg is a cRDCModalPanel
    Set Size to 151 312
    Set Label to "Create New Database"
    Set piMinSize to 53 211
    Set Location to 2 2
    Set Icon to "ActionCreateDatabase1.ico"
    
    Property String psDriverID                   
    Property String psCollation 
    Property String psDatabaseName 

//    Object oCollate_cf is a cRDCComboForm
//        Set Size to 13 216
//        Set Location to 42 63
//        Set Label to "Use Collation:"  
//        Set psToolTip to "Select/Enter the collation to use when creating the database. If none is specified the default SQL server collation above will be used. Note: Collations starting with 'SQL_' is obsolete and should not be used."
//        // This must be set to False; else the filling takes for ever.
//        Set pbAutoListWidth to False
//        
//        Procedure Combo_Fill_List
//            String[] asCollations   
//            Integer iCount iSize      
//            String sDriverID sCollation
//            
//            Get psDriverID to sDriverID      
//            
//            // The _SqlEnumerateDatabaseCollations function is *at current* for MSSQLDRV_ID only.
//            If (sDriverID = MSSQLDRV_ID) Begin
//                Send Cursor_Wait of Cursor_Control
//                Get _SqlEnumerateDatabaseCollations of ghoDbUpdateFunctionLibrary sDriverID to asCollations
//                Move (SizeOfArray(asCollations)) to iSize
//                Decrement iSize
//                For iCount from 0 to iSize
//                    Send Combo_Add_Item asCollations[iCount]
//                Loop
//                
//                Get psCollation of ghoDbUpdateFunctionLibrary to sCollation
//                Set Value to sCollation
//                Send Cursor_Ready of Cursor_Control
//            End
//        End_Procedure
//    
//        Procedure OnChange
//            String sValue
//            Get Value to sValue
//            Set psCollation to sValue
//        End_Procedure
//     
//    End_Object

    Object oInfo_tb is a TextBox
        Set Auto_Size_State to False
        Set Size to 8 50
        Set Location to 133 120
        Set TextColor to clGreen
        Set Justification_Mode to JMode_Right
        Set peAnchors to anBottomRight
    End_Object

    Object oMain_RDCHeaderGroup is a cRDCHeaderGroup
        Set Size to 108 293
        Set Location to 8 10
        Set psImage to "ActionCreateDatabase1.ico"
        Set Label to "Create Database"
        Set psNote to "Enter collation and database name"
        Set peAnchors to anTopBottom

        Object oDriver_fm is a cRDCForm
            Set Size to 13 100
            Set Location to 34 63
            Set Label to "Driver"
            Set Enabled_State to False  
        End_Object  
        
        Object oServerCurrentCollating_fm is a cRDCForm
            Set Size to 13 216
            Set Location to 49 63
            Set Label to "Default Collation"
            Set psToolTip to "The SQL server's default collation. It will be used when creating a new database, if nothing else is specified."
            Set Enabled_State to False  
            
            Procedure InitializeCollation 
                String sCollation
                
                Move False to Err
                Send Ignore_All of Error_Object_Id
                // Fetch the SQL server's default collation:
                // It will be used when creating a new database, if nothing else is specified.
                Get SqlDatabaseCollationQuery of ghoDbUpdateFunctionLibrary "master" False to sCollation
                Send Trap_All of Error_Object_Id
                If (Err = True) Begin
                    Move "" to sCollation
                End
                Set Value to sCollation
            End_Procedure     
    
        End_Object  
        
        Object oCollate_fm is a cRDCSuggestionIniForm 
            Set Size to 13 216
            Set Location to 65 63
            Set Label to "Collation"
            Set psToolTip to "Suggestion list. Select/Enter the collation to use when creating the database. If none is specified the default SQL server collation above will be used. Note: Collations starting with 'SQL_' is obsolete and should not be used."
            Set pbAskToRemoveMissingFile to False
            Set pbAutoCheckValueExistsAsFile to False
            Set pbUseWorkspaceIniFile to True
            Set piStartAtChar to 3
            Set Prompt_Button_Mode to PB_PromptOn
            
            Function ReadWorkspaceIniFile Returns String[]
                String[] asCollations   
                Integer iCount iSize      
                String sDriverID sCollation
                
                Get psDriverID to sDriverID      
                // The _SqlEnumerateDatabaseCollations function is at current for MSSQLDRV_ID only.
                If (sDriverID <> MSSQLDRV_ID) Begin
                    Function_Return asCollations
                End                 
                
                Send Cursor_Wait of Cursor_Control
                Get _SqlEnumerateDatabaseCollations of ghoDbUpdateFunctionLibrary sDriverID to asCollations
                Set psSavedFormValues to asCollations
                    
                If (sDriverID = MSSQLDRV_ID) Begin
                    Get psCollation of ghoDbUpdateFunctionLibrary to sCollation
                    Set Value to sCollation
                End
                Send Cursor_Ready of Cursor_Control
                Function_Return asCollations
            End_Function
    
            Procedure OnChange
                String sValue
                Get Value to sValue
                Set psCollation to sValue
            End_Procedure
         
            Procedure WriteWorkspaceIniFile
            End_Procedure             
            
//            Procedure Prompt      
//                If (ghoSuggestionTimer=0) Begin
//                    Send CreateSuggestionTimer
//                End
//                Send ActivateSuggestionList
//                Send FindSuggestions
//                Set Visible_State of ghoSuggestionList to True
//            End_Procedure 
            
            Procedure OnEnterObject
                Set pbUseWorkspaceIniFile to True
                Forward Send OnEnterObject
            End_Procedure

            Procedure OnExitObject
                Set pbUseWorkspaceIniFile to False
                Forward Send OnExitObject
            End_Procedure
        
            On_Key kCancel Send KeyAction of oCancel_Btn   
//            On_Key kPrompt Send Prompt
        End_Object   
        
        Object oDatabaseName_fm is a cRDCForm
            Set Size to 13 216
            Set Location to 81 63
            Set Label_Col_Offset to 2
            Set Label_Justification_Mode to JMode_Right
            Set Label to "Database Name"
            Set psToolTip to "Specify the name of the database to be created."
        End_Object
    
    End_Object

    Object oOK_Btn is a Button
        Set Label    to "&OK"
        Set Location to 130 198
        Set peAnchors to anBottomRight   
        
        Property String psWorkingText "Working..."
        Property String psIdleText    ""

        Procedure OnClick
            String sDatabase sDriverID sCollation
            Boolean bOK bExists
        
            Get Value of oDatabaseName_fm to sDatabase    
            Get psDriverID to sDriverID
            Set psDatabaseName to ""

            If (sDatabase <> "" and sDriverID <> "") Begin    
                Set Value of oInfo_tb to (psWorkingText(Self))
                If (sDriverID <> DATAFLEX_ID) Begin
                    Get psCollation to sCollation
                    Set psCollation of ghoDbUpdateFunctionLibrary to sCollation
                End

                Get SqlUtilCheckIfDatabaseExists of ghoDbUpdateFunctionLibrary sDatabase to bExists
                If (bExists = True) Begin
                    Set Value of oInfo_tb to ""
                    Send Info_Box "The database already exists. No changes made."
                    Procedure_Return
                End
                Send Cursor_Wait of Cursor_Control     
                Get SqlDatabaseCreate of ghoDbUpdateFunctionLibrary sDriverID sDatabase False False to bOK
                Send Cursor_Ready of Cursor_Control
                Set Value of oInfo_tb to (psIdleText(Self))
                If (bOK = False) Begin
                    Send Info_Box ("Something went wrong and the database could NOT be created. Please check your database manager.")
                    Procedure_Return
                End 
                Set psDatabaseName to sDatabase
                Send Close_Panel
            End                                 
            Else If (sDatabase = "") Begin
                Send Info_Box ("Please enter a database name and try again.")
            End
        End_Procedure

    End_Object

    Object oCancel_Btn is a Button
        Set Label    to "&Cancel"
        Set Location to 130 253
        Set peAnchors to anBottomRight

        Procedure OnClick
            Send Close_Panel
        End_Procedure

    End_Object

    Object oHelp_btn is a Button
        Set Location to 130 75
        Set Label to "Help"
        Set psImage to "ActionHelpInternet1.ico"
        Set psToolTip to "Show a Full List of Collations Supported in SQL Server 2017"
        Set peAnchors to anBottomLeft
    
        Procedure OnClick
            Runprogram Shell Background "https://database.guide/full-list-of-collations-supported-in-sql-server-2017/"
        End_Procedure
    
    End_Object
    
    On_Key Key_Alt+Key_O Send KeyAction of oOK_Btn
    On_Key Key_Alt+Key_C Send KeyAction of oCancel_Btn
    On_Key kCancel       Send KeyAction of oCancel_Btn
End_Object

Function Create_DataBase_Dialog String sDriverID Returns String
    String sDatabaseName         
    Handle ho
    
    Move (oCreateDatabase_dg(Self)) to ho
    Set psDriverID of ho to sDriverID
    Set Value of (oDriver_fm(ho)) to sDriverID
    Set Value of (oDatabaseName_fm(ho)) to ""
    Send InitializeCollation of (oServerCurrentCollating_fm(ho))
    Send Popup of ho
    
    Get psDatabaseName of ho to sDatabaseName
    Function_Return sDatabaseName
End_Function                         

Use Windows.pkg
Use cRDCButtonDPI.pkg
Use cRichEdit.pkg
Use seq_chnl.pkg
Use vWin32fh.pkg

Object oSourceCodeDialog_dg is a ModalPanel
    Set Size to 214 395
    Set Label to "View Source Code Dialog"
    Set piMinSize to 89 211
    Set Location to 2 2
    Set Border_Style To Border_Thick
    Set Locate_Mode to Center_On_Parent

    Property String psSourceCode ""
    Property Integer piIndent

    Object oSourceCode_edt is a cRichEdit
        Set Size to 169 373
        Set Location to 16 11
        Set TextColor to clBlack
        Set peAnchors to anAll
        Set Label_Row_Offset to 3
        Set Label_TextColor to clGreenGreyLight
        Set Label_FontItalics to True
        Set piFontSize to 200
        Set psTypeFace to 'Consolas'

        Delegate Set piIndent to (piParagraphIndent(Self))

        Procedure DoSaveDocument
            String sSourceCode
            Boolean bOk bOkToSave bChanged

            Move False to bOkToSave
            Get pbCanUndo to bChanged
            If (bChanged = False) Begin
                Procedure_Return
            End

            Get psSourceCode to sSourceCode
            Send Write sSourceCode
            // clear undo buffer on save
            // we want undo buffer to only apply to the new document
            Send ClearUndoBuffer
            Send Info_Box "Changes saved."
        End_Procedure

        Procedure LoadData
            String sSourceCode
            Integer iTwips iIndent

            Get psSourceCode to sSourceCode
            Set Label to sSourceCode
            Move 1440 to iTwips
            Get piIndent to iIndent
            Set piParagraphIndent to (iIndent + (iTwips * 0.2))

            Send Read sSourceCode
        End_Procedure

        On_Key Key_Ctrl+Key_S Send DoSaveDocument
        On_Key kCancel Send Cancel
    End_Object

    Object oCancel_Btn is a cRDCButtonDPI
        Set Label    to "&Close"
        Set Location to 193 334
        Set peAnchors to anBottomRight

        Procedure OnClick
            Send Close_Panel
        End_Procedure

    End_Object


    Object oFirstRun_btn is a cRDCButtonDPI
        Set Location to 193 220
        Set Label to "View &Top"
        Set peAnchors to anBottomRight

        Procedure OnClick
            Send Beginning_of_Data to oSourceCode_edt
        End_Procedure

    End_Object

    Object oLatestRun_btn is a cRDCButtonDPI
        Set Location to 193 277
        Set Label to "View &Bottom"
        Set peAnchors to anBottomRight

        Procedure OnClick
            Send End_of_Data to oSourceCode_edt
        End_Procedure

    End_Object

    // Automatically load data into the grid when activating.
    Procedure Activating
        Handle ho
        Move (oSourceCode_edt(Self)) to ho
        Send LoadData    of ho
        Send Activate    of ho
    End_Procedure

    Procedure Page Integer iPageObject
        Forward Send Page iPageObject
        Set Icon to "ViewSourceCode1.ico"
    End_Procedure

    // Put a status bar at the bottom of the panel to add a gripper in the lower right corner.
    Procedure End_Construct_Object
        Integer iStyle iSize iOffset

        Forward Send End_Construct_Object

        Get Border_Style to iStyle
        Move 8 to iOffset
        If (iStyle = Border_Thick) Begin
            Object oDialogCommandbar is a cCJCommandBarSystem
                Object oStatusBar is a cCJStatusBar
                    Object oStatusIdle is a cCJStatusBarPane
                        Set piId to sbpIDIdlePane
                        Set pbStyleStretch to True
                    End_Object
                End_Object
            End_Object

            Get Size to iSize
            Set Size to (Hi(iSize) + iOffset) (Low(iSize))
        End
    End_Procedure

    On_Key Key_Alt+Key_T  Send KeyAction of oFirstRun_btn
    On_Key Key_Ctrl+Key_T Send KeyAction of oFirstRun_btn
    On_Key Key_Alt+Key_B  Send KeyAction of oLatestRun_btn
    On_Key Key_Ctrl+Key_B Send KeyAction of oLatestRun_btn
    On_Key Key_Alt+Key_C  Send KeyAction of oCancel_Btn
End_Object

Procedure ActivateSourceCodeDialog String sSourceCode
    Handle ho
    Boolean bExists

    Get vFilePathExists sSourceCode to bExists
    If (bExists = False) Begin
        Send Info_Box "The file doesn't exist."
        Procedure_Return
    End
    Move (oSourceCodeDialog_dg(Self)) to ho
    Set psSourceCode of ho to sSourceCode
    Send Popup of ho
End_Procedure

// Note: At the bottom of this package there's a general purpose access method for this dialog.
Use Windows.pkg
Use vWin32fh.pkg

Object oSQLDatabaseBackup_dg is a ModalPanel
    Set Size to 104 298
    Set Label to "SQL Database Backup"
    Set piMinSize to 89 211
    Set Location to 2 4
    Set Border_Style To Border_Thick

    Property Boolean pbOK False
    Property String  psDatabase
    Property String  psPath
    Property String  psBackupName

    Object oDatabase_fm is a Form
        Set Size to 13 204
        Set Location to 14 71
        Set Label_Justification_Mode to JMode_Right
        Set Label_Col_Offset to 2
        Set Label to "Database Name"
        Set Enabled_State to False
        Set peAnchors to anTopLeftRight

        Procedure Page Integer iPageObject
            String sValue
            Get psDatabase of ghoDbUpdateFunctionLibrary to sValue
            Set Value to sValue
            Set psDatabase to sValue
            Forward Send Page iPageObject
        End_Procedure

    End_Object

    Object oBackupName_fm is a Form
        Set Size to 13 204
        Set Location to 29 71
        Set Label_Justification_Mode to JMode_Right
        Set Label_Col_Offset to 2
        Set Label to "Backup Name"
        Set peAnchors to anTopLeftRight

        Procedure Page Integer iPageObject
            String sValue
            Get psDatabase of ghoDbUpdateFunctionLibrary to sValue
            Set Value to sValue
            Forward Send Page iPageObject
        End_Procedure

        Procedure OnChange
            String sValue
            Get Value to sValue
            Set psBackupName to sValue
        End_Procedure

    End_Object

    Object oUseDefaultBackupFolder_cb is a CheckBox
        Set Size to 10 50
        Set Location to 50 71
        Set Label to "Use Default SQL Backup Folder"
        Set Checked_State to True

        Procedure OnChange
            Boolean bChecked
            Get Checked_State to bChecked
            Set Enabled_State of oPath_fm to (bChecked = False)
        End_Procedure

    End_Object

    Object oPath_fm is a Form
        Set Size to 13 204
        Set Location to 62 71
        Set Label_Justification_Mode to JMode_Right
        Set Label_Col_Offset to 2
        Set Label to "Path"
        Set Prompt_Button_Mode to PB_PromptOn
        Set peAnchors to anTopLeftRight
        Set Enabled_State to False

        Procedure Page Integer iPageObject
            String sValue
            Get psDataPathFirstPart of ghoDbUpdateFunctionLibrary to sValue
            Get vFolderFormat sValue to sValue
            Move (sValue + "Backup") to sValue
            Set Value to sValue
            Forward Send Page iPageObject
        End_Procedure

        Procedure Prompt
            String sPath sFileMask sRetval

            Get Value to sPath
            Move "Database Backup files (*.bak)|*.bak" to sFileMask
            Get vSelect_File sFileMask "Please select a .bak file" sPath to sRetval
            If (sRetval <> "") Begin
                Get ParseFolderName sRetval to sPath
                If (Right(sPath, 1) ="\") Begin
                    Move (Left(sPath, Length(sPath) -1)) to sPath
                End
                Set Value to sPath
            End
        End_Procedure

        Procedure OnChange
            String sValue
            Get Value to sValue
            Set psPath to sValue
        End_Procedure

    End_Object

    Object oOK_Btn is a Button
        Set Label    to "&OK"
        Set Location to 81 169
        Set peAnchors to anBottomRight

        Procedure OnClick
            Set pbOK to True
            Send Close_Panel
        End_Procedure

    End_Object

    Object oCancel_Btn is a Button
        Set Label    to "&Cancel"
        Set Location to 81 224
        Set peAnchors to anBottomRight

        Procedure OnClick
            Set pbOK to False
            Send Close_Panel
        End_Procedure

    End_Object

    Object oButton1 is a Button
        Set Size to 14 96
        Set Location to 80 32
        Set Label to "Enum table types"

        Procedure OnClick
            String sDriverID
            tSQLConnection SQLConnection
            Get pSQLConnection of ghoDbUpdateFunctionLibrary to SQLConnection
            Send ShowAllTablesAndColumns SQLConnection.sDriverID SQLConnection.sConnectionString
        End_Procedure

        Procedure ShowAllTablesAndColumns String sDriver String sLogin
            Handle hoCLIHandler
            Integer iNumTables iTableCount

            Get Create U_cCLIHandler to hoCLIhandler
            If (hoCLIHandler > 0) Begin
                Set psDriverID of hoCLIHandler to sDriver
                Get EnumerateTables of hoCLIHandler sLogin to iNumTables
                For iTableCount from 1 to iNumTables
                    Show (TableName(hoCLIHandler, iTableCount)) "; "
                    Show (SchemaName(hoCLIHandler, iTableCount)) "; "
                    Show (TableType(hoCLIHandler, iTableCount)) "; "
                    Showln (TableComment(hoCLIHandler, iTableCount))
//                    Get EnumerateColumns of hoCLIHandler sLogin (TableName(hoCLIHandler, iTableCount)) to iNumColumns
//                    For iColumnCount from 1 to iNumColumns
//                        Showln " " (ColumnName(hoCLIHandler, iColumnCount))
//                    Loop
                    Showln
                Loop
                Send Destroy of hoCLIHandler
            End
        End_Procedure

    End_Object

    Procedure Page Integer iPageObject
        Set Icon to "DbBackup1.ico"
        Forward Send Page iPageObject
    End_Procedure

    On_Key Key_Alt+Key_O Send KeyAction of oOK_Btn
    On_Key Key_Alt+Key_C Send KeyAction of oCancel_Btn

End_Object

// *** General purpose access method for this dialog ***
Function MakeSQLDatabaseBackup Returns Boolean
    Handle ho
    Boolean bOK bDefault
    String sDatabase sPath sBackupName

    Move (oSQLDatabaseBackup_dg(Self)) to ho
    Set pbOK of ho to False
    Send Popup of ho

    Get pbOk of ho to bOK
    If (bOK = True) Begin
        Get Value of (oDatabase_fm(ho))   to sDatabase
        Get Value of (oBackupName_fm(ho)) to sBackupName
        Get Checked_State of (oUseDefaultBackupFolder_cb(ho)) to bDefault
        If (bDefault = False) Begin
            Get Value of (oPath_fm(ho)) to sPath
        End
        Get SqlDatabaseBackupToDisk of ghoDbUpdateFunctionLibrary sDatabase sBackupName sPath True to bOK
    End

    Function_Return bOK
End_Function

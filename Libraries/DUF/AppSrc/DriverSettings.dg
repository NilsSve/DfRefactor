Use cApplication.pkg
Use Dfline.pkg
Use cRichEdit.pkg
Use cScrollingContainer.pkg
Use cRDCModalPanel.pkg
Use cRDCForm.pkg
Use cRDCComboForm.pkg
Use cRDCButton.pkg
Use cRDCHeaderGroup.pkg
Use cDbDriverSettingsForm.pkg
Use cDbUpdateFunctionLibrary.pkg

Register_Procedure psOrgToolTip String sTooltip

Object oDriverSettings_dg is a cRDCModalPanel
    Set Size to 291 345
    Set Label to "Driver Configuration File Info"
    Set Location to 2 2
    Set Maximize_Icon to False
    Set Locate_Mode to CENTER_ON_PARENT
    Set Icon to "DriverSettings1.ico"
    
    Property String psDriverID MSSQLDRV_ID
    Property Boolean pbDriverLoadingError False
    
    Object oCancel_Btn is a cRDCButton
        Set Label    to "&Close"
        Set Location to 269 287
        Set peAnchors to anBottomRight

        Procedure OnClick
            Send Close_Panel
        End_Procedure

    End_Object

    Object oDriver_cf is a cRDCComboForm
        Set Label to "Database Driver"
        Set Size to 13 80
        Set Location to 8 90
        Set Form_Border to 0
        Set Label_Col_Offset to 2
        Set Label_Justification_Mode to JMode_Right
        Set Status_Help to "Database driver."
        Set Enabled_State to False
        Set Skip_State to True

        Procedure Combo_Fill_List
            Send Combo_Add_Item DATAFLEX_ID
            Send Combo_Add_Item DFBTRDRV_ID
            Send Combo_Add_Item ODBC_DRV_ID
            Send Combo_Add_Item MSSQLDRV_ID
            Send Combo_Add_Item DB2_DRV_ID
        End_Procedure

    End_Object

    Object oDriverFileName_cf is a cRDCForm
        Set Label to "Database Driver File"
        Set Size to 12 193
        Set Location to 24 90
        Set Label_Col_Offset to 2
        Set Label_Justification_Mode to JMode_Right
        Set Status_Help to "Database driver to be used when creating new tables."
        Set Color to clBtnFace
        Set Focus_Mode to NonFocusable
        Set peAnchors to anTopLeftRight

        Procedure OnChange
            String sDriverID sValue sDriverIntFileName sTooltip sPath

            Forward Send OnChange
            Get Value to sValue
            Set Status_Help to sValue

            Get psDriverID to sDriverID
            If (sDriverID <> DATAFLEX_ID) Begin
                Get DatabaseDriverIniFilePath sDriverID to sPath
                If (sPath <> "") Begin
                    Move (sPath + sDriverID + ".int") to sDriverIntFileName
                End
            End
            Else Begin
                Move sDriverID to sDriverIntFileName
            End

            Set Value of oDriverIniFile_fm to sDriverIntFileName
            Get psOrgToolTip of oDriverIniFile_fm to sTooltip
            Set Status_Help of oDriverIniFile_fm to (sTooltip * sDriverIntFileName)

            // In case driver attribute objects already exists; destroy them
            Broadcast Send Destroy    of oScrollingClientArea
            // Then recreate them to show driver attributes
            Send DoReadDriverSettings of oScrollingClientArea sDriverID
        End_Procedure  
        
    End_Object

    Object oDriverInfo_btn is a cRDCButton
        Set Size to 13 50
        Set Location to 24 287
        Set Label to "Version Info"
        Set Status_Help to "Shows a dialog with driver properties and SQL client software version(s), license info and more."
        Set peAnchors to anTopRight

        Procedure OnClick
            Object oSysInfoDialog is a SysInfoDialog
            End_Object

            Send Popup of oSysInfoDialog
        End_Procedure
    End_Object

    Object oDriverIniFile_fm is a cRDCForm
        Set Size to 13 193
        Set Location to 39 90
        Set Label_Col_Offset to 2
        Set Label_Justification_Mode to JMode_Right
        Set Label to "Driver Configuration File"
        Set peAnchors to anTopLeftRight
        Set Color to clBtnFace
        Set Focus_Mode to NonFocusable
        Property String psOrgToolTip "The file is the first found in the DF_OPEN_PATH path string."
        Set Status_Help to (psOrgToolTip(Self))
    End_Object

    Object oOpenDriverFile_Btn is a cRDCButton
        Set Size to 13 50
        Set Label to "Edit File"
        Set Location to 39 287
        Set Status_Help to "Opens the driver configuration file that contains configuration settings that are used when tables are restructured, in Notepad."
        Set psImage to "ActionEdit1.ico"
        Set peAnchors to anTopRight
        Set pbAutoEnable to True

        Procedure OnClick
            String sFileName
            Get Value of oDriverIniFile_fm to sFileName
            If (sFileName <> DATAFLEX_ID) Begin
                Runprogram Background "Notepad.exe" sFileName
            End
            Else Begin
                Send Info_Box "Sorry, there exist no driver ini file for the DataFlex database driver, so nothing to show here."
            End
        End_Procedure   
        
        Function IsEnabled Returns Boolean
            String sFileName
            Get Value of oDriverFileName_cf to sFileName
            Function_Return (sFileName <> "")
        End_Function

    End_Object

    Object oDumpDriverData_Btn is a cRDCButton
        Set Size to 13 50
        Set Label to "Dump"
        Set Location to 54 233
        Set peAnchors to anTopRight
        Set psToolTip to "Dumps the current status of the Connectivity Kit driver to an ASCII disk file."
        Set psImage to "ActionExport1.ico"
        Set pbAutoEnable to True
        
        Procedure OnClick
            Handle hoCLI
            String sDriver sPath sFileName
            Integer iRetval

            Get psDriverID to sDriver
            Get Create (RefClass(cCLIHandler)) to hoCLI
            Set psDriverID of hoCLI  to sDriver
            If (sDriver = DATAFLEX_ID or sDriver = DFBTRDRV_ID) Begin
                Send Destroy of hoCLI
                Send Info_Box "Sorry, no attributes can be shown for the selected database driver."
                Procedure_Return
            End

            Get psHome of (phoWorkspace(ghoApplication)) to sPath
            Get vFolderFormat sPath to sPath
            Move (sDriver + "dump.txt") to sFileName
            Send DumpStatus of hoCLI (sPath + sFileName)
            Send Destroy of hoCLI

            Send vShellExecute "open" sFileName "" sPath
            Sleep 1
            Get vDeleteFile (sPath + sFileName) to iRetval
        End_Procedure

        Function IsEnabled Returns Boolean
            String sFileName
            Boolean bDriverLoadingError 
            Get pbDriverLoadingError to bDriverLoadingError
            Get Value of oDriverFileName_cf to sFileName
            Function_Return (sFileName <> "" and bDriverLoadingError = False)
        End_Function

    End_Object

    Object oReset_Btn is a cRDCButton
        Set Size to 13 50
        Set Label to "Refresh"
        Set Status_Help to "If you edit the driver configuration file, the display of attributes above needs to be refreshed. Click here to refresh."
        Set Location to 54 287
        Set psImage to "ActionRefresh1.ico"
        Set peAnchors to anTopRight
        Set pbAutoEnable to True

        Procedure OnClick
            String sDriver
            Get psDriverID to sDriver
            Unload_Driver sDriver
            Load_Driver sDriver
            Send Info_Box "This dialog needs to be re-opened to display the refreshed values. Dialog will now close and you need to re-open it manually."
            Send Close_Panel
        End_Procedure

        Function IsEnabled Returns Boolean
            String sFileName
            Boolean bDriverLoadingError 
            Get pbDriverLoadingError to bDriverLoadingError
            Get Value of oDriverFileName_cf to sFileName
            Function_Return (sFileName <> "" and bDriverLoadingError = False)
        End_Function

    End_Object

    Object oDriverSetings_grp is a cRDCHeaderGroup
        Set Size to 125 328
        Set Location to 74 9
        Set psImage to "DriverSettings1.ico"
        Set Label to "Driver Configuration File Settings"   
        Set psNote to "Hover the mouse over an attribute field to read information about it."
        Set peAnchors to anAll

        Object oInvisible_Container is a Container3d
            Set Size to 100 327
            Set Location to 24 0
            Set Border_Style to Border_None
            Set peAnchors to anAll
    
            Object oScrollingContainer is a cScrollingContainer
               
                Object oScrollingClientArea is a cScrollingClientArea

                    // Just fill the array with all available driver attributes from Cli.pkg,
                    // and one object will automatically be created for each attribute
                    // for the current driver by the DoReadDriverSettings procedure.
                    Function GeneralDriverAttributes Returns String[]
                        String[] sAttributesArray
    
                        Move "DF_DRIVER_DEFAULT_DEFAULT_ASCII             |CI1101" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_DEFAULT_NULLABLE_ASCII            |CI1102" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_DEFAULT_DEFAULT_NUMERIC           |CI1103" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_DEFAULT_NULLABLE_NUMERIC          |CI1104" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_DEFAULT_DEFAULT_DATE              |CI1105" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_DEFAULT_NULLABLE_DATE             |CI1106" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_DEFAULT_DEFAULT_TEXT              |CI1107" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_DEFAULT_NULLABLE_TEXT             |CI1108" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_DEFAULT_DEFAULT_BINARY            |CI1109" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_DEFAULT_NULLABLE_BINARY           |CI1110" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_DUMMY_ZERO_DATE_VALUE             |CI1111" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_MAX_ACTIVE_STATEMENTS             |CI1112" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_CACHE_PATH                        |CI1113" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_DRIVER_DECIMAL_SEPARATOR          |CI1114" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_DEFAULT_TABLE_CHARACTER_FORMAT    |CI1115" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_DRIVER_THOUSANDS_SEPARATOR        |CI1116" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_APPLICATION_CHARACTER_FORMAT      |CI1117" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_DRIVER_DATE_FORMAT                |CI1118" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_LAST_ERROR_TEXT                   |CI1119" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_DRIVER_DATE_SEPARATOR             |CI1120" to sAttributesArray[SizeOfArray(sAttributesArray)]
    
                        // Exception to the rule!
                        // Someone - in its infinite wisdome - decided that the keyword in the ini file should be "DFConnectionId",
                        // and not "Connection_ID" that is the API attribute name. This brakes the cDriverSettingsForm tooltip logic.
                        // We fix it by changing the attribute constant string here (removed the _ (underscore) before the "ID" part.
                        Move "DF_DRIVER_CONNECTIONID                      |CI1121" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_IGNORE_UCASE_SUPPORT              |CI1122" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_CONNECTION_ID_STRING              |CI1123" to sAttributesArray[SizeOfArray(sAttributesArray)]
    
                        Move "DF_DRIVER_IGNORE_WARNINGS                   |CI1124" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_USE_DF_LOCKERROR                  |CI1126" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_FIND_CACHE_TIMEOUT                |CI1128" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_JIT_TRESHOLD                      |CI1130" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_TRUNCATE_BINARY_ZEROES            |CI1132" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_ERROR_DEBUG_MODE                  |CI1134" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_USE_CACHE                         |CI1136" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_REPORT_CACHE_ERRORS               |CI1138" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_USE_CACHE_EXPIRATION              |CI1140" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_DEFAULT_USE_DUMMY_ZERO_DATE       |CI1142" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_DEFAULT_RECORD_IDENTITY_HIDING    |CI1144" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_REPORT_ACTIVE_COLUMN_ERRORS       |CI1146" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_SILENT_LOGIN                      |CI1148" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_DEFAULT_MAP_TO_RECNUM             |CI1150" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_CONNECTION_ID_OPTIONS             |CI1152" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_NUMBER_CONNECTION_IDS             |CI1154" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_DEFAULT_DEFAULT_DATETIME          |CI1155" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_DEFAULT_NULLABLE_DATETIME         |CI1156" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_MATCH_CLIENT_SERVER_VERSION       |CI1158" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_SQLSERVER_CLIENT_VERSION          |CI1160" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_MAP_DFDATE_TO_ODBCTYPE            |CI1168" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_MAP_DFDATE_TO_SQLTYPE             |CI1169" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_MAP_DFDATETIME_TO_ODBCTYPE        |CI1170" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_MAP_DFDATETIME_TO_SQLTYPE         |CI1171" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_MAP_DFASCII_TO_ODBCTYPE           |CI1172" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_MAP_DFASCII_TO_SQLTYPE            |CI1173" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_MAP_DFTEXT_TO_ODBCTYPE            |CI1174" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_MAP_DFTEXT_TO_SQLTYPE             |CI1175" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_MAP_DFBINARY_TO_ODBCTYPE          |CI1176" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_MAP_DFBINARY_TO_SQLTYPE           |CI1177" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_DEFAULT_MAP_DF_TO_SQL_TYPE_SCHEMA |CI1178" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_LOGIN_ON_OPEN                     |CI1180" to sAttributesArray[SizeOfArray(sAttributesArray)]
                        Move "DF_DRIVER_MINIMUM_CLIENT_VERSION            |CI1182" to sAttributesArray[SizeOfArray(sAttributesArray)]
    
                        // Sort items alphabetically
                        Move (SortArray(sAttributesArray)) to sAttributesArray
                        Function_Return sAttributesArray
                    End_Function
    
                    Function SplitValue String sValue Integer ByRef iAttribute Returns String
                        String sAttribute sTmp
                        Integer iPos
    
                        Move (Pos("|", sValue)) to iPos
                        Move (Left(sValue, (iPos -1))) to sAttribute
                        Move (Mid(sValue, Length(sValue), iPos)) to sTmp
                        Move (Trim(sAttribute)) to sAttribute
                        Move (Eval(sTmp)) to iAttribute
    
                        Function_Return sAttribute
                    End_Function
    
                    // Note: Creates dynamic objects.
                    // They are not being destroyed by this method.
                    // It is the reponsibility of the calling method to do that,
                    // prior calling this method.
                    Procedure DoReadDriverSettings String sDriver
                        Integer iLocW iLocH iHfactor iFirstLineOffset iCount iItems iAttribute
                        Handle ho hoParent
                        String[] sAttributesArray
                        String sValue sAttribute sDriverIniFile 
                        Boolean bDriverLoadingError
    
                        Move (Self) to hoParent
                        Move 13  to iHfactor // Height offset between form
                        Move 120 to iLocW
                        Move 10  to iLocH                            
                        Move  0  to iFirstLineOffset // Offset from the top for the very first object.
    
                        Get Value of oDriverIniFile_fm to sDriverIniFile
                        If (Trim(sDriverIniFile) = "") Begin
                            Get Create (RefClass(cDbDriverSettingsForm)) to ho
                            Move (iFirstLineOffset + iLocH) to iLocH
                            Set Location of ho to iLocH (iLocW + 30)
                            Set Label of ho to "No Driver Configuration File found!"
                            Set Value of ho to "No attributes to display..."   
                            Procedure_Return
                        End
                            
                        // Driver attributes aren't supported by the Pervasive driver,
                        // so notify and quite.
                        If (sDriver = DFBTRDRV_ID or sDriver = DATAFLEX_ID) Begin
                            Get Create (RefClass(cDbDriverSettingsForm)) to ho
                            Move (iFirstLineOffset + iLocH) to iLocH
                            Set Location of ho to iLocH (iLocW + 30)
                            Set Label of ho to "Query of driver attributes unsupported!"
                            Set Value of ho to "Sorry, no attributes to display"
                            Procedure_Return
                        End
    
                        Get GeneralDriverAttributes to sAttributesArray
                        Move (SizeOfArray(sAttributesArray)) to iItems
                        Decrement iItems
                        Send Ignore_Error of Error_Object_Id CLIERR_CONNECTIONIDOUTOFRANGE 
                        Send Ignore_Error of Error_Object_Id DFERR_UNSUPPORTED_ATTRIBUTE  // Invalid driver value
    
                        For iCount from 0 to iItems
                            Move sAttributesArray[iCount] to sValue
                            Get SplitValue sValue (&iAttribute) to sAttribute
    
                            Get Create (RefClass(cDbDriverSettingsForm)) to ho 
                            Set psDriverIniFile of ho to sDriverIniFile
                            Send SetValue of ho sDriver sAttribute iAttribute
                            Get private.pbDriverLoadingError of ho to bDriverLoadingError
                            If (bDriverLoadingError = True) Begin
                                Set Label of ho to "Driver could not be loaded!"   
                                If (Trim(sDriverIniFile) = "") Begin
                                    Set Value of ho to "Driver .dll file not found!"
                                End
                                Else Begin
                                    Set Value of ho to "No attributes to display..."   
                                End
                                Set psToolTip of ho to "Check that the database engine for the selected driver is installed and is running correctly!"  
                                Delegate Set pbDriverLoadingError to True
                                Move iItems to iCount // We're out of here.   
                                Move False to Err
                            End
                            If (Err = True) Begin
                                Send Destroy of ho
                            End
                            Else Begin
                                If (iCount = 0) Begin
                                    Move (iFirstLineOffset + iLocH) to iLocH
                                End
                                Set Location of ho to iLocH (iLocW + 30)
                                Move (iLocH + iHfactor) to iLocH
                            End
                        Loop
    
                        Send Trap_Error of Error_Object_Id CLIERR_CONNECTIONIDOUTOFRANGE
                        Send Trap_Error of Error_Object_Id DFERR_UNSUPPORTED_ATTRIBUTE
                    End_Procedure
    
                End_Object

            End_Object
    
        End_Object

    End_Object

    Object oInfo_edt is a cRichEdit
        Set Size to 49 328
        Set Location to 213 9
        Set Label to "General Connectivity Kit Information:"
        Set Value to "It is possible to configure the behavior of a Connectivity Kit through configuration files. Configuration files can be located anywhere in DFPATH. In general, one configuration file per install is enough. There are situations where there is a need to have different configurations for different deploy environments on one machine/network. In that case the configuration file should be placed in the deployment environment rather than in the overall DataFlex environment."
        Set Skip_State to True
        Set Read_Only_State to True
        Set pbItalics to True
        Set peAnchors to anBottomLeftRight

        Procedure DoIndent
            Integer iTwipsPerInch iCurrentIndent
        
            // number of twips per inch
            Move 1440 to iTwipsPerInch
        
            Set piSpacingBefore to (1440 * 0.05)
            // indent 0.1 inches from current indentation
            Get piParagraphIndent to iCurrentIndent
            Set piParagraphIndent to (iCurrentIndent + (iTwipsPerInch * 0.1))
        End_Procedure
        Send DoIndent

    End_Object

    Procedure Activating
        Forward Send Activating
        Send Cursor_Ready of Cursor_Control
    End_Procedure

    On_Key Key_Alt+Key_C Send KeyAction of oCancel_Btn
    On_Key kCancel       Send KeyAction of oCancel_Btn
End_Object

// General purpose access method for the dialog above:
Procedure PopupDriverSettings String sDriverID 
    Handle ho
    String sDriverFile sPath

    Send Cursor_Wait of Cursor_Control
    Move (oDriverSettings_dg(Self)) to ho
    Set psDriverID of ho to sDriverID
    Set pbDriverLoadingError of ho to False

    Set Value of (oDriver_cf(ho)) to sDriverID
    Get DatabaseDriverPath sDriverID to sPath
    If (sPath <> "") Begin
        Move (sDriverID + ".dll")  to sDriverFile
    End
    Set Value of (oDriverFileName_cf(ho)) to (sPath + sDriverFile)
    Send Cursor_Ready of Cursor_Control

    Send Popup of ho
End_Procedure

Function DatabaseDriverPath String sDriverID Returns String
    String sPath sDriverFileName

    Move "" to sPath
    If (sDriverID = DATAFLEX_ID) Begin
        Function_Return sPath
    End                   
    
    Move (sDriverID + ".dll") to sDriverFileName
    Get SearchLocallyThenGloballyForFile of (phoSQLConnectionIniFile(ghoSQLConnectionHandler)) sDriverFileName to sPath
    
    Function_Return sPath
End_Function      

Function DatabaseDriverIniFilePath String sDriverID Returns String
    String sPath sDriverFileName

    Move "" to sPath
    If (sDriverID = DATAFLEX_ID) Begin
        Function_Return sPath
    End                   
    
    Move (sDriverID + ".int") to sDriverFileName
    Get SearchLocallyThenGloballyForFile of (phoSQLConnectionIniFile(ghoSQLConnectionHandler)) sDriverFileName to sPath
    
    Function_Return sPath
End_Function      

// StatusPanelExample.src
Use DfAllEnt.pkg
Use cApplication.pkg

Use ProcessStatusPanel.pkg

Object oApplication is a cApplication
End_Object

Use cCJSkinFramework.pkg
Object oCJSkinFramework is a cCJSkinFramework
    Set psSkinFile to "Windows8.cjstyles"
    Set psSkinIni to "NormalAero.ini"
End_Object

Object oStatusPanelExample is a ModalPanel
    Set Locate_Mode to CENTER_ON_SCREEN
    Set Size to 150 200
    Set Label to "Status Panel Example"
    Set Sysmenu_Icon to False

    Object oInform_User is a Textbox
        Set Location to 10 5
        Set Size to 13 180
    End_Object

    Object oOk_Btn is a Button
        Set Location to 95 75
        Set Label to "Start"

        Procedure onClick
            Integer x y z iCnt
            Boolean bStopIt
            Set Enabled_State to False
            Set Enabled_State of oExit_Btn to False
            //
            Send Update_Textbox "Processing..."
            Send Reset_StatusPanel of ghoStatusPanel // reset object visibility to their defaults.
            Set Progress_Bar_Overall_Visible_State of ghoProgressBarOverall to True // Default is False
            Set piMinimum of ghoProgressBarOverall to 0
            Set piMaximum of ghoProgressBarOverall to (15 * 5000)
            For x from 1 to 15
                Send Initialize_StatusPanel of ghoStatusPanel "Caption" "Title" "Message"
                Set Allow_Cancel_State of ghoStatusPanel to False                       // You would normally wish to do this within a batch update process.
                Set Cancel_Button_Visible_State of ghoStatusPanel to False              // It makes sense not to display the cancel button.
                Set Progress_Bar_Visible_State of ghoProgressBar to True                // Default is False
                Set piMinimum of ghoProgressBar to 0
                Set piMaximum of ghoProgressBar to (5000)
                Send Start_StatusPanel of ghoStatusPanel
                //
                // This For-from-to loop is standing in for some data processing.
                For y from 1 to 5000
                    Increment iCnt
                    Send Update_StatusPanel of ghoStatusPanel ("y =" * String(y))
                    Set piPosition of ghoProgressBar to (y)
                    Set piPosition of ghoProgressBarOverall to (iCnt)
                Loop
                //
                Send Stop_StatusPanel of ghoStatusPanel // the status panel is only being stopped here to demonstrate that its location and size remain the same when skinning is used.
                Set piPosition of ghoProgressBar to 0 // reset
                //
                // This For-from-to loop is standing in simply as a minor delay.
                For z from 1 to 10000
                Loop
                //
            Loop
            Set piPosition of ghoProgressBarOverall to 0 // reset
            Send Reset_StatusPanel of ghoStatusPanel // reset object visibility back to their defaults.
            //
            // Inform user that update was successful and give focus to exit button.
            //
            Send Finish_Update "COMPLETED... Process was successful."
            Set Enabled_State of oExit_Btn to True
            Send Next
            //
        End_Procedure



        Procedure Update_Textbox String sVal
            Set Value of oInform_User to sVal
        End_Procedure


        Procedure Finish_Update String sVal
            Set Value of oInform_User to sVal
        End_Procedure
    End_Object


    Object oExit_Btn is a Button
        Set Location to 115 75
        Set Label to "Exit"

        Procedure onClick
            Send Close_Panel of oStatusPanelExample
        End_Procedure
    End_Object
End_Object


Procedure Do_It
    Send Popup of oStatusPanelExample
End_Procedure

Send Do_It

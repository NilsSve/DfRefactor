Object oDatabaseToolbar is a cCJToolbar
	Set psTitle to "Database Toolbar"
	Set peStretched to stStretchShared

	Object oDatabaseMenuItem is a cCJMenuItem
		Set peControlType to xtpControlComboBox
		Set peControlStyle to xtpButtonCaption
		Set psCaption to "Database:"

		Procedure OnCreateControl Handle hoObj
			Set ComDropDownWidth of hoObj to 200
			Set ComWidth of hoObj to 300
			Send FillComboList hoObj
		End_Procedure
		
		Function DatabaseName String sConnection Returns String
			String[] sConnectionParts
			Integer iElements iElement iPos

			Move (StrSplitToArray (sConnection, ";")) to sConnectionParts
			Move (SizeOfArray (sConnectionParts)) to iElements
			If (iElements > 0) Begin
				Decrement iElements
				For iElement from 0 to iElements
					Move (Pos ("=", sConnectionParts[iElement])) to iPos
					If (iPos > 0) Begin
						If (Uppercase (Left (sConnectionParts[iElement], iPos - 1)) = "DATABASE") Begin
							Function_Return (Right (sConnectionParts[iElement], Length (sConnectionParts[iElement]) - iPos))
						End
					End
				Loop
			End
			
			Function_Return ""
		End_Function
		
		Procedure FillComboList Handle hoCombo
			tConnection[] ConnectionsInfo
			Boolean bOk
			Integer iElements iElement iIndex
			String sDatabaseName
			
			Send ComClear of hoCombo
			
			Get LoadStoredConnections of ghoConnection (&ConnectionsInfo) to bOk
			If (bOk) Begin
				Move (SizeOfArray (ConnectionsInfo)) to iElements
				If (iElements > 0) Begin
					Decrement iElements
					For iElement from 0 to iElements
						Get DatabaseName ConnectionsInfo[iElement].sString to sDatabaseName
						Increment iIndex
						Send ComAddItem of hoCombo sDatabaseName iIndex
						Set ComItemData of hoCombo iIndex to iElement
						If (not (ConnectionsInfo[iElement].bDisabled)) Begin
							Set ComListIndex of hoCombo to iIndex
						End
					Loop
				End
			End
		End_Procedure 
	
		Procedure OnExecute Variant vCommandBarControl
			Handle hoCombo
			Integer iIndex iElement
			Boolean bOk
			tConnection[] ConnectionsInfo

			Get LoadStoredConnections of ghoConnection (&ConnectionsInfo) to bOk
			If (bOk) Begin
				Get CreateProxyControl vCommandBarControl to hoCombo
				Get ComListIndex of hoCombo to iIndex
				If (iIndex > 0) Begin
					Get ComItemData of hoCombo iIndex to iElement
					Get RedirectConnectionId of ghoConnection ConnectionsInfo[iElement].sId ConnectionsInfo[iElement].sString ConnectionsInfo[iElement].sUID ConnectionsInfo[iElement].sPWD ConnectionsInfo[iElement].bTrustedConnection True to bOk
				End
				Send Destroy of hoCombo
			End
		End_Procedure             
	End_Object
End_Object

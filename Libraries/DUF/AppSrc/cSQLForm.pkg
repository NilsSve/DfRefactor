Use Windows.pkg

Class cSQLForm is a Form
    Procedure Construct_Object
        Forward Send Construct_Object

        // This will shadow the label if the form itself has its Enabled_State=False
        Property Boolean pbAutoShadowLabelObject True

        // The piItem should be set to the object's corresponding tSQLConnection member (One based)
        Property Integer piItem

        Set Label_Justification_Mode to JMode_Right
        Set Label_Col_Offset to 2
    End_Procedure

    Procedure End_Construct_Object
        Boolean bAutoShadowLabelObject
        Get pbAutoShadowLabelObject to bAutoShadowLabelObject
        If (bAutoShadowLabelObject = True) Begin
            Set Private.Label_Shadow_Display_Mode to TBSHADOW_ON_LOCAL
        End
        Forward Send End_Construct_Object
    End_Procedure

    Procedure DbTypeUpdate Integer iDbType String sDriverID
    End_Procedure

    Procedure ReadConnectionData String[] sConnectionData
        Integer iItem
        Get piItem to iItem
        Set Value to sConnectionData[iItem]
    End_Procedure

    Procedure WriteConnectionData String[] ByRef sConnectionData
        String sValue
        Integer iItem
        Get piItem to iItem
        Get Value to sValue
        Move sValue to sConnectionData[iItem]
    End_Procedure

    Procedure Set psToolTip String sToolTip
        String sStatusHelp

        Get Status_Help to sStatusHelp
        If (sStatusHelp = "") Begin
            Set Status_Help to sToolTip
        End

        Forward Set psToolTip to sToolTip
    End_Procedure

End_Class

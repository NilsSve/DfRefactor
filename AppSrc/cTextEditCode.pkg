Use cTextEdit.pkg

Class cTextEditCode is a cTextEdit
    Procedure Construct_Object
        Forward Send Construct_Object    
        
        Set psTypeFace to "Consolas"
        // The font size is expressed as twips, therefore we multiply with 20 to get the correct point size (1 twips = 1/20 point)
        Set piFontSize to (11 * 20)
//        Set pbBold  to True
        Set pbWrap to False  
        Set Color to clDarkGreen  
        Set TextColor to clWhite //(RGB(156,255,0))
        
        Property Boolean pbReadFile False
        Property Integer piLines 0
        
    End_Procedure

    Function ReadImageData Integer iImg Returns String[]
        Integer iChannel iCount
        String sTextValue sLine
        Boolean bSeqEof
        String[] asLegacyCode
        
        Move "" to sTextValue
        Move 0 to iCount
        
        Get Seq_New_Channel to iChannel
        If (iChannel = DF_SEQ_CHANNEL_NOT_AVAILABLE) Begin
             Send Stop_box "No channel available. Function: 'ReadImageData'"
             Function_Return asLegacyCode
        End
        
        Set pbReadFile to True    
        Send Delete_Data
        Direct_Input channel iChannel ("image: " + String(iImg))
        
        While (SeqEof = False)
            Readln channel iChannel sLine
            Move sLine to asLegacyCode[iCount]
            Increment iCount
        Loop
        Close_Input channel iChannel
        Send Seq_Release_Channel iChannel 
        Set pbReadFile to False
        Move (ResizeArray(asLegacyCode, (iCount -1))) to asLegacyCode
        
        Function_Return asLegacyCode
    End_Function
    
    Procedure DisplayCode String[] asLegacyCode
        Integer iSize iCount  

        Set pbReadFile to True        
        Send Delete_Data
        Move (SizeOfArray(asLegacyCode)) to iSize
        Decrement iSize
        For iCount from 0 to iSize  
            If (iCount < iSize) Begin
                Send AppendTextLn asLegacyCode[iCount]
            End 
            Else Begin
                Send AppendText asLegacyCode[iCount]
            End
        Loop

        Set pbReadFile to False
        Send Beginning_of_Data
        Set piLines to iCount 
        Send OnChange   
    End_Procedure     
    
    Function ReadCode Returns String[]
        String[] asLegacyCode
        String sLine
        Integer iSize iCount 
        
        Get Line_Count to iSize
        For iCount from 0 to (iSize -1)
            Get Line iCount to sLine
            Move sLine to asLegacyCode[iCount]    
        Loop                                  
        
        Function_Return asLegacyCode
    End_Function   
    
End_Class

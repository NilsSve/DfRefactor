Use Windows.pkg
Use cRichEdit.pkg
Use cRDCButtonDPI.pkg
Use cRDCModalPanel.pkg
Use seq_chnl.pkg
Use vwin32fh.pkg

Object oErrorLogDialog_dg is a cRDCModalPanel
    Set Size to 214 624
    Set Label to "Compile Errors"
    Set piMinSize to 89 211
    Set Location to 2 2
//    Set Color to clGreenGreyLight 
    Set Icon to "CompileErrors.ico"

    Property String psErrorLog ""
    Property Integer piIndent

    Object oErrorLog_edt is a cRichEdit
        Set Size to 169 602
        Set Location to 16 11
        Set TextColor to clBlack
        Set peAnchors to anAll
        Set Label_Row_Offset to 3
        Set Label_FontItalics to True
        Set piFontSize to 200
        Set psTypeFace to 'Consolas'
        Set Color to clGreenGreyLight
        Set Label_TextColor to clGreenGreyLight
//        Set TextColor to clGreenGreyLight 
        Set TextColor to clWhite
        Set TextBackColor to clBlack

        Delegate Set piIndent to (piParagraphIndent(Self))

        Procedure DoSaveDocument
            String sErrorLog
            Boolean bOk bOkToSave bChanged

            Move False to bOkToSave
            Get pbCanUndo to bChanged
            If (bChanged = False) Begin
                Procedure_Return
            End

            Get psErrorLog to sErrorLog
            Send Write sErrorLog
            // clear undo buffer on save
            // we want undo buffer to only apply to the new document
            Send ClearUndoBuffer
            Send Info_Box "Changes saved."
        End_Procedure

        Procedure LoadData
            String sErrorLog
            Integer iTwips iIndent

            Get psErrorLog to sErrorLog
            Set Label to sErrorLog
            Move 1440 to iTwips
            Get piIndent to iIndent
            Set piParagraphIndent to (iIndent + (iTwips * 0.2))

            Send Read sErrorLog   
        End_Procedure  
        
        Function ParseSourceLineNumber Returns Integer
            Integer iLine iPos iSize
            String sLine sSearch           
            Handle ho
            
            Get phoEditorRefactored of ghoApplication to ho
            Get Line_Count of ho to iSize
            If (iSize <= 1) Begin
                Procedure_Return
            End
            
            Move -1 to iLine    
            Move " ON LINE: " to sSearch
            Get LineFromChar iLine to iLine
            Get Line iLine to sLine     
            Move (Pos(sSearch, sLine)) to iPos
            
            // It can happen that the info we're after is on the next line,
            // after the one that is selected:
            If (iPos = 0) Begin
                Increment iLine
                Get Line iLine to sLine     
                Move (Pos(sSearch, sLine)) to iPos
            End
            If (iPos <> 0) Begin
                Move (Mid(sLine, Length(sLine), (iPos + Length(sSearch)))) to sLine
                Move (Pos(" ", sLine)) to iPos
                Move (Left(sLine, (iPos - 1))) to sLine
                Move (Trim(sLine)) to iLine 
                Decrement iLine
            End
            
            Function_Return iLine
        End_Function
        
        Procedure DisplayCurrentSourceLine  
            Handle ho
            Integer iLine
            
            Get phoEditorRefactored of ghoApplication to ho
            Get ParseSourceLineNumber to iLine
            If (iLine <> -1) Begin         
                Send Close_Panel
                Send JumpToSourceLine of (Client_Id(phoMainPanel(ghoApplication))) iLine    
                Send Activate of ho
            End
        End_Procedure   
        
        // sets the current line to iLine and highlights it
        Procedure SelectRow Integer iLine
            Integer iPos iLen
            Get FirstCharInLinePos iLine to iPos
            Get LineLength iLine to iLen
            Send SetSel iPos (iPos + iLen)
        End_Procedure
        
        // get the current selected Line
        Function SelectedRow Returns Integer
            Integer iLine
            Get LineFromChar -1 to iLine
            Function_Return iLine
        End_Function
        
        // select the current row
        Procedure Mouse_Up Handle hCell Integer iPos  
            Integer iLine
            Get SelectedRow to iLine
            Send SelectRow iLine
        End_Procedure
   
        Procedure Mouse_Click
            Send DisplayCurrentSourceLine
        End_Procedure
        
        Procedure Down_Row
            Integer iLine
            Get SelectedRow to iLine
            Increment iLine
            Send SelectRow iLine
        End_Procedure
        
        Procedure Up_Row
            Integer iLine
            Get SelectedRow to iLine
            Decrement iLine
            Send SelectRow iLine
        End_Procedure
        
        Procedure Page Integer iPageObject
            Forward Send Page iPageObject
            Send SelectRow 0
        End_Procedure

        On_Key Key_Ctrl+Key_S Send DoSaveDocument
        On_Key kEnter         Send DisplayCurrentSourceLine  
        On_Key kDownArrow     Send Down_Row
        On_Key kUpArrow       Send Up_Row
        On_Key Key_Ctrl+Key_B Send None
        On_Key Key_Ctrl+Key_I Send None
        On_Key Key_Ctrl+Key_U Send None
        On_Key kCancel        Send Cancel  
    End_Object

    Object oCancel_Btn is a cRDCButtonDPI
        Set Label    to "&Close"
        Set Location to 193 563
        Set peAnchors to anBottomRight

        Procedure OnClick
            Send Close_Panel
        End_Procedure

    End_Object

    Object oGoToSourceLine_btn is a cRDCButtonDPI
        Set Size to 14 73
        Set Location to 193 339
        Set Label to "Go To Source Line"
        Set peAnchors to anBottomRight
        Set Default_State to True

        Procedure OnClick
            Send DisplayCurrentSourceLine to oErrorLog_edt
        End_Procedure

    End_Object

    Object oFirstRun_btn is a cRDCButtonDPI
        Set Location to 193 449
        Set Label to "View Top"
        Set peAnchors to anBottomRight

        Procedure OnClick
            Send Beginning_of_Data to oErrorLog_edt
        End_Procedure

    End_Object

    Object oLatestRun_btn is a cRDCButtonDPI
        Set Location to 193 506
        Set Label to "View Bottom"
        Set peAnchors to anBottomRight

        Procedure OnClick
            Send End_of_Data to oErrorLog_edt
        End_Procedure

    End_Object

    // Automatically load data into the grid when activating.
    Procedure Activating
        Handle ho
        Move (oErrorLog_edt(Self)) to ho
        Send LoadData    of ho
        // We need to active before we can send end_of_data.
        Send Activate    of ho
        Send End_of_Data to ho
    End_Procedure

    On_Key Key_Alt+Key_F  Send KeyAction of oFirstRun_btn
    On_Key Key_Ctrl+Key_F Send KeyAction of oFirstRun_btn
    On_Key Key_Alt+Key_L  Send KeyAction of oLatestRun_btn
    On_Key Key_Ctrl+Key_L Send KeyAction of oLatestRun_btn
    On_Key Key_Alt+Key_C  Send KeyAction of oCancel_Btn
End_Object

// Public access method for this dialog.
// Pass a file name including path to display.
Procedure ActivateErrorDialog String sErrorLog
    Handle ho
    Boolean bExists

    Get vFilePathExists sErrorLog to bExists
    If (bExists = False) Begin
        Send Info_Box "The ErrorLog doesn't exist."
        Procedure_Return
    End
    Move (oErrorLogDialog_dg(Self)) to ho
    Set psErrorLog of ho to sErrorLog
    Send Popup of ho
End_Procedure

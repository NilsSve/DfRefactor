Use cRDCModalPanel.pkg
Use Cursor.pkg
Use vWin32fh.pkg
Use cRDCCJSelectionGrid.pkg
Use cRefactorTextbox.pkg
Use cRDCButtonDPI.pkg
Use RefactorConstants.inc
Use cUnusedSourceFiles.pkg

Object oOtherLogFiles_dg is a cRDCModalPanel
    Set Label to "Log Files for Function Type: eOther/Report_Function(All)"
    Set Size to 209 366
    Set Location to 2 2
    Set piMinSize to 209 366
    Set Icon to "OtherLogFiles.ico"

    Property String[] pasOtherLogFiles

    Object oOtherLogFiles_grd is a cRDCCJSelectionGrid
        Set Size to 164 338
        Set Location to 14 14
        Set pbAllowDeleteRow to False
        Set pbAllowEdit to False
        Set pbAutoAppend to False
        Set pbAutoSave to False
        Set pbEditOnClick to False
        Set pbEditOnTyping to False

        Object oSourceFile_col is a cCJGridColumn
            // This must be here:
            Set phoData_Col to Self
            Set piWidth to 675
            Set psCaption to "Full Path and File Name"
        End_Object

        Procedure LoadData
            Handle hoDataSource
            tDataSourceRow[] TheData TheDataEmpty
            String[] asOtherLogFiles
            Integer iSize iRow iCount iDataCol iCheckBoxCol

            If (not(IsComObjectCreated(Self))) Begin
                Procedure_Return
            End

            Get pasOtherLogFiles to asOtherLogFiles
            Move (SizeOfArray(asOtherLogFiles)) to iSize
            If (iSize = 0) Begin
                Procedure_Return
            End

            Move 0 to iCount
            Decrement iSize
            Get phoDataSource to hoDataSource
            Get DataSource of hoDataSource to TheData
            Move TheDataEmpty to TheData
            Get piColumnId of oSourceFile_col         to iDataCol
            Get piColumnId of (phoCheckbox_Col(Self)) to iCheckBoxCol

            Move 0 to iRow
            For iCount from 0 to iSize                  
                Move asOtherLogFiles[iCount] to TheData[iRow].sValue[iDataCol]
                Move False  to TheData[iRow].sValue[iCheckBoxCol]
                Increment iRow
            Loop

            If (iRow <> 0) Begin
                Send ReInitializeData TheData False
                Send MoveToFirstRow
            End
            Else Begin
                Send InitializeData TheDataEmpty
            End
            Move (iSize + 1) to iCount
            Set psFooterText of oSourceFile_col  to ("No of Log Files:" * String(iCount))
        End_Procedure

        Procedure OpenLogFile
            String sLogFile
            Get SelectedRowValue of oSourceFile_col to sLogFile
            If (sLogFile <> "") Begin
                Send ActivateEngineLogFile sLogFile
                Send Close_Panel
            End
        End_Procedure
        
        Procedure OnComRowDblClick Variant llRow Variant llItem
            Forward Send OnComRowDblClick llRow llItem
            Send OpenLogFile
        End_Procedure
        
        On_Key kEnter Send OpenLogFile    
    End_Object

    Object oOK_Btn is a cRDCButtonDPI
        Set Size to 14 76
        Set Label to "Open Log File"
        Set Location to 186 222
        Set peAnchors to anBottomRight
        Set psToolTip to "Open the selected logfile in a dialog."
        Set psImage to "ActionOpen.ico"
        Set pbAutoEnable to True

        Procedure OnClick
            Send OpenLogFile of oOtherLogFiles_grd
        End_Procedure
        
        Function IsEnabled Returns Boolean
            Integer iCheckedItems
            Get CheckedItems of oOtherLogFiles_grd to iCheckedItems
            Function_Return (iCheckedItems <> 0)
        End_Function

    End_Object

    Object oCancel_Btn is a cRDCButton
        Set Label to "&Close"
        Set Location to 186 303
        Set peAnchors to anBottomRight

        Procedure OnClick
            Send Close_Panel
        End_Procedure

    End_Object

    Object oTextBox1 is a TextBox
        Set Size to 9 86
        Set Location to 3 25
        Set Label to "Right click grid for options"
    End_Object

    On_Key Key_Alt+Key_A  Send SelectAll
    On_Key Key_Ctrl+Key_A Send SelectAll
    On_Key Key_Alt+Key_N  Send SelectNone
    On_Key Key_Ctrl+Key_N Send SelectNone
    On_Key Key_Alt+Key_I  Send SelectInvert
    On_Key Key_Ctrl+Key_I Send SelectInvert
    On_Key Key_Alt+Key_C  Send KeyAction of oCancel_Btn
End_Object

Procedure ShowOtherLogFilesDialog String[] asOtherLogFiles
    Handle ho
    Move (oOtherLogFiles_dg(Self)) to ho
    Set pasOtherLogFiles of ho to asOtherLogFiles
    Send Popup of ho
End_Procedure

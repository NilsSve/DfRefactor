// The Refactor Testing program is aimed at testing small refactoring functions, to make they work exactly as intended.
// After those new refactoring functions have been thoroughly tested, they can be implemented in the main DfRefactor program.
//
Use DFAllEnt.pkg
Use cCJStandardCommandBarSystem.pkg
Use cCJCommandBarSystem.pkg
Use File_dlg.pkg
Use gFormatNumbers.pkg
Use vwin32fh.pkg
Use RefactorConstants.inc
Use cFilesystem.pkg
Use cRefactorApplication.pkg

// Not sure why we need these, but it is due to the "IsEnabled functions in the ghoCommandBarSytem"
Procedure Exit_Application for cDesktop
    Send SuspendGUI of Desktop True
    If (Exit_System_Confirmation(Self)) ;
        Abort
End_Procedure

Object oHtmlHelp is a cHtmlHelp
    Set pbAlwaysOnTop to False
End_Object

Object oApplication is a cRefactorApplication
    Set psProduct to "Test Bench"
    Set psCompany to "RDC Tools International"
    Set psHelpFile to "DFRefactor.chm"
    Set peHelpType to htHtmlHelp
    Set pbWorkspaceMode to False

    Property Boolean pbIsRefactoring False
    Property Handle phoEditorLegacy
    Property Handle phoEditorRefactored
//    Property Handle phoUseConstraints_cb
    Property String private.psExpFileJson
    Property String private.psImpFileJson   
    
    Procedure OnWorkspaceOpened
        Forward Send OnWorkspaceOpened
        Set psHomePath to (psHome(phoWorkspace(Self)))
    End_Procedure
    
    Procedure Set psExpFileJson String sJsonFile
        Send WriteString CS_Settings CS_ExportJsonFile sJsonFile
        Set private.psExpFileJson to sJsonFile
    End_Procedure

    Function psExpFileJson Returns String
        String sJsonFile
        Get private.psExpFileJson to sJsonFile
        If (sJsonFile = "") Begin
            Get ReadString CS_Settings CS_ExportJsonFile "" to sJsonFile
            Set private.psExpFileJson to sJsonFile
        End
        Function_Return sJsonFile
    End_Function

    Procedure Set psImpFileJson String sJsonFile
        Send WriteString CS_Settings CS_ImportJsonFile sJsonFile
        Set private.psImpFileJson to sJsonFile
    End_Procedure

    Function psImpFileJson Returns String
        String sJsonFile
        Get private.psImpFileJson to sJsonFile
        If (sJsonFile = "") Begin
            Get ReadString CS_Settings CS_ImportJsonFile "" to sJsonFile
            Set private.psImpFileJson to sJsonFile
        End
        Function_Return sJsonFile
    End_Function

    Procedure CompareFiles
        Boolean bExist bExist2
        String sCompareApp sLegacyCodeFile sRefactoredCodeFile

        Get psFileCompareApp to sCompareApp
        File_Exist sCompareApp bExist
        If (bExist = False) Begin
            Send Info_Box "You need to specify the compare program first."
            Procedure_Return
        End

        Get psCodeFile of (phoEditorLegacy(Self)) to sLegacyCodeFile
        File_Exist sLegacyCodeFile bExist
        Get psCodeFile of (phoEditorRefactored(Self)) to sRefactoredCodeFile
        File_Exist sRefactoredCodeFile bExist2
        If (bExist = False or bExist2 = False) Begin
            Send Info_Box "You have to create the legacy and refactored source code files first."
            Procedure_Return
        End
        
        Runprogram Background ('"' + sCompareApp + '"') ('"' + sLegacyCodeFile + '" "' + sRefactoredCodeFile + '"')
    End_Procedure

    Procedure SaveCodeFiles
        Handle hoEditor
        Boolean bChanged
        Get phoEditorLegacy to hoEditor
        If (hoEditor) Begin
            Send SaveFile of hoEditor
        End
        
        Get phoEditorRefactored to hoEditor
        If (hoEditor) Begin
            Send SaveFile of hoEditor
        End
    End_Procedure

    Procedure CompileRefactoredCode
        String sRefactoredFile sBinPath sHome sAppSrc sCompareApp
        Boolean bExists bIsRefactoring
        Integer iLines
        Handle ho
        
        Get pbIsRefactoring to bIsRefactoring
        If (bIsRefactoring = True) Begin
            Procedure_Return
        End
        Get phoEditorRefactored to ho
        Get Line_Count of ho to iLines
        If (iLines <= 1) Begin
            Send Info_Box "You need to press 'Refactor Legacy Code', or short-cut key (Ctrl+R)"
            Procedure_Return
        End
        
        Get DFBinPath to sBinPath
        Get vFolderFormat sBinPath to sBinPath
        Get psHome of (phoWorkspace(Self)) to sHome
        Get vFolderFormat sHome to sHome
        Get psAppSrcPath of (phoWorkspace(Self)) to sAppSrc
        Get vFolderFormat sAppSrc to sAppSrc
        Send Execute of (oSave_ToolItem(ghoCommandBars))
        
        Get psCodeFile of (phoEditorRefactored(Self)) to sRefactoredFile
        Send Cursor_Wait of Cursor_Control
        Runprogram Wait ('"' + sBinPath + CS_Compiler + '"' * "-x" + '"' + sHome + CS_SWSFile + '"' * CS_CompOptions * CS_TestProgram)
        
        Send Cursor_Ready of Cursor_Control
        Get FileSizeEx of ghoFileSystem (sAppSrc + CS_TestErrFile) to iLines
        If (iLines > 0) Begin
            Send ActivateErrorDialog of (Client_Id(phoMainPanel(ghoApplication))) (sAppSrc + CS_TestErrFile)
        End
        Else Begin
            Send Info_Box "The refactored code compiled successfully!"
        End
    End_Procedure
    
    Procedure OpenContainingContainer
        String sPath sFile
        Handle hoEditor
        Get phoEditorLegacy to hoEditor
        Get psCodeFile of hoEditor to sFile
        Get ParseFolderName sFile to sPath
        // We want to have that file to be selected in Windows Explorer
        If (sFile <> "") Begin
            Move ("/select, " + sFile) to sPath
        End
        Send vShellExecute "open" "explorer.exe" sPath ""
    End_Procedure
                
    Procedure SelectSourceFile
        Boolean bOpen bReadOnly
        String  sFileName sInitialFolder
        String[] asFiles
        Handle ho hoEditor

        Get Create (RefClass(OpenDialog)) to ho
        Set Filter_String of ho to (CS_SourceCodeFilters+"|All Files (*.*)|*.*")
        Get psAppSrcPath of (phoWorkspace(ghoApplication)) to sInitialFolder
        Set Initial_Folder of ho to sInitialFolder
        Set MultiSelect_State of ho to False
        
        Get Show_Dialog of ho to bOpen
        If (bOpen = True) Begin
            Get Selected_Files of ho to asFiles
            Get TickReadOnly_State of ho to bReadOnly
            If (bReadOnly = True) Begin
                Send Info_Box ("Files should *not* be opened as ReadOnly.") "Warning!"
            End
            Else Begin
                Move asFiles[0] to sFileName
                Get phoEditorLegacy of ghoApplication to hoEditor 
                Set psCodeFile of hoEditor to sFileName
                Send LoadFile of hoEditor sFileName
            End
        End
        Else Begin
            Send Info_Box "No file selected."
        End
        Send Destroy of ho
    End_Procedure

End_Object

Object oToolTipController is a cToolTipController
    Move Self to ghoToolTipController
    Set piIcon to TTI_INFO
    Set psTitle to "Info"
    Set piMaxWidth to 250
    Set piDurationPopup to 14000 // 14 seconds, needed for long tooltips.
    Set pbUsePrefix to True
End_Object

Open SysFile
Register_Object oSysfile_DD

Use CaptureWindow.pkg
Use oEditContextMenu.pkg
Use oDEOEditContextMenu.pkg
#IF (!@ < 200)
    Define xtpThemeNativeWindows10  for 17
#ENDIF

Object oMain is a Panel
    Set Label to (Uppercase(psProduct(ghoApplication)) * "- DFRefactor")
    Set Location to 4 3
    Set Size to 300 633
    Set piMinSize to 294 550
    
    Object oCommandBarSystem is a cCJCommandBarSystem
        Set pbTimerUpdate to True
        Set piLayoutBuild to 4
        Set pbToolBarAccelTips to True
        Set pbTabbedWorkspaces to True
        Set pbAutoResizeIcons to True
//        Set peRestoreLayout to rlSaveRestoreAll
//        Set peVisualTheme to xtpThemeVisualStudio2022Light

        Property Handle phoTabWorkspace
        Property Handle phoTabPaintManager

        Procedure OnCreateCommandBars
            Send DoChangeToolTipFormat
        End_Procedure

        Procedure OnCreateTabbedWorkspace Handle hoTabWorkspace Handle hoTabPaintManager
            Set phoTabWorkspace     to hoTabWorkspace
            Set phoTabPaintManager  to hoTabPaintManager
            Send DisplayTabWorkspace
        End_Procedure

        Procedure DisplayTabWorkspace
            Handle hoTab hoPaintManager hoOptions
            Integer iSize iType

            If (not(IsComObjectCreated(Self))) Begin
                Procedure_Return
            End

            Get OptionsObject to hoOptions

            // Create the tab workspace object. It will get created for the Client_Area so
            // each view will be displayd on a separate tab-page (instead of the standard DF MDI-interface)
            Get phoTabWorkspace    to hoTab
            Get phoTabPaintManager to hoPaintManager

            // Don't show the tab-pages "close" and next/prev buttons.
            Set ComFlags of hoTab to xtpWorkspaceHideAll

            Move 16 to iSize
            Send ComSetIconSize of hoPaintManager iSize iSize   // Set icon size for Tab-workspace.
            Send ComSetPopupIconSize of hoOptions 24 24
            // To show or not to show tab-page icons...
            Set ComShowIcons    of hoPaintManager to True

            // Sets the tab-pages to display on the left side, instead of at the top:
            Get ReadInteger of ghoApplication CS_Settings CS_TabPosition xtpTabPositionTop to iType
            Set ComPosition     of hoPaintManager to iType

            // This will truncate the middle part of long items
            //Set ComDrawTextPathEllipsis of hoPaintManager to True

            Get piToolbarIconSize of ghoApplication to iSize
            Set piToolbarIconSize to iSize
            Set ComToolTipBehaviour of hoPaintManager to xtpTabToolTipAlways // We _must_ have this for tooltips to show on tab-pages (views)
        End_Procedure

        Procedure DoChangeToolTipFormat
            Variant vToolTip
            Handle  hoObject
            Integer eTheme iToolTipStyle
            Boolean bIsBalloonStyleSupported

            Get ComToolTipContext to vTooltip
            Get Create (RefClass(cCJToolTipContext)) to hoObject
            Set pvComObject of hoObject to vTooltip
            Move xtpToolTipStandard to iToolTipStyle
            
            // Baloon tooltip style requires IE 5.0 or later, so check if installed.
            // The ComShowTitleAndDescription also requires IE 5.0.
            Get ComIsBalloonStyleSupported of hoObject to bIsBalloonStyleSupported
            If (bIsBalloonStyleSupported = False) Begin
                // If not supported we cannot show baloon tooltip; use standard style instead.
                If (iToolTipStyle = xtpToolTipBalloon) Begin
                    Move xtpToolTipStandard to iToolTipStyle
                End
            End
            If (bIsBalloonStyleSupported = True) Begin
                Send ComShowTitleAndDescription of hoObject True xtpToolTipIconInfo
            End
            
            Set ComStyle            of hoObject to iToolTipStyle
            Set ComShowOfficeBorder of hoObject to True
            Set ComShowShadow       of hoObject to True
            // Set the max width for a tooltip. 250 just seems to be a good
            // compromise. After 250 pixels the text will wrap to the next line automatically.
            Set  ComMaxTipWidth     of hoObject to 250 // In pixels
            Send Destroy            of hoObject
            
            If (phoStatusBar(ghoCommandBars)) Begin
                Send DoChangeToolTipFormat of (phoStatusBar(Self))
            End
        End_Procedure

        Procedure Set piToolbarIconSize Integer iSize
            Handle hoOptions

            If (not(IsComObjectCreated(Self))) Begin
                Procedure_Return
            End

            If (iSize < 16) Begin
                Move 32 to iSize
            End
            Set pbLargeIcons to (iSize = 32)
            Get OptionsObject to hoOptions
            Send ComSetIconSize of hoOptions False iSize iSize  // Set icon size for Toolbar buttons.
            Send ComRecalcLayout
        End_Procedure

        Procedure SetTheTheme Integer eTheme
            Integer iColor
            Set peVisualTheme to eTheme
            // If the color is changed it messes with the grid alternate background color.
            // Get ComGetSpecialColor XPCOLOR_TOOLBAR_FACE to iColor
            // Broadcast Recursive Set Color of (Client_Id(phoMainPanel(ghoApplication))) to iColor
            Send ComRecalcLayout
        End_Procedure

        Object oEditToolBar is a cCJToolbar
            Set psTitle to "Edit Toolbar"
            Set pbCloseable to False
            Set pbEnableAnimation to True
            Set pbShowExpandButton to False

            Object oCutToolbarItem is a cCJCutMenuItem
            End_Object

            Object oCopyToolbarItem is a cCJCopyMenuItem
            End_Object

            Object oPasteToolbarItem is a cCJPasteMenuItem
            End_Object

            Object oDeleteEditToolbarItem is a cCJDeleteEditMenuItem
                Set pbControlBeginGroup to True
            End_Object

            Object oToggleWhiteSpaceItm is a cCJMenuItem
                Set pbControlBeginGroup to True
                Set psImage to "ToggleWhiteSpace.ico"
                Set psCaption to "Toggle &White Space"
                Set psToolTip to "Toggle white space (Ctrl+W)"
                Set pbActiveUpdate to True
    
                Procedure OnExecute Variant vCommandBarControl
                    Integer iRetval
                    Boolean bIsWhiteSpace
                    Handle hoEditor
                    
                    Get phoEditorLegacy of ghoApplication to hoEditor
                    Get CM_IsWhitespaceDisplayEnabled of hoEditor to bIsWhiteSpace
                    Get CM_EnableWhitespaceDisplay of hoEditor (not(bIsWhiteSpace)) to iRetval
                    Get phoEditorRefactored of ghoApplication to hoEditor
                    Get CM_EnableWhitespaceDisplay of hoEditor (not(bIsWhiteSpace)) to iRetval
                End_Procedure
                
                Function IsEnabled Returns Boolean
                    Handle hoLegacy hoRefactored
                    Get phoEditorLegacy     of ghoApplication to hoLegacy
                    Get phoEditorRefactored of ghoApplication to hoRefactored
                    Function_Return (Focus(Desktop) = hoLegacy or Focus(Desktop) = hoRefactored)
                End_Function
                
            End_Object

            Object oToogleIndentationGuides is a cCJMenuItem
                Set psImage to "ToggleIndentationGuides.ico"
                Set psCaption to "Toggle &Indentation Guides"
                Set psToolTip to "Toggle Indentation Guides (Ctrl+I)"
                Set pbActiveUpdate to True
                
                Procedure OnExecute Variant vCommandBarControl
                    Integer iRetval
                    Boolean bIsWhiteSpace bMode
                    Handle hoEditor
                    
                    Get phoEditorLegacy of ghoApplication to hoEditor
                    Send ToggleIndentationGuides of hoEditor
                    Get phoEditorRefactored of ghoApplication to hoEditor
                    Send ToggleIndentationGuides of hoEditor
                End_Procedure
    
                Function IsEnabled Returns Boolean
                    Handle hoLegacy hoRefactored
                    Get phoEditorLegacy     of ghoApplication to hoLegacy
                    Get phoEditorRefactored of ghoApplication to hoRefactored
                    Function_Return (Focus(Desktop) = hoLegacy or Focus(Desktop) = hoRefactored)
                End_Function
                
            End_Object

            Object oToogleScopeBlocks is a cCJMenuItem
                Set psImage to "ToogleScopeBlocks.ico"
                Set psCaption to "Expand/Collapse Blocks"
                Set psToolTip to "Expand/Collapse Blocks"
                Set pbActiveUpdate to True
                
                Property Boolean pbToggleScopeBlocks True
                
                Procedure OnExecute Variant vCommandBarControl
                    Boolean bMode
                    Handle hoEditor
                    
                    Get phoEditorLegacy of ghoApplication to hoEditor
                    Get pbToggleScopeBlocks to bMode
                    Send ToggleScopeBlocks of hoEditor (not(bMode))
                    Get phoEditorRefactored of ghoApplication to hoEditor
                    Send ToggleScopeBlocks of hoEditor (not(bMode))
                    Set pbToggleScopeBlocks to (not(bMode))
                End_Procedure
    
                Function IsEnabled Returns Boolean
                    Handle hoLegacy hoRefactored
                    Get phoEditorLegacy     of ghoApplication to hoLegacy
                    Get phoEditorRefactored of ghoApplication to hoRefactored
                    Function_Return (Focus(Desktop) = hoLegacy or Focus(Desktop) = hoRefactored)
                End_Function
                
            End_Object

        End_Object
        
        Object oActions_toolbar is a cCJToolbar
            Set psTitle to "Action Toolbar"
            Set pbCloseable to False
            Set pbEnableAnimation to True
            Set pbShowExpandButton to False

            Object oRefactor_ToolItem is a cCJMenuItem
                Set pbControlBeginGroup to True
                Set pbActiveUpdate to True
                Set peControlStyle to xtpButtonIconAndCaption
                Set psCaption to "Start &Refactoring"
                Set psToolTip to "Refactor Legacy Code (Ctrl+F5)"
                Set psDescription to "Refactor Legacy Code (Ctrl+F5)"
                Set psImage to "Start.ico"
                Set psShortcut to "Ctrl+R"
                Set pbActiveUpdate to True
                
                Procedure OnExecute Variant vCommandBarControl
//                    Boolean bUseConstraints
//                    Get Checked_State of (phoUseConstraints_cb(ghoApplication)) to bUseConstraints
                    Send RefactoreCode of (phoRefactorView(ghoApplication)) //bUseConstraints
                End_Procedure

                Function IsEnabled Returns Boolean
                    String sViewName sObjectName
//                    Boolean bUseConstraints
                    Move (Name(phoRefactorView(ghoApplication))) to sViewName
                    Move (Name(Focus(Desktop))) to sObjectName
//                    Get Checked_State of (phoUseConstraints_cb(ghoApplication)) to bUseConstraints
                    Function_Return ((SysFile.SelectedFunctionTotal > 0 and sObjectName contains sViewName) or SysFile.bCountSourceLines = True)
                End_Function

                Procedure OnCreateControl Handle hoObj
                    Set ComDefaultItem of hoObj to True
                End_Procedure
                
            End_Object

            Object oCompile_ToolItem is a cCJMenuItem
                Set peControlStyle to xtpButtonIconAndCaption
                Set pbControlBeginGroup to True
                Set pbActiveUpdate to True
                Set psCaption to "Compile"
                Set psToolTip to "Compile refactored code (F5)"
                Set psDescription to "Compile refactored code (F5)"
                Set psImage to "CompileProject.ico"
                Set psShortcut to "F5"
                
                Procedure OnExecute Variant vCommandBarControl
                    Send CompileRefactoredCode of ghoApplication
                End_Procedure

                Function IsEnabled Returns Boolean
                    Integer iLines
                    If (pbIsRefactoring(ghoApplication) = True) Begin
                        Function_Return False
                    End
                    Get SC_LineCount of (phoEditorRefactored(ghoApplication)) to iLines
                    Function_Return (iLines > 1)
                End_Function
                
            End_Object

            Object oErrorLog_ToolItem is a cCJMenuItem
                Set peControlStyle to xtpButtonIconAndCaption
                Set pbControlBeginGroup to True
                Set pbActiveUpdate to True
                Set psCaption to "Compile &Errors"
                Set psToolTip to "Show errors from compilation (Ctrl+E)"
                Set psDescription to "Show Errors (Ctrl+E)"
                Set psImage to "CompileErrors.ico"
                Set psShortcut to "Alt+E"

                Procedure OnExecute Variant vCommandBarControl
                    String sAppSrcPath
                    Boolean bExists

                    Get psAppSrcPath of (phoWorkspace(ghoApplication)) to sAppSrcPath
                    Get vFolderFormat sAppSrcPath to sAppSrcPath
                    Get vFilePathExists (sAppSrcPath + CS_TestErrFile) to bExists
                    If (bExists = True) Begin
                        Send ActivateErrorDialog of (Client_Id(phoMainPanel(ghoApplication))) (sAppSrcPath + CS_TestErrFile)
                    End
                End_Procedure

                Function IsEnabled Returns Boolean
                    Boolean bExists
                    String sAppSrcPath
                    Integer iLines

                    Get psAppSrcPath of (phoWorkspace(ghoApplication)) to sAppSrcPath
                    Get vFolderFormat sAppSrcPath to sAppSrcPath
                    Get vFilePathExists (sAppSrcPath + CS_TestErrFile) to bExists
                    Get SC_LineCount of (phoEditorRefactored(ghoApplication)) to iLines
                    Function_Return (bExists = True and iLines > 1)
                End_Function

            End_Object

            Object oCompare_MenuItem is a cCJMenuItem
                Set peControlStyle to xtpButtonIconAndCaption
                Set psCaption to "Co&mpare Code"
                Set psToolTip to "Co&mpare Diff's Legacy && Refactored (Alt+M)"
                Set psDescription to "Compare file changes after running refactoring function(s) (Alt+M)"
                Set psImage to "Compare.ico"
                Set pbActiveUpdate to True
                Set pbControlBeginGroup to True

                Procedure OnExecute Variant vCommandBarControl
                    String sCompareApp
                    Send Execute of (oSave_ToolItem(ghoCommandBars))
                    Get psFileCompareApp of ghoApplication to sCompareApp
                    Send CompareFiles of ghoApplication sCompareApp
                End_Procedure

                Function IsEnabled Returns Boolean
                    Integer iLegacyLines iRefactorLines      
                    If (pbIsRefactoring(ghoApplication) = True) Begin
                        Function_Return False
                    End
                    Get SC_LineCount of (phoEditorLegacy(ghoApplication)) to iLegacyLines
                    Get SC_LineCount of (phoEditorRefactored(ghoApplication)) to iRefactorLines
                    Function_Return (iLegacyLines > 1 and iRefactorLines > 1)
                End_Function

            End_Object

            Object oOpenLogFile_MenuItem is a cCJMenuItem
                Set psToolTip to "View logfile"
                Set psDescription to "View the logfile that is added to for each run"
                Set psImage to "OpenLogFile.ico"
                Set peControlStyle to xtpButtonIcon
                Set pbActiveUpdate to True
                Set pbControlBeginGroup to True

                Procedure OnExecute Variant vCommandBarControl
                    Send ShowLogFile of ghoApplication
                End_Procedure

                Function IsEnabled Returns Boolean
                    String sPath sLogFile
                    Boolean bLogFileExists

                    Get psHomePath of ghoApplication to sPath
                    Get vFolderFormat sPath to sPath
                    Move (sPath + CS_BackupFolder + CS_DirSeparator + CS_SummaryLogfileName) to sLogFile
                    Get vFilePathExists sLogFile to bLogFileExists

                    Function_Return (bLogFileExists = True)
                End_Function

            End_Object

            Object oOpenOtherLogFile_MenuItem is a cCJMenuItem
                Set psToolTip to "Other logfiles"
                Set psDescription to "View a list of logfiles created by functions of type: 'eOther_Function"
                Set psImage to "OtherLogFiles.ico"
                Set peControlStyle to xtpButtonIcon
                Set pbActiveUpdate to True
                Set pbControlBeginGroup to True

                Procedure OnExecute Variant vCommandBarControl            
                    Send ShowOtherLogFiles of ghoApplication
                End_Procedure

                Function IsEnabled Returns Boolean
                    String[] asOtherLogFiles
                    Get pasOtherLogFiles of ghoRefactorFuncLib to asOtherLogFiles
                    Function_Return (SizeOfArray(asOtherLogFiles) > 0)
                End_Function

            End_Object

            Object oShowErrorLog_MenuItem is a cCJMenuItem
                Set psToolTip to "View Refactoring Error Log"
                Set psDescription to "View the refactoring error log"
                Set psImage to "ErrorLog.ico"
                Set peControlStyle to xtpButtonIcon
                Set pbActiveUpdate to True

                Procedure OnExecute Variant vCommandBarControl
                    Send Popup of (oErrorLog_dg(Client_Id(ghoCommandBars)))
                End_Procedure 
                
                Function IsEnabled Returns Boolean
                    Integer iErrors
                    Get_Attribute DF_FILE_RECORDS_USED of StatLog.File_Number to iErrors
                    Function_Return (iErrors > 0)
                End_Function

            End_Object


        End_Object

        Object oFindToolBar is a cCJToolbar
            Set psTitle to "Find Edit Toolbar"
            Set pbDockNextTo to False
            Set pbVisible to False
            
            Object oFindFirstTool is a cCJFindFirstMenuItem
            End_Object
            
            Object oFindPreviousTool is a cCJFindPreviousMenuItem
            End_Object
            
            Object oFindMenuTool is a cCJFindMenuItem
            End_Object
            
            Object oFindNextTool is a cCJFindNextMenuItem
            End_Object
            
            Object oFindLastTool is a cCJFindLastMenuItem
            End_Object
            
            Object oPromptToolItem is a cCJPromptMenuItem
                Set pbControlBeginGroup to True
            End_Object
            
            Object oClear_ToolItem is a cCJClearMenuItem
                Set peControlStyle to xtpButtonIconAndCaption
            End_Object

            Object oRefresh_ToolItem is a cCJMenuItem
                Set psCaption to "Refresh"
                Set psToolTip to "Refresh view data"
                Set psDescription to "Reloades the 'Legacy Code' editor file and clears the 'Refactored Code' editor"
                Set psImage to "Refresh.ico"
                Set psShortcut to "Ctrl+R"
                Set peControlStyle to xtpButtonIconAndCaption
                Set pbActiveUpdate to True
                
                Procedure OnExecute Variant vCommandBarControl
                    Handle ho
                    String sFileName
                    
                    Get phoRefactorView of ghoApplication to ho
                    Get phoEditorRefactored of ghoApplication to ho
                    Send Request_Clear of ho
                    Get phoEditorLegacy of ghoApplication to ho
                    Get psCodeFile of ho to sFileName
                    Send LoadFile of ho sFileName   
                    Send OnModified of ho
                End_Procedure
                
                Function IsEnabled Returns Boolean   
                    String sViewName sObjectName
                    Move (Name(phoRefactorView(ghoApplication))) to sViewName
                    Move (Name(Focus(Desktop))) to sObjectName
                    Function_Return (sObjectName contains sViewName)
                End_Function
                
            End_Object
            
            Object oSave_ToolItem is a cCJSaveMenuItem
                Set peControlStyle to xtpButtonIconAndCaption
                Set pbControlBeginGroup to True

                Procedure OnExecute Variant vCommandBarControl
                    Handle ho hoEditor
                    Get phoRefactorView of ghoApplication to ho
                    If (Active_State(ho)) Begin
                        Get phoEditorLegacy of ghoApplication to hoEditor
                        If (hoEditor) Begin
                            Send SaveFile of hoEditor
                        End
                    End
                    Send Request_Save_No_Clear of (Focus(Self))
                End_Procedure

                Function IsEnabled Returns Boolean
                    Boolean bIsDEO bHasRecord bChanged bEnabled bHasIndex
                    Handle hoServer
                    Boolean bChangedLegacy bChangedRefactored

                    Get DEOInformation (&hoServer) (&bHasRecord) (&bChanged) (&bHasIndex) to bIsDeo
                    Get CM_IsModified of (phoEditorLegacy(ghoApplication))     to bChangedLegacy
                    Get CM_IsModified of (phoEditorRefactored(ghoApplication)) to bChangedRefactored
                    
                    Function_Return (bChanged = True or bChangedLegacy = True or bChangedRefactored = True)
                End_Function
                
            End_Object
            
            Object oDeleteToolItem is a cCJDeleteMenuItem
                Set peControlStyle to xtpButtonIconAndCaption
            End_Object
            
        End_Object

        Object oPrograms_toolbar is a cCJToolbar
            Set psTitle to "Programs Toolbar"
            Set pbCloseable to False
            Set pbEnableAnimation to True
            Set pbShowExpandButton to False
            
            Object oOpenContainingFolder_MenuItem is a cCJMenuItem
                Set psImage to "ActionOpenContainingFolder.ico"
                Set psCaption to "Containing Folder"
                Set psToolTip to "Open Containing Folder (Ctrl+O)"
                Set psDescription to "Open Containing Folder"
                Set pbActiveUpdate to True
                Set pbControlBeginGroup to True

                Procedure OnExecute Variant vCommandBarControl
                    String sPath sFile
                    Handle hoEditor

                    Get psAppSrcPath of (phoWorkspace(ghoApplication)) to sPath
                    Get phoEditorLegacy of ghoApplication to hoEditor
                    Get psCodeFile of hoEditor to sFile

                    // We want to have that file to be selected in Windows Explorer
                    If (sFile <> "") Begin
                        Move ("/select, " + sFile) to sPath
                    End
                    Send vShellExecute "open" "explorer.exe" sPath ""
                End_Procedure

                Function IsEnabled Returns Boolean
                    String sFile
                    Handle hoEditor
                    Boolean bExists
                    Get phoEditorLegacy of ghoApplication to hoEditor
                    Get psCodeFile of hoEditor to sFile
                    Get vFilePathExists sFile to bExists
                    Function_Return (bExists = True)
                End_Function

            End_Object

            // This object needs to be here for the Scintilla objects's right-click menu.
            Object oOpenFolderMenuItem is a cCJMenuItem
                Set psCaption to CS_BrowseSourceFileTxt
                Set psToolTip to "Select source file"
                Set psDescription to "Displays an Open File dialog to select a source file from"
                Set psImage to "ActionOpen.ico"
                Set peControlStyle to xtpButtonIconAndCaption
                
                Procedure OnExecute Variant vCommandBarControl
                    Send SelectSourceFile of ghoApplication
                End_Procedure

            End_Object

        End_Object

        Object oExternalPrograms_toolbar is a cCJToolbar
            Set psTitle to "External Toolbar"
            Set pbCloseable to False
            Set pbEnableAnimation to True
            Set pbShowExpandButton to False

            Object oUnitTest_MenuItem is a cCJMenuItem
                Set peControlStyle to xtpButtonIconAndCaption
                Set psCaption to "Run &Unit Tests"
                Set psToolTip to "Run Unit Testing program (Alt+U)"
                Set psDescription to "If needed the program is alawys compiled first."
                Set psImage to "UnitTesting.ico"
                Set psShortcut to "Ctrl+U"
                
                Procedure OnExecute Variant vCommandBarControl
                    Boolean bExists bEnabled
                    String sProgramPath sProgramFile sSourceFile sBinPath sHome
                    
                    Get DFBinPath of ghoApplication to sBinPath
                    Get vFolderFormat sBinPath to sBinPath
                    Get psHome of (phoWorkspace(ghoApplication)) to sHome
                    Get vFolderFormat sHome to sHome
                    Get psProgramPath of (phoWorkspace(ghoApplication)) to sProgramPath
                    Get vFolderFormat sProgramPath to sProgramPath
                    Move (CS_DFUnitTest + ".src") to sSourceFile
                    Move (Replace(".src", sSourceFile, ".exe")) to sProgramFile
                    Get vFilePathExists (sProgramPath + sProgramFile) to bExists

                    Runprogram Wait ('"' + sBinPath + CS_Compiler + '"' * "-x" + '"' + sHome + CS_SWSFile + '"' * CS_CompOptions * sSourceFile)

                    // If program compilation failed:
                    If (bExists = False) Begin
                        Send Info_Box ("The compilation of the" * (CS_DFUnitTest + ".src program") * "failed and can't be started. Please check it with the Studio.")
                    End
                    Else Begin
                        // Extra protection from user clicking this icon again after the Unit program was started.
                        Set pbEnabled to False
                        Runprogram Wait sProgramFile
                        Send PumpMsgQueue Of Desktop
                        Set pbEnabled to True
                    End
                End_Procedure
            
            End_Object

            Object oDFRefactor_MenuItem is a cCJMenuItem
                Set peControlStyle to xtpButtonIconAndCaption
                Set psCaption to "Run &DFRefactor"
                Set psToolTip to "Run the main DFRefactor program (Alt+D)"
                Set psDescription to "If needed the program is alawys compiled first."
                Set psImage to "DFRefactor.ico"
                Set psShortcut to "Ctrl+D"

                // This could be used instead of "OnExecute" to just run the DFRefactor program without recompiling it first:
                Procedure RunDFRefactorProgram
                    Boolean bExists
                    String sProgramPath sProgramFile
                    
                    Move (CS_DFRefactor + ".exe") to sProgramFile
                    Get psProgramPath of (phoWorkspace(ghoApplication)) to sProgramPath
                    Get vFolderFormat sProgramPath to sProgramPath
                    Get vFilePathExists (sProgramPath + String(sProgramFile)) to bExists
                    If (bExists = False) Begin
                        Send Info_Box "Can't find the DFRefactor.exe program. Is it compiled?"
                        Procedure_Return
                    End
                    Runprogram Background (sProgramPath + String(sProgramFile))
                End_Procedure
                
                Procedure OnExecute Variant vCommandBarControl
                    Boolean bExists
                    String sProgramPath sProgramFile sSourceFile sBinPath sHome
                    Integer iPos
                    
                    Get DFBinPath of ghoApplication to sBinPath
#IFDEF IS$WIN64
                    Append sBinPath "64"
#ENDIF
                    Get vFolderFormat sBinPath to sBinPath
                    Get psHome of (phoWorkspace(ghoApplication)) to sHome
                    Get vFolderFormat sHome to sHome
                    Get psProgramPath of (phoWorkspace(ghoApplication)) to sProgramPath
                    Get vFolderFormat sProgramPath to sProgramPath
                    Move (CS_DFRefactor + ".src") to sSourceFile
                    Move (Replace(".src", sSourceFile, ".exe")) to sProgramFile
#IFDEF IS$WIN64
                    Move (Pos(".", sProgramFile)) to iPos
                    If (iPos <> 0) Begin
                        Move (Insert("64", sProgramFile, iPos)) to sProgramFile
                    End
#ENDIF
                    Get vFilePathExists (sProgramPath + sProgramFile) to bExists

                    Runprogram Wait ('"' + sBinPath + CS_Compiler + '"' * "-x" + '"' + sHome + CS_SWSFile + '"' * String(CS_CompOptions) * String(sSourceFile))

                    Get vFilePathExists (sProgramPath + sProgramFile) to bExists
                    // If program compilation failed:
                    If (bExists = False) Begin
                        Send Info_Box ("The compilation of the" * (CS_DFRefactor + ".src program") * "failed and can't be started. Please check it with the Studio.")
                    End
                    Else Begin
                        Runprogram Background sProgramFile
                    End
                End_Procedure
            
            End_Object

            // ToDo: Not sure this is useful in this program. It can be
            //       started from the DFRefactor program though.
//            Object oCodeExplorer_ToolbarButton is a cCJMenuItem
//                Set peControlStyle to xtpButtonIconAndCaption
//                Set psCaption to "DataFlex Source &Explorer"
//                Set psToolTip to "DataFlex Source Explorer"
//                Set psDescription to "Run Starzen's DataFlex Source Explorer (Alt+E)"
//                Set psImage to "DataFlexSourceExplorer.ico"
//                Set psShortcut to "Alt+E"
//                Set pbActiveUpdate to True
//
//                Procedure OnExecute Variant vCommandBarControl
//                    String sProgram sSWSPath
//
//                    Get private.psStarZenSourceExplorer of ghoApplication to sProgram
//                    If (sProgram = "") Begin
//                        Send Info_Box "You first need to setup the path to StarZen's Source Code Explorer program with 'Program Settings'"
//                        Procedure_Return
//                    End
//                    Get psSWSFile of ghoApplication to sSWSPath
//                    If (sSWSPath <> "") Begin
//                        Move ("-ws" * '"' + sSWSPath + '"') to sSWSPath
//                    End
//                    // This doesn't quite work because the program is expecting a .sws file,
//                    // not a .ws file. And we don't know the .sws file in this program.
//                    Runprogram Background sProgram //sSWSPath
//                End_Procedure
//
//                Function IsEnabled Returns Boolean
//                    String sProgram sSWSPath
//                    Get psStarZenSourceExplorer of ghoApplication to sProgram
//                    Get psSWSFile of ghoApplication to sSWSPath
//                    Function_Return (sProgram <> "" and sSWSPath <> "")
//                End_Function
//
//            End_Object

            Object oSettings_Toolbarbutton is a cCJMenuItem
                Set psToolTip to "Program Settings"
                Set psCaption to "Se&ttings"
                Set psDescription to "Program Settings (Alt+T)"
                Set psImage to "Settings.ico"
                Set peControlStyle to xtpButtonIcon
                Set pbControlBeginGroup to True
                Set psShortcut to "Alt+T"

                Procedure OnExecute Variant vCommandBarControl
                    Forward Send OnExecute vCommandBarControl
                    Send Popup of (oProgramSetup_dg(Client_Id(ghoCommandBars)))
                    Broadcast Recursive Send DoChangeFontSize of (Client_Id(ghoCommandBars))
                End_Procedure

            End_Object

            Object oScintillaEditorSettings_Mentuitem is a cCJMenuItem
                Set psToolTip to "Editor Settings"
                Set psCaption to "E&ditor Settings"
                Set psDescription to "Configure the editor"
                Set psImage to "EditorSettings.ico"
                Set peControlStyle to xtpButtonIcon

                Procedure OnExecute Variant vCommandBarControl
                    Forward Send OnExecute vCommandBarControl
                    Send Popup of (oScintillaParameters_dg(Client_Id(ghoCommandBars)))
                End_Procedure

            End_Object

        End_Object

        Object oExit_toolbar is a cCJToolbar
            Set pbCloseable to False
            Set pbEnableAnimation to True
            Set pbShowExpandButton to False

            Object oCaptureViewMenuItem is a cCJMenuItem
                Set psCaption to "Take shapshot"
                Set psToolTip to "Take shapshot"
                Set psDescription to "Take a snapshot image of the current view/panel. (F10)"
                Set psImage to "Camera.ico"

                Procedure OnExecute Variant vCommandBarControl
                    Send TakeSnapshot False True
                End_Procedure
            End_Object

            Object oAbout_MenuItem is a cCJAboutMenuItem
            End_Object

            Object oHelpMenu is a cCJMenuItem
                Set psDescription to "Help about the program (F1)"
                Set psToolTip to "Help"
                Set psImage to "ActionHelp.ico"
                Set peControlType to xtpControlSplitButtonPopup

                // Default action when clicking the drop-down button.
                Procedure OnExecute Variant vCommandBarControl
                    Send Help of (Focus(Self))
                End_Procedure

                Object oHelpMenuItemLocal is a cCJHelpMenuItem
                    Set psCaption to "Local HTML Help"
                End_Object

                Object oHelpMenuItemInternetBrowser is a cCJMenuItem
                    Set psCaption to "Online HTML Help"
                    Set psImage to "ActionHelpInternet.ico"
                    Procedure OnExecute Variant vCommandBarControl
                        Runprogram Shell Background "https://www.rdctools.com/HTMLHelpDFRefactor/DFRefactor - Automated Code Refactoring for DataFlex.html"
                    End_Procedure

                End_Object

                Object oHelpGitHub is a cCJMenuItem
                    Set psCaption to "GitHub source code repository"
                    Set psToolTip to "Link to GitHub source code repository"
                    Set psDescription to "Github source code"
                    Set psImage to "GitHub.ico"
                    Set pbControlBeginGroup to True
                    Procedure OnExecute Variant vCommandBarControl
                        Forward Send OnExecute vCommandBarControl
                        Runprogram Shell Background "https://github.com/NilsSve/DFRefactor.git"
                    End_Procedure
                End_Object
                
            End_Object

            Object oExitMenuItem is a cCJExitMenuItem
                Set psImage to "ActionExit.ico"
                Set psToolTip to "Exit"
                Set psDescription to "Exit the program (Alt+F4)"
                Set pbControlBeginGroup to True
            End_Object

        End_Object

        Object oStatusBar is a cCJStatusBar
            Set StatusBar_Id to Self

            Object oStatusPane1 is a cCJStatusBarPane
                Set piID to sbpIDIdlePane
                Set pbStyleStretch to True
            End_Object

            Object oStatusPane2 is a cCJStatusBarPane
                Set phoViewPane to Self
                Set pbStyleStretch to True
            End_Object

            Object oStatusPane3 is a cCJStatusBarPane
//                Set psText to "Current Action:"
            End_Object

            Object oNumberOfLines_StatusbarPane is a cCJStatusBarPane
                Set psText to "No of Lines:"
            End_Object

            Object oNumberOfCharacters_StatusbarPane is a cCJStatusBarPane
                Set psText to "Characters:"
            End_Object

            Procedure Set NumberOfEditorLines String sText
                Move (FormatValue(sText, ",###")) to sText
                Set psText of oNumberOfLines_StatusbarPane to ("No of Lines:" * String(sText))
            End_Procedure

            Procedure Set NumberOfEditorCharacters String sText
                Move (FormatValue(sText, ",###")) to sText
                Set psText of oNumberOfCharacters_StatusbarPane to ("Characters:" * String(sText))
            End_Procedure

            Procedure Set ActionText String sText
                Handle hoViewPane
                Get phoViewPane to hoViewPane
                Set psText of hoViewPane to sText
            End_Procedure

            Procedure Set CurrentActionText String sText
                Handle hoViewPane
                Move (oStatusPane3(Self)) to hoViewPane
                Set psText of hoViewPane to sText
            End_Procedure
            
        End_Object
        
    End_Object

    Object oClientArea is a ClientArea
        Use ScintillaParameters.dg
        Use CompileErrors.dg
        Use ErrorLog.dg
        Use LogFileDialog.dg
        Use ProgramSetup.dg      
        Use ShowOtherLogFiles.dg
        Use UnusedSourceFiles.dg

        Use RefactorTestBench.vw
        Use SelectFunctions.vw
        Use FunctionMaintenance.vw
        Use FunctionExportImport.vw
        //Use SortSourceCode.vw

        // Sorting function for package fileïs Classes, Procedures & Functions.
        
        Use StdAbout.pkg
        Procedure Activate_About 
            String sAlphaTesters
            Move  ("Author: Nils Svedmyr" + CS_CRLF) to sAlphaTesters
               Append sAlphaTesters " " "Alpha Testers:"         CS_CRLF
               Append sAlphaTesters " " "  - Sture Andersen"     CS_CRLF
               Append sAlphaTesters " "  " - Samuel Pizarro"     CS_CRLF
               Append sAlphaTesters " "  " - Yannick Lucassen"   CS_CRLF
               Append sAlphaTesters " "  " - Marco Kuipers"      CS_CRLF
               Append sAlphaTesters " "  " - Raveen Sundram"     CS_CRLF
               Append sAlphaTesters " "  " - Anderson Rodriquez"
            Send DoAbout "" "" "" sAlphaTesters "DFRefactorTest72x72.bmp" "http://www.rdctools.com" "http://vdf-guidance.com"
        End_Procedure
    
        // We doesn't allow closing of a view - it just doesn't make
        // sense with a tabbed interface.
        Function Exit_Loss_Confirmation for cUIObject Returns Integer
            Function_Return 1
        End_Function

        On_Key Key_Ctrl+Key_W  Send Execute of (oToggleWhiteSpaceItm(ghoCommandBars))
        On_Key Key_Ctrl+Key_I  Send Execute of (oToogleIndentationGuides(ghoCommandBars))
        On_Key Key_Ctrl+Key_S  Send Execute of (oSave_ToolItem(ghoCommandBars))
        On_Key Key_Ctrl+Key_R  Send Execute of (oRefresh_ToolItem(ghoCommandBars))
        On_Key Key_Ctrl+Key_F5 Send Execute of (oRefactor_ToolItem(ghoCommandBars))
        On_Key Key_Ctrl+Key_M  Send Execute of (oCompare_MenuItem(ghoCommandBars))
        On_Key Key_F5          Send Execute of (oCompile_ToolItem(ghoCommandBars))
        On_Key Key_Ctrl+Key_E  Send Execute of (oErrorLog_ToolItem(ghoCommandBars))
        On_Key Key_Ctrl+Key_U  Send Execute of (oUnitTest_MenuItem(ghoCommandBars))
        On_Key Key_Ctrl+Key_O  Send OpenContainingContainer of ghoApplication
        On_Key Key_Ctrl+Key_Tab           Send Switch_Next_View
        On_Key Key_Ctrl+Key_Shift+Key_Tab Send Switch_Prior_View
    End_Object
                                
    // This will exit the application regardless if there is a change or not.
    // The reason for this is that there seems to be a focus issue with the app.
    // If we activate the "Changes Exists. Do you want to exit?" dialog and answer "No",
    // the app will seem to be hung, aka it doesn't respond to input any more because the
    // focus tree is fonfused.
    Function Verify_Exit_Application Returns Integer
        Function_Return 0
    End_Function

End_Object

Send Switch_Prior_View of (Client_Id(phoMainPanel(ghoApplication)))

Start_UI

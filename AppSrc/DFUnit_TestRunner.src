Use DFAllEnt.pkg                  
Use DFUnit.pkg  
Use cRefactorApplication.pkg

// Dummy proc.
Procedure DFUnitRegisterFixture Handle hFixture
End_Procedure

Object oApplication is a cRefactorApplication
    Set psCompany to "RDC Tools International"
    Set psProduct to "DF-Unit Tester"  
    Set pbPreserveEnvironment to True   
End_Object

Object oMain is a Panel
    Set Label to "DF-Unit TestRunner"
    Set Size to 499 378
    Set Location to 94 200
    Set piMinSize to 300 250 
    Set Border_Style to Border_Thick 
    Set peAnchors to anAll
    
    { Visibility=Private }
    Property Handle phoOutputBox

    Object oCJCommandBarSystem is a cCJCommandBarSystem
        Set peVisualTheme to xtpThemeOffice2016Publisher

        Procedure OnCreateCommandBars
            Send SetTheTheme xtpThemeOffice2016Publisher 
        End_Procedure

        Procedure SetTheTheme Integer eTheme  
            Get ReadInteger of ghoApplication CS_Settings CS_ThemeKeyWord eTheme to eTheme
            Set peVisualTheme to eTheme
            Send ComRecalcLayout
        End_Procedure

        Object oCJStatusBar is a cCJStatusBar
            Set StatusBar_Id to Self

            Object oStatusPane1 is a cCJStatusBarPane
                Set piID to sbpIDIdlePane
                Set pbStyleStretch to True
            End_Object

            Object oStatusPane2 is a cCJStatusBarPane
                Set phoViewPane to Self
                Set pbStyleStretch to True
            End_Object

            Object oUTF8_OEM_Text is a cCJStatusBarPane
                Set psText to "OEM"
            End_Object

            Object oNumberOfLines_StatusbarPane is a cCJStatusBarPane
            End_Object

            Object oNumberOfCharacters_StatusbarPane is a cCJStatusBarPane
            End_Object

            Procedure Set NumberOfEditorLines String sText
            End_Procedure

            Procedure Set NumberOfEditorCharacters String sText
            End_Procedure
            
            Procedure Set StatusText String sText
                Set psText of oStatusPane1 to sText
            End_Procedure                          
            
            Procedure Set ActionText String sText
                Handle hoViewPane
                Get phoViewPane to hoViewPane
                Set psText of hoViewPane to sText
            End_Procedure

            Procedure Set UTF8Text String sText
                Set psText of (oUTF8_OEM_Text(Self)) to sText
            End_Procedure
            
        End_Object 
        
    End_Object
    
    Object oClientArea is a ClientArea    
        Use ScintillaParameters.dg
        Use DFUnitTestRunner.vw   

        Object oTestApplication is a cDFUnitTestApplication
            Use ScintillaParameters.dg
            Use DeleteWorkspaceHistory.dg  
            
            // *** UNIT TESTS GOES HERE ***
            Use oUnit_Tests.pkg    
            // This is needed to display the source line number for the
            // failed testfixture procedure:
            Set psUnitTestTestingPackage to "oUnit_Tests.pkg"
            
            { Published=True }
            Procedure ExpectingAnError
                Send ExpectError DFERR_PROGRAM
                Error DFERR_PROGRAM "An expected error."
                Send UnExpectError DFERR_PROGRAM
            End_Procedure 
            
            Procedure End_Construct_Object             
                Handle hoOutputBox
                
                Delegate Get phoOutputBox to hoOutputBox
                If (hoOutputBox <> 0) Begin
                    Send AddReporter hoOutputBox
                End
                Forward Send End_Construct_Object
            End_Procedure

        End_Object

        Procedure End_Construct_Object             
            Boolean bAutoRun

            Forward Send End_Construct_Object
            Get pbAutoRun of ghoTestApplication to bAutoRun
            If (bAutoRun = True) Begin
                Send AutoRun of ghoTestApplication
            End
        End_Procedure

        On_Key Key_Ctrl+Key_F4 Send None
        On_Key Key_Escape      Send None
    End_Object
    
End_Object

Procedure Startup
    Boolean bUseUI bUseUIIfInDebugger
    Handle hoMainView 
    Integer iIsFirstTime
    
    Get pbUseUI of ghoTestApplication to bUseUI
    Get pbUseUIIfInDebugger of ghoTestApplication to bUseUIIfInDebugger
    If (bUseUI = True or (IsDebuggerPresent() and bUseUIIfInDebugger = True)) Begin
        Get phoRefactorView of ghoApplication to hoMainView
        If (hoMainView <> 0) Begin
            Send Activate_DFUnitTestRunner_vw of (Client_id(ghoCommandBars))
            Get ReadInteger of ghoApplication CS_Settings "IsFirstTime" 0 to iIsFirstTime
            If (iIsFirstTime = 0) Begin
                // Can only set to zoom the first time the program is started, before the
                // the sizing is saved to the registry, else the whole program is zoomed when started.
                // Need to set the view_mode after the view has been activated.
                Set View_Mode of (DFUnitTestRunner_vw(Client_Id(ghoCommandBars))) to Viewmode_Zoom
                Send WriteInteger of ghoApplication CS_Settings "IsFirstTime" 1
            End
            Send ManualRunTests of ghoTestApplication
        End
        Start_UI
    End
End_Procedure
Send Startup

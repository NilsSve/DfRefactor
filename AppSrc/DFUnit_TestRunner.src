Use DFAllEnt.pkg                  
Use cRefactorApplication.pkg
Use DFUnit.pkg  

// Dummy proc.
Procedure DFUnitRegisterFixture Handle hFixture
End_Procedure

Object oApplication is a cRefactorApplication
    Set psCompany to "RDC Tools International"
    Set psProduct to "DF-Unit Tester"  
    Set pbPreserveEnvironment to True   
    
    // These dummy messages are needed by the DFRefactor functions:
    Procedure DisplayPerfCounters
    End_Procedure

    Function StartPerfCounter Integer eOperation String sComment Returns Integer
        Function_Return 0
    End_Function

    Procedure StopPerfCounter Integer iCounter
    End_Procedure

    { MethodType=Property }
    Function psSWSFile Returns String
        Function_Return (psWorkspaceWSFile(phoWorkspace(ghoApplication)))
    End_Function   
    
    Procedure UpdateSourceFileNameDisplay String sFileName
    End_Procedure 
    
End_Object

Object oMain is a Panel
    Set Label to "DF-Unit TestRunner"
    Set Size to 543 305
    Set Location to 50 200
    Set piMinSize to 300 250 
    Set Border_Style to Border_Thick 
    Set peAnchors to anAll
    
    { Visibility=Private }
    Property Handle phoTestView
    Property Handle phoOutputBox

    Object oCJCommandBarSystem is a cCJCommandBarSystem
        Object oCJStatusBar is a cCJStatusBar
            Set StatusBar_Id to Self

            Object oStatusPane1 is a cCJStatusBarPane
                Set piID to sbpIDIdlePane
                Set pbStyleStretch to True
            End_Object

            Object oStatusPane2 is a cCJStatusBarPane
                Set phoViewPane to Self
                Set pbStyleStretch to True
            End_Object

            Object oStatusPane3 is a cCJStatusBarPane
            End_Object

            Object oNumberOfLines_StatusbarPane is a cCJStatusBarPane
            End_Object

            Object oNumberOfCharacters_StatusbarPane is a cCJStatusBarPane
            End_Object

            Procedure Set NumberOfEditorLines String sText
            End_Procedure

            Procedure Set NumberOfEditorCharacters String sText
            End_Procedure

            Procedure Set ActionText String sText
                Handle hoViewPane
                Get phoViewPane to hoViewPane
                Set psText of hoViewPane to sText
            End_Procedure

        End_Object 
        
    End_Object
    
    Object oClientArea is a ClientArea    
        Use ScintillaParameters.dg
        Use DFUnitTestRunner.vw   

        Object oTestApplication is a cDFUnitTestApplication
            Use ScintillaParameters.dg
            Use oRefactor_Unit_Tests.pkg    
            // This is needed to display the source line number for the
            // failed testfixture procedure:
            Set psUnitTestTestingPackage to "oRefactor_Unit_Tests.pkg"
            
            { Published=True }
            Procedure ExpectingAnError
                Send ExpectError DFERR_PROGRAM
                    Error DFERR_PROGRAM "An expected error."
                Send UnExpectError DFERR_PROGRAM
            End_Procedure 
            
            Procedure End_Construct_Object             
                Handle hoOutputBox
                
                Delegate Get phoOutputBox to hoOutputBox
                If (hoOutputBox <> 0) Begin
                    Send AddReporter hoOutputBox
                End
                Forward Send End_Construct_Object
            End_Procedure

        End_Object

        Procedure End_Construct_Object             
            Boolean bAutoRun

            Forward Send End_Construct_Object
            Get pbAutoRun of ghoTestApplication to bAutoRun
            If (bAutoRun = True) Begin
                Send AutoRun of ghoTestApplication
            End
        End_Procedure

        On_Key Key_Ctrl+Key_F4 Send None
        On_Key Key_Escape      Send None
    End_Object
    
End_Object

Procedure Startup
    Boolean bUseUI bUseUIIfInDebugger
    Handle hoMainView
    
    Get pbUseUI of ghoTestApplication to bUseUI
    Get pbUseUIIfInDebugger of ghoTestApplication to bUseUIIfInDebugger
    If (bUseUI = True or (IsDebuggerPresent() and bUseUIIfInDebugger = True)) Begin
        Get phoTestView of (phoMainPanel(ghoApplication)) to hoMainView
        If (hoMainView <> 0) Begin
            Send Activate_DFUnitTestRunner_vw of (Client_id(phoMainPanel(ghoApplication)))
            Send ManualRunTests of ghoTestApplication
        End
        Start_UI
    End
End_Procedure
Send Startup

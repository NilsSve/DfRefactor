Use DFClient.pkg
Use Windows.pkg
Use cCommandLinkButton.pkg
Use cCJStandardMenuItemClasses.pkg
Use cScrollingContainer.pkg
Use File_dlg.pkg
Use vwin32fhW.pkg
Use cRefactorApplication.pkg
Use cRDCModalPanel.pkg
Use cLinkLabel.pkg
Use cSplitButton.pkg
Use cWebView2Browser.pkg
Use dfBitmap.pkg
Use cCJSkinFramework.pkg

Define CI_DarkTheme for (RGB(36,36,36)) // clBtnFace // (RGB(36,36,36))
Define CI_DarkThemeText for clWhite // clBlack // clWhite
Register_Object oSelectWorkspace_dg   

Object oCJSkinFramework is a cCJSkinFramework
    Set psSkinFile to "Office2010.cjstyles"
    Set psSkinIni to "NormalBlack.ini"
End_Object

Class cWsTextBox is a Form
    Procedure Construct_Object
        Forward Send Construct_Object
        Set Enabled_State to False
        Set Form_Border to Border_None
        Set Transparent_State to False
    End_Procedure  

    Procedure End_Construct_Object
        Forward Send End_Construct_Object
        Set Color to CI_DarkTheme
        Set TextColor to CI_DarkThemeText 
    End_Procedure
    
    Procedure Set Label String sLabel
        Set Value to sLabel
    End_Procedure
    
End_Class

// This class is designed to be inside oSelectWorkspace_dg
Class cCmdLnkButton is a cCommandLinkButton
    Procedure Construct_Object
        Forward Send Construct_Object
        Set Size to 26 216
        Set Location to 53 18    
        Set Label to 'oCommandLinkButton'
        Set psNote to "...sws file path"
        Set psImage to "Default.ico"
        Set peImageAlign to Button_ImageList_Align_Center
        // This doesn't work
        //Set piImageMarginLeft to 150
        Set TextColor to CI_DarkThemeText
        Set phoButtonPopup to (oRemoveRegValContextMenu(oPreCJCommandBarSystem(Self)))
        Set Visible_State to False 
        Set Skip_State to True
        Set piImageSize to 24
        Set MultiLineState to True
//        Set Transparent_State to False
        Set pbCenterToolTip to True
    End_Procedure
    
    Procedure End_Construct_Object
        Handle[] aObjects
        Forward Send End_Construct_Object
        Get paObjects to aObjects
        Move Self to aObjects[SizeOfArray(aObjects)]
        Set paObjects to aObjects
    End_Procedure

    Procedure OnClick
        Send OpenWorkspace
    End_Procedure
    
    Procedure Mouse_Down2 Integer iWindowNumber Integer iPosition  
        Handle hoButtonPopup
        Forward Send Mouse_Down2 iWindowNumber iPosition
        Get phoButtonPopup to hoButtonPopup
        If (hoButtonPopup <> 0) Begin
            Set phInvokingObject of (phoButtonPopup(Self)) to (Object_Id(Self))
            Send Popup of (phoButtonPopup(Self))
        End
    End_Procedure

    Procedure OpenWorkspace
        String sSWSFile sPath 
        Get psNote to sPath
        Get vFolderFormat sPath to sPath
        Get Label to sSWSFile
        Send UpdateWorkspaceSelectorDisplay of ghoApplication (sPath + String(sSWSFile))
        Send Destroy of ghoSkinFramework
        Move 0 to ghoSkinFramework
        Send Stop_UI
    End_Procedure
    
    Procedure OpenContainingFolder
        String sSWSFile sPath 
        Get psNote to sPath
        Send vShellExecute "open" "explorer.exe" sPath ""
    End_Procedure
    
    Procedure CopyPath
        String sPath
        Integer iCh
        
        Get psNote to sPath
        Get Seq_New_Channel to iCh
        Direct_Output channel iCh "clipboard:"
        Writeln channel iCh sPath
        Close_Output channel iCh
        Send Seq_Release_Channel iCh
    End_Procedure
    
    Procedure DeleteWorkspaceKey 
        Boolean bOK              
        Integer iSize iCount iItem
        String sKey sWs sPath sSWSFile
        String[] asWorkSpaces
        
        Get ReadWorkspaceKeysFromRegistry of ghoApplication to asWorkspaces
        Get Label to sSWSFile
        Get psNote to sPath
        Get vFolderFormat sPath to sPath
        Move (sPath + sSWSFile) to sWs
        Move (SearchArray(sWs, asWorkSpaces)) to iItem
        If (iItem = -1) Begin
            Send Info_Box "Couldn't find the registry key. Workspace not deleted."
            Procedure_Return
        End
        
        Move (RemoveFromArray(asWorkSpaces, iItem)) to asWorkSpaces
        Get piWorkspaceItems of ghoApplication to iSize  
        // 1. Clear all "RecentNN" registry key values.
        For iCount from 0 to iSize   
            Send WriteString of ghoApplication CS_WorkspacesKey (CS_WorkspaceRecentKey + String(iCount)) ""
        Loop
        
        // 2. Write the remaining asWorkspace values to the registry:
        Move (SizeOfArray(asWorkSpaces)) to iSize
        Decrement iSize
        For iCount from 0 to iSize
            Send WriteString of ghoApplication CS_WorkspacesKey (CS_WorkspaceRecentKey + String(iCount)) asWorkSpaces[iCount]
        Loop
    End_Procedure

End_Class                            

Object oSelectWorkspace_dg is a ModalPanel
    Set Label to "Select Workspace"
    Set Size to 317 551
    Set piMinSize to 300 500
    Set Location to 6 6
    Set Border_Style to Border_Thick
    Set Locate_Mode to CENTER_ON_SCREEN
    Set Icon to "DFRefactor.ico"
    Set Color to CI_DarkTheme     
    //    Set Caption_Bar to False       
    //    Set Sysmenu_Icon to False

    Property Handle[] paObjects


    Object oScrollingContainer is a cScrollingContainer
        Object oScrollingClientArea is a cScrollingClientArea
            Set Color to clBlack 
            Set Color to CI_DarkTheme
            Set pbAutoSetTabWorkspaceView to False

            Object oPreCJCommandBarSystem is a cCJCommandBarSystem

                Object oRemoveRegValContextMenu is a cCJContextMenu
                    Set phoCommandBars to (oPreCJCommandBarSystem(Self))

                    Property Handle phInvokingObject 0
                    Property Handle phInvokingItem 0
        
                    Object oOpenItem is a cCJMenuItem
                        Set psCaption     to "Open Workspace"
                        Set psToolTip     to "Open selected workspace"
                        Set psDescription to "Open selected workspace"
                        Set psImage       to "ActionOpen.ico"
                        Set psCategory    to C_$CategoryOther
                        
                        Procedure OnExecute Variant vCommandBarControl
                            Handle hObject
                            Get phInvokingObject to hObject
                            If (hObject <> 0) Begin
                                Send OpenWorkspace to hObject
                            End
                        End_Procedure
                    End_Object
            
                    Object oOpenContainingFolder is a cCJMenuItem
                        Set psCaption     to "Open containing folder"
                        Set psToolTip     to "Open containing folder"
                        Set psDescription to "Open containing folder"
                        Set psImage       to "ActionOpenContainingFolder.ico"
                        Set psCategory    to C_$CategoryOther
                        
                        Procedure OnExecute Variant vCommandBarControl
                            Handle hObject
                            Get phInvokingObject to hObject
                            If (hObject <> 0) Begin
                                Send OpenContainingFolder to hObject
                            End
                        End_Procedure
                    End_Object
            
                    Object oCopyPathItem is a cCJMenuItem
                        Set psCaption     to "Copy path"
                        Set psToolTip     to "Copy workspace path"
                        Set psDescription to "Copy workspace path"
                        Set psImage       to "ActionCopy.ico"
                        Set psCategory    to C_$CategoryOther
                        
                        Procedure OnExecute Variant vCommandBarControl
                            Handle hObject
                            Get phInvokingObject to hObject
                            If (hObject <> 0) Begin
                                Send CopyPath to hObject
                            End
                        End_Procedure
                    End_Object
            
                    Object oRemoveItem is a cCJMenuItem
                        Set psCaption     to "Remove from list"
                        Set psToolTip     to "Remove workspace from list"
                        Set psDescription to "Remove workspace from list"
                        Set psImage       to "ActionDelete.ico"
                        Set psCategory    to C_$CategoryOther
                        
                        Procedure OnExecute Variant vCommandBarControl
                            Handle hObject
                            Get phInvokingObject to hObject
                            If (hObject <> 0) Begin
                                Send DeleteWorkspaceKey to hObject
                                Set Visible_State of hObject to False       
                                Send Info_Box "Workspace item removed. (The display will get refreshed automatically the next time the program is started.)"
                            End
                        End_Procedure
                    End_Object
            
                    Object oClosePanelItem is a cCJMenuItem
                        Set pbControlBeginGroup to True
                        Set psCaption     to "Close Panel"
                        Set psToolTip     to "Close Panel"
                        Set psDescription to "Close Panel" 
                        Set psImage       to "ActionClose.ico"
                        Set psCategory    to C_$CategoryOther
                        
                        Procedure OnExecute Variant vCommandBarControl
                            Send Exit_Application
                        End_Procedure
                    End_Object

//                    Object oStatusBar is a cCJStatusBar
//                        Object oStatusPane1 is a cCJStatusBarPane
//                            Set piId to sbpIDIdlePane
//                            Set pbStyleStretch to True
//                        End_Object
//                    End_Object

                End_Object   
                
            End_Object

            Object oProgram_tb is a cWsTextBox
                Set Size to 15 259
                Set Location to 3 14
                Set FontPointHeight to 18  
                Set TextColor to clBlack
//                Set Label to "Program Name 2024"
                Set Value to "Program Name 2024"
                
                Procedure OnCreate
                    Handle hoVersionInfo                   
                    Integer iMajorVersion
                    Get phoVersionInfo of ghoApplication to hoVersionInfo
                    Get piVersionMajor of hoVersionInfo  to iMajorVersion
                    Set Label to (psProduct(ghoApplication) * String(iMajorVersion))
                End_Procedure     
                Send OnCreate
            End_Object

            Object oOpenRecent_tb is a cWsTextBox
                Set Size to 13 57
                Set Location to 37 20
                Set Label to "Open recent"
                Set FontPointHeight to 14
            End_Object

            Object oCommandLinkButton1 is a cCmdLnkButton
                Set Size to 47 234
                Set Location to 63 24
                Set Label to 'oCommandLinkButton1'
            End_Object

            Object oCommandLinkButton2 is a cCmdLnkButton
                Set Size to 47 234
                Set Location to 114 24
                Set Label to 'oCommandLinkButton2'
            End_Object

            Object oCommandLinkButton3 is a cCmdLnkButton
                Set Size to 47 234
                Set Location to 165 24
                Set Label to 'oCommandLinkButton3'
            End_Object

            Object oCommandLinkButton4 is a cCmdLnkButton
                Set Size to 47 234
                Set Location to 216 24
                Set Label to 'oCommandLinkButton4'
            End_Object

            Object oCommandLinkButton5 is a cCmdLnkButton
                Set Size to 47 234
                Set Location to 267 24
                Set Label to 'oCommandLinkButton5'
            End_Object

            Object oCommandLinkButton6 is a cCmdLnkButton
                Set Size to 47 234
                Set Location to 318 24
                Set Label to 'oCommandLinkButton6'
            End_Object

            Object oCommandLinkButton7 is a cCmdLnkButton
                Set Size to 47 234
                Set Location to 369 24
                Set Label to 'oCommandLinkButton7'
            End_Object

            Object oCommandLinkButton8 is a cCmdLnkButton
                Set Size to 47 234
                Set Location to 420 24
                Set Label to 'oCommandLinkButton8'
            End_Object

            Object oCommandLinkButton9 is a cCmdLnkButton
                Set Size to 47 234
                Set Location to 471 24
                Set Label to 'oCommandLinkButton9'
            End_Object

            Object oCommandLinkButton10 is a cCmdLnkButton
                Set Size to 47 234
                Set Location to 522 24
                Set Label to 'oCommandLinkButton10'
            End_Object

            Object oGetStarted_tb is a cWsTextBox
                Set Size to 15 56
                Set Location to 37 276
                Set Label to "Get started"
                Set FontPointHeight to 14
                Set peAnchors to anTopRight
            End_Object

            Object oOpenLocalFolder_lb is a cCmdLnkButton
                Set Size to 47 234
                Set Location to 64 276
                Set Label to "Open a project"
                Set MultiLineState to True
                Set piImageSize to 32
                Set psImage to "ActionOpen.ico"
                Set psNote to "Open a Studio workspace file (*.sws)"
                Set psToolTip to "Open a *.sws or *.ws file"
                Set Visible_State to True
                Set Skip_State to False
                Set peAnchors to anTopRight
                Set phoButtonPopup to 0
                
                Procedure OnClick
                    Handle hoDialog 
                    Boolean bOpen
                    String sPath sFileName sSWSFile sExt
                    String[] asSelectedFiles
            
                    Get Create (RefClass(OpenDialog)) to hoDialog 
                    Set Dialog_Caption    of hoDialog to "Please select a Studio Workspace File (*.sws)" 
                    Set Filter_String     of hoDialog to "Studio Workspace Files (*.sws)|*.sws|Workspace Files (*.ws)|*.ws|All Files (*.*)|*.*"
                    Set MultiSelect_State of hoDialog to False
                    Get Show_Dialog       of hoDialog to bOpen
                    If (bOpen = False) Begin     
                        Send Destroy of hoDialog
                        Procedure_Return
                    End

                    Get Selected_Files of hoDialog to asSelectedFiles
                    Move asSelectedFiles[0] to sFileName
                    Get ParseFileExtension sFileName to sExt
                    If (Lowercase(sExt) <> "sws" and Lowercase(sExt) <> "ws") Begin
                        Send Info_Box "You must select a *.sws or *.ws file. Please try again."
                        Procedure_Return
                    End
                    Send Destroy of hoDialog

                    Send UpdateWorkspaceSelectorDisplay of ghoApplication sFileName
                    Send AddWorkSpaceFileToRegistry     of ghoApplication sFileName   
                    Send Destroy of ghoSkinFramework
                    Move 0 to ghoSkinFramework
                    Send Stop_UI
                End_Procedure 

            End_Object

//            Object oExit_tb is a cWsTextBox
//                Set Size to 15 56
//                Set Location to 237 276
//                Set Label to "Exit"
//                Set FontPointHeight to 14
//                Set peAnchors to anTopRight
//            End_Object
//
//            Object oClose_lb is a cCmdLnkButton
//                Set Size to 47 234
//                Set Location to 267 276
//                Set Label to "Close"
//                Set MultiLineState to True
//                Set piImageSize to 32
//                Set psImage to "ActionExit.ico"
//                Set psNote to "End program"
//                Set psToolTip to "Close program"
//                Set Visible_State to True
//                Set Skip_State to False
//                Set peAnchors to anTopRight
//                Set phoButtonPopup to 0
//                
//                Procedure OnClick   
//                    Send Exit_Application
//                End_Procedure 
//
//            End_Object

        End_Object

    End_Object

    Procedure Add_Focus Handle hoParent                       
        String[] asWorksSpaces
        String sObjektName
        Handle[] aObjects           
        Handle hoButton
        String   sFileName sSWSFile sPath sIconFile
        Integer iSize iCount iObject iOffset iLocation
        
        Set Icon to "DFRefactor.ico"

        Forward Send Add_Focus hoParent
        Get paObjects to aObjects
        Get ReadWorkspaceKeysFromRegistry of ghoApplication to asWorksSpaces
        Move (SizeOfArray(asWorksSpaces)) to iSize
        Decrement iSize
        
        For iCount from 0 to iSize
            Move asWorksSpaces[iCount]     to sFileName
            Get ParseFolderName sFileName  to sPath
            Get ParseFileName   sFileName  to sSWSFile 
            Set Label of aObjects[iCount]  to sSWSFile
            Set psNote of aObjects[iCount] to sPath
            Get AppIconFromSWSFileName of ghoApplication sFileName to sIconFile
            Set psImage of aObjects[iCount] to sIconFile
            Set Visible_State of aObjects[iCount] to True    
            Set Skip_State    of aObjects[iCount] to False
        Loop
    End_Procedure

    Procedure Close_Panel
        Send Exit_Application                                          
    End_Procedure

    On_Key kCancel Send Close_Panel
End_Object


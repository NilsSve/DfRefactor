// Cuortesy of Vincent Oorsprong, DAE
// Subclassed Windows treeview that contains the code to extract the contents in a 
// JSON Object and you can call / test that by adding
//
//Sample:
//
//On_Key Key_Alt+Key_F1 Send CollectTreeData
//
//Procedure CollectTreeData
//    Handle hoJson
//    String sJson
//
//    Get ConvertToJSON to hoJson
//    // Do whatever you want to do with the JSON object
//    // for example make it a string and write to disk?
//    Set peWhiteSpace of hoJson to jpWhitespace_Pretty
//    Get Stringify of hoJson to sJson
//End_Procedure


Use Dftreevw.pkg
Use cJsonObject.pkg

Class cTreeView is a TreeView
    Procedure AddDataToJson Handle hItem Handle hoJSON Integer iRecurseLevel
        Handle hoJSONTreeNode hChildItem hoJSONTreeNodeChild
        String sItemLabel
        Integer iItemData iItemImage iItemSelectedImage
        Boolean bItemBold bItemHasCheckbox bItemChecked bItemExpanded

        Get ItemLabel hItem to sItemLabel
        Get ItemData hItem to iItemData
        Get ItemImage hItem to iItemImage
        Get ItemBold hItem to bItemBold
        Get ItemExpandedState hItem to bItemExpanded
        Get ItemSelectedImage hItem to iItemSelectedImage
        Get ItemCheckBox hItem to bItemHasCheckbox
        If (bItemHasCheckbox) Begin
            Get ItemChecked hItem to bItemChecked
        End

        Get Create (RefClass(cJsonObject)) to hoJSONTreeNode
        Send InitializeJsonType of hoJSONTreeNode jsonTypeObject

        Send SetMemberValue of hoJSONTreeNode "Item" jsonTypeInteger hItem
        Send SetMemberValue of hoJSONTreeNode "ItemLabel" jsonTypeString sItemLabel
        Send SetMemberValue of hoJSONTreeNode "ItemData" jsonTypeInteger iItemData
        Send SetMemberValue of hoJSONTreeNode "ItemImage" jsonTypeInteger iItemImage
        Send SetMemberValue of hoJSONTreeNode "ItemSelectedImage" jsonTypeInteger iItemSelectedImage
        Send SetMemberValue of hoJSONTreeNode "ItemBold" jsonTypeBoolean bItemBold
        Send SetMemberValue of hoJSONTreeNode "ItemHasCheckbox" jsonTypeBoolean bItemHasCheckbox
        Send SetMemberValue of hoJSONTreeNode "ItemChecked" jsonTypeBoolean bItemChecked
        Send SetMemberValue of hoJSONTreeNode "ItemExpanded" jsonTypeBoolean bItemExpanded

        Send AddMember of hoJson hoJSONTreeNode

        Get ChildItem hItem to hChildItem
        If (hChildItem <> 0) Begin
            Get Create (RefClass (cJsonObject)) to hoJSONTreeNodeChild
            Send InitializeJsonType of hoJSONTreeNodeChild jsonTypeArray
            Send SetMember of hoJSONTreeNode "Children" hoJSONTreeNodeChild
            While (hChildItem > 0)
                Send AddDataToJson hChildItem hoJSONTreeNodeChild (iRecurseLevel + 1)
                Get NextSiblingItem hChildItem to hChildItem
            Loop
            Send Destroy of hoJSONTreeNodeChild
        End

        Send Destroy of hoJSONTreeNode
    End_Procedure

    Procedure CreateTreeNodes Handle hoJson Handle hRootItem Integer iRecurseLevel
        Integer iMembers iMember eJsonType iItemData iItemImage iItemSelectedImage
        Handle hoJsonMember hItem hoChildrenMember
        String sItemLabel
        Boolean bItemBold bItemHasCheckbox bItemChecked bHasChildrenMember bItemExpanded

        Get MemberCount of hoJson to iMembers
        If (iMembers > 0) Begin
            Decrement iMembers
            For iMember from 0 to iMembers
                Move 0 to hItem
                Get MemberByIndex of hoJson iMember to hoJsonMember
                If (hoJsonMember <> 0) Begin
                    Get JsonType of hoJsonMember to eJsonType
                    If (eJsonType = jsonTypeObject) Begin
                        Get MemberValue of hoJsonMember "ItemLabel" to sItemLabel
                        Get MemberValue of hoJsonMember "ItemData" to iItemData
                        Get MemberValue of hoJsonMember "ItemImage" to iItemImage
                        Get MemberValue of hoJsonMember "ItemSelectedImage" to iItemSelectedImage
                        Get MemberValue of hoJsonMember "ItemHasCheckbox" to bItemHasCheckbox
                        Get MemberValue of hoJsonMember "ItemChecked" to bItemChecked
                        Get MemberValue of hoJsonMember "ItemBold" to bItemBold
                        Get MemberValue of hoJsonMember "ItemExpanded" to bItemExpanded

                        Get AddTreeItem sItemLabel hRootItem iItemData iItemImage iItemSelectedImage to hItem
                        If (hItem <> 0) Begin
                            Set ItemBold hItem to bItemBold
                            Get HasMember of hoJsonMember "Children" to bHasChildrenMember
                            If (bHasChildrenMember) Begin
                                Get Member of hoJsonMember "Children" to hoChildrenMember
                                Send CreateTreeNodes hoChildrenMember hItem (iRecurseLevel + 1)
                            End
                            If (bItemExpanded) Begin
                                Send DoExpandItem hItem
                            End
                        End
                    End
                End
            Loop
        End
    End_Procedure

    Procedure LoadFromJson String sJson
        Handle hoJson
        Boolean bOk
        Integer eJsonType

        Get Create (RefClass (cJsonObject)) to hoJSON
        Get ParseString of hoJson sJson to bOk
        If (bOk) Begin
            Send ClearAll
            Get JsonType of hoJson to eJsonType
            If (eJsonType = jsonTypeArray) Begin
                Send CreateTreeNodes hoJson 0 0
            End
        End
    End_Procedure

    Function ConvertToJSON Returns Handle
        Handle hoJSON hRootItem

        Get Create (RefClass (cJsonObject)) to hoJSON
        Send InitializeJsonType of hoJson jsonTypeArray

        Get RootItem to hRootItem
        While (hRootItem > 0)
            Send AddDataToJson hRootItem hoJSON 0
            Get NextSiblingItem hRootItem to hRootItem
        Loop

        Function_Return hoJSON
    End_Function
End_Class

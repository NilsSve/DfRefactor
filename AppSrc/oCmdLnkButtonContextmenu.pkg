Use cCJStandardMenuItemClasses.pkg
Use cCJDeoMenuItemClasses.pkg

Register_Procedure OpenWorkspace
Register_Procedure OpenContainingFolder
Register_Procedure CopyPath 
Register_Procedure DeleteWorkspaceKey

// Note: As this cCJContextMenu menu is to be used by the pre-program
//       workspace selector, a cCJCommandBarSytem hasn't been 
//       established yet and a cCJContextMenu needs to be inside one,
//       so we also create a cCJCommandBarSystem object and destroy
//       it before closing the workspace selector panel.
Object oPreCJCommandBarSystem is a cCJCommandBarSystem
    Set peVisualTheme to xtpThemeVisualStudio2017Dark

    Object oRemoveRegValContextMenu is a cCJContextMenu
        Set phoCommandBars to (oPreCJCommandBarSystem(Self))
    
        Property Handle phInvokingObject 0
        Property Handle phInvokingItem 0
    
        Object oOpenItem is a cCJMenuItem
            Set psCaption  to "Open Workspace"
            Set psImage    to "ActionOpen.ico"
            Set psCategory to C_$CategoryOther
            
            Procedure OnExecute Variant vCommandBarControl
                Handle hObject
                Get phInvokingObject to hObject
                If (hObject <> 0) Begin
                    Send OpenWorkspace to hObject
                End
            End_Procedure
        End_Object
    
        Object oOpenContainingFolder is a cCJMenuItem
            Set psCaption  to "Open containing folder"
            Set psImage    to "ActionOpenContainingFolder.ico"
            Set psCategory to C_$CategoryOther
            
            Procedure OnExecute Variant vCommandBarControl
                Handle hObject
                Get phInvokingObject to hObject
                If (hObject <> 0) Begin
                    Send OpenContainingFolder to hObject
                End
            End_Procedure
        End_Object
    
        Object oCopyPathItem is a cCJMenuItem
            Set psCaption  to "Copy workspace path"
            Set psImage    to "ActionCopy.ico"
            Set psCategory to C_$CategoryOther
            
            Procedure OnExecute Variant vCommandBarControl
                Handle hObject
                Get phInvokingObject to hObject
                If (hObject <> 0) Begin
                    Send CopyPath to hObject
                End
            End_Procedure
        End_Object
    
        Object oRemoveItem is a cCJMenuItem
            Set psCaption  to "Remove from list"
            Set psImage    to "ActionDelete.ico"
            Set psCategory to C_$CategoryOther
            
            Procedure OnExecute Variant vCommandBarControl
                Handle hObject
                Get phInvokingObject to hObject
                If (hObject <> 0) Begin
                    Send DeleteWorkspaceKey to hObject
                    Set Visible_State of hObject to False       
                    Send Info_Box "Workspace item removed. (The display will get refreshed automatically the next time the program is started.)"
                End
            End_Procedure
        End_Object
    
        Object oClosePanelItem is a cCJMenuItem
            Set pbControlBeginGroup to True
            Set psCaption  to "Close Panel"
            Set psImage    to "ActionClose.ico"
            Set psCategory to C_$CategoryOther
            
            Procedure OnExecute Variant vCommandBarControl
                Send Exit_Application
            End_Procedure
        End_Object
    
    End_Object   
    
End_Object

Use cCJCommandBarSystem.pkg   
Use RefactorConstants.inc
Use cRefactorApplication.pkg

Class cCJWorkspaceComboMenuItem is a cCJMenuItem
    
    Procedure Construct_Object
        Forward Send Construct_Object

        Property Handle[] phoMenuItems  
        Property tWorkspace[] pWorkspaces 
        Property Handle phoWorkspaceSelectorText_MenuItem
        Property Handle phoResetWorkspace_MenuItem

        Set psToolTip to "Recent Workspaces"
        Set psImage to "ActionOpenWorkspace.ico"
        Set peControlType to xtpControlComboBox
    End_Procedure

    Procedure End_Construct_Object
        Forward Send End_Construct_Object

    End_Procedure

    // Event for when an item is selected from the comboform
    Procedure OnExecute Variant vCommandBarControl
        Handle hoCombo
        Integer iSelection
        tWorkspace[] Workspaces
        String sWorkspace sWorkspaceShort

        // Create and bind proxy control
        Get CreateProxyControl vCommandBarControl to hoCombo
        // Get the current selection
        Get ComListIndex of hoCombo to iSelection
        Decrement iSelection
        If (iSelection = 0) Begin
            Send Execute of (phoResetWorkspace_MenuItem(Self))
            Procedure_Return
        End
        
        Get pWorkspaces to Workspaces
        Move Workspaces[iSelection].sWorkspaceFull to sWorkspace
        If (sWorkspace <> CS_NoWorkspaceSelected) Begin
            Send UpdateWorkspaceSelectorDisplay of ghoApplication sWorkspace
            Set psToolTip     to CS_CurrentWorkspaceText
            Set psDescription to sWorkspace
            Set psCaption     of (phoWorkspaceSelectorText_MenuItem(Self)) to CS_CurrentWorkspaceText
            Set psDescription of (phoWorkspaceSelectorText_MenuItem(Self)) to sWorkspace
            Move Workspaces[iSelection].sWorkspace to sWorkspaceShort
            Get ComListCount of hoCombo to iSelection
            Increment iSelection
            Send ComAddItem  of hoCombo sWorkspaceShort iSelection
            Set ComListIndex of hoCombo to iSelection
        End

        // Dispose of the proxy control
        Send Destroy of hoCombo
    End_Procedure

    Procedure OnCreateControl Handle hoCombo
        Send ComAddItem of hoCombo CS_NoWorkspaceSelected 1
        Set ComListIndex of hoCombo to 1
        Set ComWidth     of hoCombo to 150
    End_Procedure
    
    Procedure FillComboList Handle hoCombo
        Integer iCount iSize iLength iTxtEntentSize iLengthShort iTxtEntentSizeShort
        String[] asWorkspaces
        tWorkspace[] Workspaces
        String sWorkspace

        Get ReadWorkspaceKeysFromRegistry of ghoApplication to asWorkspaces
        Move (InsertInArray(asWorkspaces, 0, CS_NoWorkspaceSelected)) to asWorkspaces
        Move (SizeOfArray(asWorkspaces)) to iSize
        Decrement iSize
        Send ComClear of hoCombo

        For iCount from 0 to iSize
            Move asWorkspaces[iCount] to sWorkspace
            Move sWorkspace to Workspaces[iCount].sWorkspaceFull
            Get ParseFileName sWorkspace to sWorkspace
            Move sWorkspace to Workspaces[iCount].sWorkspace
            Send ComAddItem of hoCombo Workspaces[iCount].sWorkspaceFull (iCount + 1)
            Get Text_Extent Workspaces[iCount].sWorkspaceFull to iLength
            If (iLength > iTxtEntentSize) Begin
                Move iLength to iTxtEntentSize
            End
            Get Text_Extent Workspaces[iCount].sWorkspace to iLengthShort
            If (iLengthShort > iTxtEntentSizeShort) Begin
                Move iLengthShort to iTxtEntentSizeShort
            End
        Loop
        
        Set pWorkspaces to Workspaces
        Move (Low(iTxtEntentSize) + 40) to iSize
        Set ComDropDownWidth of hoCombo to iSize
        Set ComWidth         of hoCombo to (Low(iTxtEntentSizeShort) + 40)
        Set ComListIndex     of hoCombo to 1
    End_Procedure

    Procedure DisplayWorkspaceItem String sWorkspace
        Handle hoCombo
        tWorkspace[] Workspaces
        String sWorkspaceShort
        Integer iCount iSize iSelection
        Boolean bFound

        // Create and bind proxy control
        If (IsComObjectCreated(Self)) Begin
            Get CreateFirstProxyControl to hoCombo
            // Start by filling the combo with previously saved workspaces (if any),
            // in case the combo hasn't been activated before and a new workspace
            // is opened.
            If (hoCombo > 0) Begin
                Send FillComboList hoCombo
                
                Get pWorkspaces to Workspaces
                Move (SizeOfArray(Workspaces)) to iSize
                Decrement iSize
                Move 0 to iSelection
                Move "" to sWorkspaceShort
                
                For iCount from 0 to iSize
                    Move (Lowercase(sWorkspace) = Lowercase(Workspaces[iCount].sWorkspace)   or ;
                    Lowercase(sWorkspace) = Lowercase(Workspaces[iCount].sWorkspaceFull)) ;
                    to bFound
                    If (bFound = True) Begin
                        Move Workspaces[iCount].sWorkspace to sWorkspaceShort
                        Move iCount to iSelection
                        Move iSize to iCount // We're done
                    End
                Loop
                
                // If found; add a new combo item at the end with the short version of the workspace (no path),
                // and point the combo to that item.
                If (iSelection <> 0) Begin
                    Set psToolTip     to CS_CurrentWorkspaceText
                    Set psDescription to sWorkspaceShort
                    Set psCaption     of (phoWorkspaceSelectorText_MenuItem(Self)) to CS_CurrentWorkspaceText
                    Set psDescription of (phoWorkspaceSelectorText_MenuItem(Self)) to sWorkspace
                    Set psDescription to sWorkspace
                    Get ComListCount of hoCombo to iSelection
                    Increment iSelection
                    Send ComAddItem  of hoCombo sWorkspaceShort iSelection
                    Set ComListIndex of hoCombo to iSelection
                End
                Send Destroy of hoCombo
            End
        End
    
    End_Procedure
    
    Procedure ResetComboForm
        Handle hoCombo
        // Create and bind proxy control
        If (IsComObjectCreated(Self)) Begin
            Get CreateFirstProxyControl to hoCombo
            If (hoCombo <> 0) Begin
                Send ComClear of hoCombo
                Send ComAddItem of hoCombo CS_NoWorkspaceSelected 1
                Set ComListIndex of hoCombo to 1
                Set psCaption of (phoWorkspaceSelectorText_MenuItem(Self)) to CS_SelectWorkspaceText
                Send Destroy of hoCombo
            End
        End
    End_Procedure

    Procedure OnCreateAction
        String sSWSFile sSourceFile

        Forward Send OnCreateAction
        Get psSWSFile of ghoApplication to sSWSFile
        Get psCurrentSourceFileName         of ghoApplication to sSourceFile
        Send UpdateWorkspaceSelectorDisplay of ghoApplication sSWSFile
        Send OnFileNameUpdate of ghoApplication sSourceFile
    End_Procedure

    // Note: We re-fill the combo each time it gets "clicked".
    Procedure OnPopupInit Variant vCommandBarControl Handle hoCommandBarControls
        Handle hoCombo
        Forward Send OnPopupInit vCommandBarControl hoCommandBarControls
        Get CreateProxyControl vCommandBarControl to hoCombo
        Send FillComboList hoCombo
        Send Destroy of hoCombo
    End_Procedure

End_Class             

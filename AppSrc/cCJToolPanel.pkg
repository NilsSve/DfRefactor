// Curtesy of Wil van Antwerpen.
// The class originates from The Hammer project but was adapted to fit the DFRefactor project.
// It is used in CompileErrors.dg to display compile errors at the bottom of screen (TestBench.src)
//
Use Windows.pkg
Use cApplication.pkg
Use cCJCommandBars.pkg

{ DesignerClass=cDTToolPanel }
{ OverrideProcedureSet=Border_Style InitialValue='Border_None' }
{ OverrideProperty=Visible_State DesignTime=False }
Class cCJToolPanel is a Container3d

    Procedure Construct_Object
        Forward Send Construct_Object

        { MethodType=Property }
        Property String  psLabel ''

        { MethodType=Property InitialValue=xtpBarFloating }
        { EnumList='xtpBarTop,xtpBarBottom,xtpBarLeft,xtpBarRight,xtpBarFloating' }
        Property Integer peBarPosition xtpBarFloating

        { DesignTime=False }
        Property Handle phoCJDialogBar // Parent

        Property Boolean pbResizable True
        Set Border_Style to Border_None
    End_Procedure

    // backward compatiblity
    Procedure Set ComVisible Boolean bState
        Set Visible_State to bState
        Set pbVisible to bState
    End_Procedure

    // The ToolPanel is a 3DContainer with a cCJDialogBar presented on top.
    // Via the toolbar button we disable the 3DContainer and set it's visible state to false
    // Via the x-button the visible state of the cCJDialogBar is set to false
    Function ComVisible Returns Boolean
        Boolean bState bBarState bIsCreated
        Handle  hoCJDialogBar
    
        Move True to bBarState
        Get Visible_State to bState
        Get phoCJDialogBar to hoCJDialogBar
        If (Object_Id(hoCJDialogBar) <> 0) Begin
            Get IsComObjectCreated of hoCJDialogBar to bIsCreated
            If (bIsCreated = True) Begin
                Get ComVisible of hoCJDialogBar to bBarState
            End
        End
        Function_Return (bState and bBarState)
    End_Function

    Procedure Set pbVisible Boolean bState
        Handle  hoToolPane
        Get phoCJDialogBar to hoToolPane
        If (hoToolPane <> 0) Begin
            Set ComVisible of hoToolPane to bState
        End
    End_Procedure

    Procedure Close_Panel
        Set pbVisible to False 
    End_Procedure

    Procedure Activate Returns Integer
        Integer iErr
        Handle  hoToolPane hoScopeObj hoMain hoClient
        Boolean IsScoped

        Get Current_Scope to hoScopeObj

        If (not(Active_State(Self)) and not(IsScoped) and (hoScopeObj > Desktop)) Begin
            If (ghoApplication <> 0) Begin
                Get phoMainPanel of ghoApplication to hoMain
            End
            If (hoMain <> 0) Begin
                Get Client_Id of hoMain to hoClient
            End
            If (hoClient <> 0) Begin
                Move hoClient to hoMain
            End
            If (hoMain <> 0) Begin
                Send Activate of hoMain
            End
        End
        Forward Get msg_Activate to iErr
        
        If (iErr = 0) Begin
            Set pbVisible to True
            Send OnUpdate
        End

        Procedure_Return iErr
    End_Procedure

    {MethodType=Event}
    Procedure OnCreateToolPanel
    End_Procedure

    Procedure Create_ToolPanel
        Variant vToolPane
        String  sLabel sObjName sTitle
        Boolean bResizable
        Integer eBarPosition eDocking iSize iWidth iHeight iMaxSize iValue
        Handle  hoToolPane hoMainPanel hWnd hoClientId

        If (ghoCommandBars <> 0) Begin
            Get psLabel to sLabel
            Get Object_Label to sObjName

            Get Container_Handle to hWnd
            Get pbResizable to bResizable

            Move (sObjName + "Pane") to sObjName

            Get piMinSize to iSize
            If (iSize = 0) Begin
                Get Size to iSize
            End

            Get DialogToGui (Hi(iSize)) (Low(iSize)) to iSize
            Move (Hi(iSize))  to iHeight
            Move (Low(iSize)) to iWidth
            Get peBarPosition to eBarPosition

            Get ComAddDialogBar of ghoCommandBars sLabel eBarPosition to vToolPane
            Get CreateNamed     of ghoCommandBars (RefClass(cCJDialogBar)) sObjName to hoToolPane

            If (hoToolPane) Begin
                Set pvComObject        of hoToolPane to vToolPane
                Set ComTitle           of hoToolPane to sLabel
                Set ComChildHandle     of hoToolPane to hWnd
                Set ComVisible         of hoToolPane to False
                Set ComShowGripper     of hoToolPane to True
                Set ComResizable       of hoToolPane to bResizable
                Send ComSetSize        of hoToolPane iWidth iHeight
                Set ComShowGripper     of hoToolPane to True 
            End

            Set phoCJDialogBar to hoToolPane
            Send OnCreateToolPanel
        End
    End_Procedure

    Procedure Add_Focus Handle hoParent Returns Integer
        Integer iErr
        Handle  hoToolPane
        
        Forward Get msg_Add_Focus hoParent to iErr
        If (iErr = 0) Begin
            If (ghoCommandBars <> 0) Begin
                Send AddActiveAction of ghoCommandBars Self
            End
            Send Update
            Send OnResize
        End
        Procedure_Return iErr
    End_Procedure

    Procedure Update
        Handle  hoToolPane
        If (ghoCommandBars <> 0) Begin
            Send RemoveActiveAction of ghoCommandBars Self
        End
        Send Create_ToolPanel
        Set pbVisible to (Visible_State(Self))
    End_Procedure

    Function DEO_Object Returns Boolean
        Function_Return False
    End_Function

    Procedure Set Label String sVal
        If (phoCJDialogBar(Self) <> 0) Begin
            Set ComTitle of (phoCJDialogBar(Self)) to sVal
        End
    End_Procedure

End_Class

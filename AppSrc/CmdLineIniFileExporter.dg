Use Windows.pkg
Use File_dlg.pkg
Use cRefactorApplication.pkg
Use cIniFile.pkg
Use RefactorConstants.h.pkg
Use cRDCDbModalPanel.pkg
Use cRDCForm.pkg

Open SysFile
Open FolderSelHeaA
Open FolderSelDtlA

Object oCmdLineIniFileExporter is a cRDCDbModalPanel
    Set Label to "Export Refactor Settings to CmdLine Ini-File"
    Set Size to 218 374
    Set piMinSize to 89 211
    Set Location to 1 2  
    Set Icon to "Export.ico"

    Object oInfo_tb is a TextBox
        Set Location to 7 81
        Set Size to 37 224
        Set Auto_Size_State to False
        Set Justification_Mode to JMode_Left
        Set Label to "Dialog to export refactoring settings to an ini-file name, that can be specified on the command line to the program with the '/config' flag. The shown ini-file here is the default one, but it can be any file-name with an .ini extension and must include the full path."
    End_Object

    Object oOpenDialog is a OpenDialog 
        Set Filter_String to "Ini Files|*.ini|.sws Files|*.sws|All Files|*.*"
        Set Initial_Folder to (psHome(phoWorkspace(ghoApplication)))  
        Set MultiSelect_State to False
    End_Object 
    
    Object oIniFile_fm is a cRDCForm
        Set Location to 46 84
        Set Size to 12 223
        Set Label to "Ini-File Name:"
        Set peAnchors to anTopLeftRight
        Procedure OnStartup
            String sPath sFileName
            Get psHome of (phoWorkspace(ghoApplication)) to sPath
            Move CS_CmdIniFileName to sFileName
            Set Value to (sPath + sFileName)
        End_Procedure
    End_Object

    Object oSelect_btn is a Button
        Set Location to 46 313
        Set Label to "Select"
        Set Size to 12 50
        Set peAnchors to anTopRight
    
        Procedure OnClick
            Boolean bOpen
            String[] asFileNames
            String sFileName
            Get Show_Dialog of oOpenDialog to bOpen
            If (bOpen = True) Begin
                Get Selected_Files of oOpenDialog to asFileNames
                Move asFileNames[0] to sFileName
                Set Value of oIniFile_fm to sFileName    
            End
        End_Procedure
    
    End_Object

    Object oSWSFile_fm is a cRDCForm
        Set Location to 69 84
        Set Size to 12 223
        Set Label to ",SWS File*:"
        Set peAnchors to anTopLeftRight 
        Set Entry_State to False
        Set psToolTip to "To change .sws file, use the 'Current Workspace' combo-button in the program's toolbar."
        Procedure OnStartup
            String sSWSFile
            Get psSWSFile of ghoApplication to sSWSFile
            Set Value to sSWSFile
        End_Procedure
    End_Object

    Object oUserInfo_tb is a TextBox
        Set Location to 70 310
        Set Size to 35 59
        Set Auto_Size_State to False
        Set Justification_Mode to JMode_Left
        Set Label to "*) See the tooltips for these two fields"
    End_Object

    Object oUserName_fm is a cRDCForm
        Set Location to 83 84
        Set Size to 12 76
        Set Label to "User Name*:"
        Procedure OnStartup
            String sUserName
            String sText

            Get psUsername of ghoApplication to sUserName
            Set Value to sUserName

            Append sText "The 'User Name' is used by index.2 for the table FolderSelHea, "
            Append sText "to find a workspace Home folder. FolderSelDtl is the child table "
            Append sText "that holds the actual folder selections. So folder selections for "
            Append sText "a workspace can be different for each user."
            Set psToolTip to sText
        End_Procedure
    End_Object
    
    Object oSelectedFolders_fm is a cRDCForm
        Set Location to 97 84
        Set Size to 12 76
        Set Label to "Selected Folders:"
        Set pbEnabled to False
        Procedure OnStartup
            String[] asFolders
            Get pasSelectedFolders of ghoApplication to asFolders
            Set Value to (SizeOfArray(asFolders))
        End_Procedure
    End_Object

    Object oSelectedFunctions_fm is a cRDCForm
        Set Location to 111 84
        Set Size to 12 76
        Set Label to "Selected Functions:"
        Set pbEnabled to False
        Procedure OnStartup
            Set Value to SysFile.SelectedFunctionTotal
        End_Procedure
    End_Object

    Object oSWSFile_cb is a CheckBox
        Set Location to 139 84
        Set Size to 10 50
        Set Label to "Export .sws File Name (See tooltips for explanations)"
        Set Checked_State to True
        Set psToolTip to "If an .sws file is not present in the ini-file, the name of the .sws file *must* be specified on the command line with the /sws flag."
    End_Object

    Object oFolders_cb is a CheckBox
        Set Location to 152 84
        Set Size to 10 50
        Set Label to "Export Selection of Source Code Folders"
        Set Checked_State to True
        Set psToolTip to "If omitted, the current selections of folders made for that workspace, will be used."
    End_Object

    Object oFilters_cb is a CheckBox
        Set Location to 165 84
        Set Size to 10 50
        Set Label to "Export File Filter Selections"
        Set Checked_State to True
        Set psToolTip to "If omitted, the current program's 'File Filter' settings will be used."
    End_Object

    Object oFunctions_cb is a CheckBox
        Set Location to 178 84
        Set Size to 10 50
        Set Label to "Export Selection of Refactoring Functions"
        Set Checked_State to True
        Set psToolTip to "If omitted, the current selections of refactoring functions will be used."
    End_Object

    Object oExport_btn is a Button
        Set Size to 14 50
        Set Label    to "Export"
        Set Location to 200 266
        Set peAnchors to anBottomRight

        Procedure OnClick
            Integer iRetval
            String sFileName sSWSFile sRetval sUserName 
            Boolean bExists bOK bSWSFile bFunctions bFolders bFilters
            
            Get Value of oIniFile_fm to sFileName 
            File_Exist sFileName bExists
            Get YesNo_Box ("Export current selections, for the selected checkboxes, to this file?" * sFileName) to iRetval
            If (iRetval <> MBR_Yes) Begin
                Procedure_Return   
            End
            If (bExists = False) Begin
                Get YesNo_Box ("The file" * sFileName * "doesn't exist. Do you want to create it?") to iRetval
                If (iRetval <> MBR_Yes) Begin
                    Procedure_Return   
                End
            End
            Get Checked_State of oSWSFile_cb to bSWSFile
            If (bSWSFile = True) Begin
                Get Value of oSWSFile_fm to sSWSFile
                File_Exist sSWSFile bExists
                If (bExists = False) Begin
                    Send Info_Box ("The .sws file:" * sSWSFile * "doesn't exist. Cannot continue with this selection.")
                    Procedure_Return
                End
            End
            Get Value of oUserName_fm to sUserName
            Get Checked_State of oFolders_cb to bFolders
            Get Checked_State of oFilters_cb to bFilters
            Get Checked_State of oFunctions_cb to bFunctions
            Get ExportToIniFile sFileName bSWSFile sSWSFile sUserName bFolders bFilters bFunctions to sRetval
            If (not(Lowercase(sRetval) contains "error")) Begin
                Send Info_Box "Settings exported successfully."
            End
            Else Begin
                Send Info_Box sRetval "Export failed!"
            End
        End_Procedure

    End_Object

    Object oClose_btn is a Button
        Set Size to 14 50
        Set Label    to "&Close"
        Set Location to 200 322
        Set peAnchors to anBottomRight

        Procedure OnClick
           Send Close_Panel
        End_Procedure

    End_Object

    Procedure WipeExistingIniValues Handle hoIni String sSection String sKey
        Integer iItems iCount
        
        Move 1000 to iItems // Just a very high number...
        For iCount from 1 to iItems
            Send WriteString of hoIni sSection (sKey + String(iCount)) ""
        Loop
    End_Procedure
    
    Function ExportToIniFile String sFileName Boolean bSWSFile String sSWSFile String sUserName Boolean bFolders Boolean bFilters Boolean bFunctions Returns String
        Boolean bOK bExists
        Handle hoIni
        String sHomePath sFilters
        Integer iCount
        
        Move False to Err
        Get Create (RefClass(cIniFile)) to hoIni  
        Set psFileName of hoIni to sFileName

        If (bSWSFile = True) Begin
            Send WriteString of hoIni CS_CmdIniSWSFileSection CS_CmdIniSWSFile sSWSFile
        End

        If (bFolders = True) Begin
            // Start by wiping any existing folder selections from the ini-file:
            Send WipeExistingIniValues hoIni CS_CmdIniFoldersSection CS_CmdIniFolderName
            
            If (sUserName = "") Begin
                Get psUserName of ghoApplication to sUserName
            End
            Send WriteString of hoIni CS_CmdIniUserSection CS_CmdIniUserName sUserName       
                
            Get psHomePath of ghoApplication to sHomePath 
            Clear FolderSelHeaA
            Move sUserName to FolderSelHeaA.UserName
            Move sHomePath to FolderSelHeaA.WorkspaceHomeFolder
            Find eq FolderSelHeaA by Index.2
            
            Constraint_Set (Self) Clear
            Constrained_Clear eq FolderSelDtlA by Index.3
            If (Found = False) Begin
                Function_Return ("Error. The FolderSelHeaA record could not be found for User name:" * String(sUserName) + "\and workspace Home path:" * String(sHomePath))
            End                   
            Constrain FolderSelDtlA.FolderSelHeaID eq FolderSelHeaA.ID
            Constrain FolderSelDtlA.bSelected eq 1
            Constrained_Find First FolderSelDtlA by Index.3
            Move 1 to iCount
            While (Found = True)
                Send WriteString of hoIni CS_CmdIniFoldersSection (CS_CmdIniFolderName + String(iCount)) (Trim(FolderSelDtlA.FolderName))
                Increment iCount  
                Constrained_Find Next
            Loop
        End 
        
        If (bFilters = True) Begin
            Move (Trim(SysFile.FileExtensionFilter)) to sFilters
            Send WriteString of hoIni CS_CmdIniFileFilterSection CS_CmdIniFileFilters sFilters
        End   
        
        If (bFunctions = True) Begin
            // Start by wiping any existing function selections from the ini-file:
            Send WipeExistingIniValues hoIni CS_CmdIniFunctionSection CS_CmdInitFunction
            
            Move 1 to iCount
            Constraint_Set (Self +1) Clear
            Constrained_Clear eq FunctionsA by Index.3
            Constrain FunctionsA.Selected eq 1
            Constrained_Find First FunctionsA by Index.3
            While (Found = True)
                If (FunctionsA.Parameter = "") Begin
                    Send WriteString of hoIni CS_CmdIniFunctionSection (CS_CmdInitFunction + String(iCount)) (Trim(FunctionsA.Function_Name))
                End
                Else Begin
                    Send WriteString of hoIni CS_CmdIniFunctionSection (CS_CmdInitFunction + String(iCount)) (Trim(FunctionsA.Function_Name) + "," + Trim(FunctionsA.Parameter))
                End
                Increment iCount  
                Constrained_Find Next
            Loop
        End

        Send Destroy of hoIni        
        Function_Return (Err = False)
    End_Function

    Procedure Popup
        Broadcast Send OnStartup
        Forward Send Popup
    End_Procedure

    On_Key Key_Alt+Key_C Send KeyAction of oClose_btn
End_Object

Use cRDCDbModalPanel.pkg
Use cStatLogDataDictionary.dd
Use cRDCDbCJGrid.pkg
Use cRDCDbCJGridColumn.pkg
Use cRDCButtonDPI.pkg
Use Windows.pkg

Object oErrorLog_dg is a cRDCDbModalPanel
    Set Label to "Refactoring Engine Error Log"
    Set Size to 247 560
    Set piMinSize to 89 211
    Set Location to 2 2
    Set Icon to "ErrorLog.ico"
    Set Border_Style to Border_Thick

    Object oStatLog_DD is a cStatLogDataDictionary
    End_Object

    Set Main_DD to oStatLog_DD
    Set Server to oStatLog_DD

    Object oInternalErrors_grd is a cRDCDbCJGrid
        Set Size to 205 542
        Set Location to 11 9
        Set pbAllowEdit to False
        Set pbAutoAppend to False
        Set pbAllowDeleteRow to True  
        Set pbDbShowRemoveFolderMenuItem to True

        Object oStatLog_FunctionName is a cRDCDbCJGridColumn
            Entry_Item StatLog.FunctionName
            Set piWidth to 262
            Set psCaption to "FunctionName"
        End_Object

        Object oStatLog_Description is a cRDCDbCJGridColumn
            Entry_Item StatLog.Description
            Set piWidth to 436
            Set psCaption to "Description"
            Set psToolTip to "Top Tip: If you see a description text that has a 'iErrLine = nnnnn', it is possible, in debug mode in the Studio, to press Alt+G, enter value and go to the offending source line."

            Function OnGetTooltip Integer iRow String sValue String sText Returns String
                Move "Top Tip: If you see a description text that has a 'iErrLine = nnnnn', it is possible, in debug mode in the Studio, to press Alt+G, enter value and go to the offending source line." to sText
                Function_Return sText
            End_Function
            
        End_Object

        Object oStatLog_Date is a cRDCDbCJGridColumn
            Entry_Item StatLog.Date
            Set piWidth to 83
            Set psCaption to "Date"  
            Set pbVisible to False
        End_Object

        Object oStatLog_Time is a cRDCDbCJGridColumn
            Entry_Item StatLog.Time
            Set piWidth to 123
            Set psCaption to "Time"
        End_Object
        
        On_Key Key_Delete       Send Request_Delete
        On_Key Key_Shift+Key_F2 Send Request_Delete 
    End_Object

    Object oZeroFile_btn is a cRDCButtonDPI
        Set Size to 14 78
        Set Label to "Delete All Records"
        Set Location to 226 408
        Set peAnchors to anBottomRight  
        Set psImage to "ActionDelete.ico"

        Procedure OnClick
            Integer eResponse
            Get YesNo_Box "Are you sure you want to delete all logged error messages?" to eResponse
            If (eResponse <> MBR_Yes) Begin
                Procedure_Return
            End
            ZeroFile StatLog  
            Send MoveToFirstRow of oInternalErrors_grd
        End_Procedure

    End_Object

    Object oClose_btn is a Button
        Set Size to 14 50
        Set Label    to "&Close"
        Set Location to 226 502
        Set peAnchors to anBottomRight

        Procedure OnClick
           Send Close_Panel
        End_Procedure

    End_Object

    Object oDebugInfo_txt is a TextBox
        Set Auto_Size_State to False
        Set Size to 17 383
        Set Location to 227 12
        Set Label to "Top Tip: If you see a description text that has a 'iErrLine = nnnnn', it is possible, in debug mode in the Studio, to press Alt+G, enter value and go to the offending source line."
        Set Justification_Mode to JMode_Left
        Set peAnchors to anBottomLeftRight
    End_Object

    On_Key Key_Alt+Key_C Send KeyAction of oClose_btn
End_Object

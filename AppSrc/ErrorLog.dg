Use Windows.pkg
Use seq_chnl.pkg
Use cRDCDbModalPanel.pkg
Use cRDCDbCJGrid.pkg
Use cRDCDbCJGridColumn.pkg
Use cRDCButtonDPI.pkg

Use cStatLogDataDictionary.dd

Object oErrorLog_dg is a cRDCDbModalPanel
    Set Label to "Refactoring Engine Error Log - Errors that occured while Refactoring Code"
    Set Size to 247 560
    Set piMinSize to 89 211
    Set Location to 2 2
    Set Icon to "ErrorLog.ico"
    Set Border_Style to Border_Thick
    Set Locate_Mode to CENTER_ON_PARENT
    
    Object oStatLog_DD is a cStatLogDataDictionary
    End_Object

    Set Main_DD to oStatLog_DD
    Set Server to oStatLog_DD

    Object oInternalErrors_grd is a cRDCDbCJGrid
        Set Size to 205 542
        Set Location to 11 9
        Set pbAllowEdit to True
        Set pbAutoAppend to False
        Set pbAllowDeleteRow to True  
        Set pbDbShowRemoveItemMenuItem to True 
        Set phoCheckbox_Col to 0 
        Set phoData_Col to 0

        Object oStatLog_FunctionName is a cRDCDbCJGridColumn
            Entry_Item StatLog.FunctionName
            Set piWidth to 162
            Set psCaption to "FunctionName"
        End_Object

        Object oStatLog_Description is a cRDCDbCJGridColumn
            Entry_Item StatLog.Description
            Set piWidth to 436
            Set psCaption to "Description - Press Ctrl+C to copy an iErrLine value"
            Set psToolTip to "Top Tip: If you see a description text that has a 'iErrLine = nnnnn', it is possible, in debug mode in the Studio, to press Alt+G, enter value and go to the offending source line."

//            Function OnGetTooltip Integer iRow String sValue String sText Returns String
//                Move "Top Tip: If you see a description text that has a 'iErrLine = nnnnn', it is possible, in debug mode in the Studio, to press Alt+G, enter value and go to the offending source line." to sText
//                Function_Return sText
//            End_Function
            
        End_Object

        Object oStatLog_Date is a cRDCDbCJGridColumn
            Entry_Item StatLog.Date
            Set piWidth to 83
            Set psCaption to "Date"  
            Set pbVisible to False
        End_Object

        Object oStatLog_Time is a cRDCDbCJGridColumn
            Entry_Item StatLog.Time
            Set piWidth to 135
            Set psCaption to "Time"
        End_Object
        
        Procedure End_Construct_Object       
            Forward Send End_Construct_Object
            Set phoContextMenu to oDEOEditContextMenu // DAW standard context menu 
        End_Procedure
        
        Procedure Copy
            Integer iCh iPos iLen
            String sErrNum
            
            Get Field_Current_Value of oStatLog_DD Field StatLog.Description to sErrNum
            Move (Pos("iErrLine = ", sErrNum)) to iPos
            If (iPos = 0) Begin
                Procedure_Return
            End

            Move (Length("iErrLine = ")) to iLen
            Move (Trim(Mid(sErrNum, Length(sErrNum), (iPos + iLen)))) to sErrNum
            Move (Pos(" ", sErrNum)) to iPos
            If (iPos <> 0) Begin
                Move (Trim(Left(sErrNum, iPos))) to sErrNum
            End
            Get Seq_New_Channel to iCh
            Direct_Output channel iCh "CLIPBOARD:"
                Writeln channel iCh sErrNum
            Close_Output channel iCh
            Send Seq_Release_Channel iCh
            
            Send Info_Box "iErrLine value copied to Windows clipboard.\nPress Alt+G in the Studio, while in debug mode, paste this value to go to the source line where the error occured."
        End_Procedure 
        
        Procedure DoSetCheckboxFooterText  
            Integer iErrors 
            
            Get_Attribute DF_FILE_RECORDS_USED of StatLog.File_Number to iErrors
            Set psFooterText of oStatLog_FunctionName to ("Total number of errors:" * String(iErrors))
        End_Procedure
          
        On_Key Key_Delete       Send Request_Delete
        On_Key Key_Shift+Key_F2 Send Request_Delete  
        On_Key Key_Ctrl+Key_C   Send Copy
    End_Object

    Object oZeroFile_btn is a cRDCButtonDPI
        Set Size to 14 82
        Set Label to "Delete All Records"
        Set Location to 226 415
        Set peAnchors to anBottomRight  
        Set psImage to "ActionDelete.ico"

        Procedure OnClick
            Integer eResponse
            Get YesNo_Box "Are you sure you want to delete all logged error messages?" to eResponse
            If (eResponse <> MBR_Yes) Begin
                Procedure_Return
            End
            ZeroFile StatLog  
            Send MoveToFirstRow of oInternalErrors_grd
        End_Procedure

    End_Object

    Object oClose_btn is a Button
        Set Size to 14 50
        Set Label    to "&Close"
        Set Location to 226 502
        Set peAnchors to anBottomRight

        Procedure OnClick
           Send Close_Panel
        End_Procedure

    End_Object

    Object oDebugInfo_txt is a TextBox
        Set Auto_Size_State to False
        Set Size to 17 383
        Set Location to 227 12
        Set Label to "Top Tip: If you see a description text that has a 'iErrLine = nnnnn', You can - wh¡le the Studio is in debug mode, to press Alt+G, enter the iErrLine value to go to that source line. (Ctrl+C to copy iErrLine value)"
        Set Justification_Mode to JMode_Left
        Set peAnchors to anBottomLeftRight
    End_Object

    On_Key Key_Alt+Key_C Send KeyAction of oClose_btn
End_Object

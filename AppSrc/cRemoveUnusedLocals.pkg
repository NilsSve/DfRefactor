//------------------------------------------------------------->
// ReportUnusedLocals.pkg
//      R.Worsley 3/09/2008
//      Locate any unused local variables
//      Conditions:
//          Variables declared in the method line are not checked
//          Read notes below on StringTokenizer
//          Will work with the old "Local" as a descriptor
//          Variables can be declared anywhere in the method
//
//      If used as a class in an another application, there are
//      a couple of properties that can be set to automate the
//      input and output file names.  See the property descriptions
//      in class cReportUnusedLocals
//
//------------------------------------------------------------>
//  04/08/2008 RLW  - Make into a class so the process can be
//                   incorporated into other applications
//  04/12/2008 RLW  - Change read/write registry process
//  04/12/2008 RLW  - Fix so that wrapped variables on the function
//                   or procedure line won't show odd results
//  06/22/2008 RLW  - Add missing Seq_Release_Channel calls
//  06/30/2018 Nils - Changed to use registry functions of the cApplication class.
//  10/26/2018 Nils - Rewrote to use a struct instead of multi-dimensional array,
//                    and to actually remove unused local variables (!), not just create a report.
//  11/03/2018 Nils - Rewrote most of the logic to make it more robust. Removed the StringTokenizer
//                    code to instead use the build in StrSplitToArray function.
//  09/09/2021 Nils - Merged all generalized functions with the cRefactorFunctionLibrary class.
//                    Also moved most constant declarations to SourceCode.inc
//------------------------------------------------------------->
Use cApplication.pkg
Use Batchdd.pkg

#IFNDEF ghoRefactorFunctionLibrary
Global_Variable Handle ghoRefactorFunctionLibrary
#ENDIF

Use cRefactorFunctionLibrary.pkg
Use vWin32fh.pkg

Struct tUnusedLocals
    Boolean bInUse
#IFDEF IS$WIN64
    Integer iMissingAlignment1
#ENDIF
    String VarName
    String MethodName
    Integer Line#
#IFDEF IS$WIN64
    Integer iMissingAlignment2
#ENDIF
End_Struct

Define CS_UnusedVariablesLogFile for "UnusedVariables.txt"

Class cRemoveUnusedLocals is a cObject
    Procedure Construct_Object
        String[] asLocalVariableTypes asLocalVariableArrayTypes

        Forward Send Construct_Object
        
        Property String[] pasSourceFile
        Property Integer piNoOfUnusedLocalVariables 0

    End_Procedure

    // Remove all variables that are used.
    Function CleanupLocalVariables tUnusedLocals[] asLocalVariables Returns tUnusedLocals
        Integer iCount iSize

        Move (SortArray(asLocalVariables))   to asLocalVariables
        Move (SizeOfArray(asLocalVariables)) to iSize
        Decrement iSize
        For iCount From 0 to iSize
            If (asLocalVariables[iCount].bInUse = True) Begin
                Move (RemoveFromArray(asLocalVariables, iCount)) to asLocalVariables
                Decrement iSize
                Decrement iCount
            End
        Loop

        Function_Return asLocalVariables
    End_Function

    // Custom comparison function:
    //   Returns (GT) struct value in first parameter > struct value in second parameter.
    //   Returns (LT) struct value in first parameter < struct value in second parameter.
    //   Otherwise returns (EQ).
    Function CompareLineNum tUnusedLocals Local1 tUnusedLocals Local2 Returns Integer
        If (Local1.Line# > Local2.Line#) ;
            Function_Return (GT)
        If (Local1.Line# < Local2.Line#) ;
            Function_Return (LT)
        Function_Return (EQ)
    End_Function

    // We now have the source file array and the unused variables struct array.
    // Remove those unused variables from the source file array.
    Procedure RemoveUnusedVariablesFromSourceArray String[] ByRef asSourceFile tUnusedLocals[] asLocalVariables Integer ByRef iUnusedLocalVariables
        Integer iCount iSize iItem iLength iOffset
        String sVarName sLine sVarType sVariableDeclarationConstant
        Boolean bInUse bFound bIgnore bChanged

        Move 0 to iOffset
        Get CleanupLocalVariables asLocalVariables      to asLocalVariables
        Move (SizeOfArray(asLocalVariables))            to iSize
        // sort array by line number as otherwise the iOffset logic won't work!
        Move (SortArray(asLocalVariables, Self, (RefFunc(CompareLineNum)))) to asLocalVariables
        Decrement iSize
        For iCount From 0 to iSize
            Move asLocalVariables[iCount].bInUse        to bInUse
            If (bInUse = False) Begin
                Move asLocalVariables[iCount].Line#     to iItem
                Move (iItem - 1 + iOffset)              to iItem
                Move asLocalVariables[iCount].VarName   to sVarName
                Move (Length(sVarName))                 to iLength
                Move asSourceFile[iItem]                to sLine
                Get RemoveLocalKeyWord of ghoRefactorFunctionLibrary (&sLine) to bIgnore
                Get _RetrieveFirstToken of ghoRefactorFunctionLibrary sLine   to sVarType

                Move ((sLine+" ") contains (" " + sVarName + " "))  to bFound
                If (bFound = False) Begin
                    Move (sLine contains sVarName)      to bFound
                End
                If (bFound = True) Begin
                    Move (Replace((" " + sVarName + " "), sLine+" ", " ")) to sLine
                End

                Get _NormalizeArrayNotation of ghoRefactorFunctionLibrary (&sLine) to bChanged
                // If all that remains on the line is the variable type declaration,
                // remove the line from the source array.
                Move sLine to sVariableDeclarationConstant
                Get RemoveLocalKeyWord of ghoRefactorFunctionLibrary (&sVariableDeclarationConstant) to bIgnore
                Get _RemoveEndComment of ghoRefactorFunctionLibrary sVariableDeclarationConstant to sVariableDeclarationConstant
                Move (Trim(sVariableDeclarationConstant)) to sVariableDeclarationConstant
                If (sVarType = sVariableDeclarationConstant) Begin
                    Move (RemoveFromArray(asSourceFile, iItem)) to asSourceFile
                    Decrement iOffset
                End
                Else Begin
                    Move (RTrim(sLine)) to sLine
                    Move sLine to asSourceFile[iItem]
                End

                Increment iUnusedLocalVariables
            End
        Loop

        Procedure_Return
    End_Procedure
    
    // *** MAIN PROCEDURE ***
    // Call this and pass a source file as string ByRef array.
    // Returns number of unused local variables
    Function MainProcedure String[] ByRef asSourceFile Returns Integer
        Set piNoOfUnusedLocalVariables to 0
        Send ParseFile (&asSourceFile)
        Function_Return (piNoOfUnusedLocalVariables(Self))
    End_Function

    Procedure ParseFile String[] ByRef asSourceFile
        String sLine sVars sVariableName sMethodName sSourceFile
        Integer iArrayCount iMax iCounter iItems iLineCount iUnusedLocalVariables iSize iCount
        tUnusedLocals[] asLocalVars  
        tTokenizer TokenizerData
        String[] asVars
        Boolean bComment bInMethod bVariableDeclaration bIsVariable bMethodEnd bIgnore   
        Boolean bLocalVariableDeclaration bLocalVariableArrayDeclaration

        Move (SizeOfArray(asSourceFile)) to iSize
        If (iSize = 0) Begin
            Get psIdleText of (phoStatusBar(ghoCommandBars)) to sSourceFile
            Send Info_Box ("Source File Array is empty.\n" + String(sSourceFile)) "Error in ParseFile method [cRemoveUnusedLocals.pkg]"
            Move True to Err
            Procedure_Return
        End

        Move False to bMethodEnd
        Decrement iSize
        Move False to Err
        Move 0 to iUnusedLocalVariables

        For iCount From 0 to iSize
            Move asSourceFile[iCount] to sLine
            Move (Trim(sLine)) to sLine
            Get Tokenizer of ghoRefactorFunctionLibrary sLine to TokenizerData
            Move TokenizerData.bIsComment to bComment
            Move TokenizerData.sOverstrikeLine to sLine

            If (bComment = False and Trim(sLine) <> "") Begin
                Get _IsMethodStart of ghoRefactorFunctionLibrary sLine to bInMethod
                Get _IsMethodEnd   of ghoRefactorFunctionLibrary sLine to bMethodEnd
                If (bInMethod = True and bMethodEnd = False) Begin
                    Get _MethodName of ghoRefactorFunctionLibrary sLine to sMethodName
                    // To start reading the next line after the start "Procedure" or "Function" line
                    Move (iCount + 1) to iLineCount
                    // If the method declaration is split over multiple lines, then we need to get past that part
                    While (Right(Trim(sLine), 1) = ";")
                        If (iLineCount <= iSize) Begin
                            Move asSourceFile[iLineCount] to sLine
                        End
                        Else Begin
                          Move "" to sLine
                        End

                        Get _OverstrikeStrings of ghoRefactorFunctionLibrary sLine to sLine
                        Increment iLineCount
                    Loop

                    // Begin search line-by-line until "End_Procedure" or "End_Function"
                    Repeat
                        If (iLineCount <= iSize) Begin
                            Move asSourceFile[iLineCount] to sLine
                        End

                        Move (Trim(sLine)) to sLine
                        Move TokenizerData.bIsComment to bComment
                        Get _IsMethodEnd       of ghoRefactorFunctionLibrary TokenizerData.sOverstrikeLine to bMethodEnd
                        If (TokenizerData.bIsComment = False and TokenizerData.bIsBlank = False and bMethodEnd = False) Begin
                            Get RemoveLocalKeyWord of ghoRefactorFunctionLibrary (&sLine) to bIgnore

                            // Does the line contain one or more variable declarations?
                            Get _IsVariableDeclarationLine of ghoRefactorFunctionLibrary TokenizerData.sOverstrikeLine to bVariableDeclaration
                            If (bVariableDeclaration = True) Begin
                            // Remove the data type command
                                Get _RemoveFirstToken of ghoRefactorFunctionLibrary TokenizerData.sOverstrikeLine to sVars
                                Move (StrSplitToArray(Trim(sVars), " ")) to asVars

                                // Loop through the variable's array and add to struct array of locally declared variables.
                                // Need to do this here because we don't know where the programmer will place variable declarations,
                                // so we need to update the array as they occur.
                                Move (SizeOfArray(asVars)) to iItems
                                Decrement iItems
                                For iCounter From 0 to iItems
                                    Move asVars[iCounter] to sVariableName
                                    Move (SizeOfArray(asLocalVars)) to iArrayCount

                                    // If the procedure or function line is wrapped, it may
                                    // contain the variable type, and we don't want that
                                    // showing up as an unused variable        
                                    
                                    Get _IsVariableDeclaration of ghoRefactorFunctionLibrary sVariableName to bLocalVariableDeclaration
                                    Get _IsVariableArrayDeclaration of ghoRefactorFunctionLibrary sVariableName to bLocalVariableArrayDeclaration
                                    If (Trim(sVariableName) <> "" and (bLocalVariableDeclaration = True or bLocalVariableArrayDeclaration = True)) Begin
                                        Move sVariableName    to asLocalVars[iArrayCount].VarName
                                        Move False            to asLocalVars[iArrayCount].bInUse
                                        Move sMethodName      to asLocalVars[iArrayCount].MethodName
                                        // Array item number (zero based) to source code line number:
                                        Move (iLineCount + 1) to asLocalVars[iArrayCount].Line#
                                    End
                                Loop
                            End

                            // If not a variable declaration line. For each line, we look in the asLocalVars array,
                            // and if the variable is found we mark it as "used"
                            Else Begin
                                If (Trim(sLine) <> "") Begin
                                    Move (SizeOfArray(asLocalVars)) to iMax
                                    Decrement iMax
                                    For iCounter From 0 to iMax
                                        If (asLocalVars[iCounter].bInUse = False and asLocalVars[iCounter].MethodName = sMethodName) Begin
                                            Get _IsVariableInLine of ghoRefactorFunctionLibrary sLine asLocalVars[iCounter].VarName to bIsVariable
                                            If (bIsVariable = True) Begin
                                                // Mark as found
                                                Move True to asLocalVars[iCounter].bInUse
                                            End
                                        End
                                    Loop
                                End
                            End
                        End

                        Increment iLineCount
                    Until (bMethodEnd or iCount = iSize or iLineCount >= iSize)
                End
            End
            If (iCount < iLineCount) Begin
                Move iLineCount to iCount
            End
        Loop

        If (SizeOfArray(asLocalVars)) Begin
            Send RemoveUnusedVariablesFromSourceArray (&asSourceFile) asLocalVars (&iUnusedLocalVariables)
        End

        Get piNoOfUnusedLocalVariables to iCounter
        Set piNoOfUnusedLocalVariables to (iCounter + iUnusedLocalVariables)
    End_Procedure

End_Class

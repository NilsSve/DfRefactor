Use cJsonObject.pkg
Use Variant.pkg

Function IsInteger Global Variant vVal Returns Boolean
    Address aVar
    Integer iType

    Move (AddressOf(vVal)) to aVar
    Move (DeRefW(aVar, 0)) to iType
    Function_Return ((iType = OLE_VT_I1)  or ;  // Char
                     (iType = OLE_VT_I2)  or ;  // Short
                     (iType = OLE_VT_I4)  or ;  // Integer
                     (iType = OLE_VT_I8)  or ;  // BigInt
                     (iType = OLE_VT_Ui1) or ;  // Unsigned Char
                     (iType = OLE_VT_Ui2) or ;  // Unsigned Short
                     (iType = OLE_VT_Ui4))      // Unsigned Integer
End_Function

Function MemberEnh For cJsonObject Variant vMember Returns Handle
    
    If ((JsonType(Self) = jsonTypeObject) and IsInteger(vMember)) Begin
        Get MemberNameByIndex vMember to vMember
    End
    
    Function_Return (Member(Self, vMember))
End_Function

Function MemberValueEnh For cJsonObject Variant vMember Returns String
    String  sVal
    
    If ((JsonType(Self) = jsonTypeObject) and IsInteger(vMember)) Begin
        Get MemberNameByIndex vMember to vMember
    End
    
    If (MemberJsonType(Self, vMember) <> jsonTypeNull) Begin
        Get MemberValue vMember to sVal
    End
    
    Function_Return sVal
End_Function

Function IsMemberOfJsonTypeEnh For cJsonObject Variant vMember Integer eType returns Boolean
    
    If ((JsonType(Self) = jsonTypeObject) and IsInteger(vMember)) Begin
        Get MemberNameByIndex vMember to vMember
    End
    
    Function_Return (MemberJsonType(Self, vMember) = eType)
End_Function

Function MemberJsonTypeEnh For cJsonObject Variant vMember Returns Integer
    
    If ((JsonType(Self) = jsonTypeObject) and IsInteger(vMember)) Begin
        Get MemberNameByIndex vMember to vMember
    End
    
    Function_Return (MemberJsonType(Self, vMember))
End_Function

Procedure RemoveMemberEnh For cJsonObject Variant vMember
    
    If ((JsonType(Self) = jsonTypeObject) and IsInteger(vMember)) Begin
        Get MemberNameByIndex vMember to vMember
    End
    
    Send RemoveMember vMember
End_Procedure

Procedure RemoveNulls For cJsonObject
    Integer i iMax
    Handle  hoMemb
    
    If not ((JsonType(Self) = jsonTypeObject) or ;
            (JsonType(Self) = jsonTypeArray)) Begin
        Procedure_Return
    End
    
    Move (MemberCount(Self) - 1) to iMax
    
    For i from 0 to iMax
        
        If (MemberJsonTypeEnh(Self, i) = jsonTypeNull) Begin
            Send RemoveMemberEnh i
            Decrement i
            Decrement iMax
        End
        Else If ((MemberJsonTypeEnh(Self, i) = jsonTypeObject) or ;
                 (MemberJsonTypeEnh(Self, i) = jsonTypeArray)) Begin
            Get MemberEnh i     to hoMemb
            Send RemoveNulls    of hoMemb
            Send Destroy        of hoMemb
        End
        
    Loop
    
End_Procedure

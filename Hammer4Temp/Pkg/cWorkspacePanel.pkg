//
//  File cWorkspacePanel.pkg
//  Author: Wil van Antwerpen
//  Company: Antwise Solutions
//  License: LGPL
//  Date: March 2, 2003
//
// This class is used to support a workspace selector for all VDF versions and will
// run under VDF 7.
//

Use cWorkSpaceHandlerEx.pkg
Use Windows.pkg

Enum_List
    Define ctWSDisplayDescription    for 1
    Define ctWSDisplayKey
    Define ctWSDisplayBoth
End_Enum_List

Class cWorkspacePanel is a ModalPanel
    Procedure Construct_Object
        Integer iOffset
        Forward Send Construct_Object
        Property String  psCurrentWorkSpace    ""
        Property Integer pcDisplayWSList       ctWSDisplayDescription
        Property String  psInvokingVersion     "" // version on starting this panel
        Property String  psInvokingVdfRootDir  "" // ditto

        Set Sysmenu_Icon to False
        Set Locate_Mode to center_on_screen
    End_Procedure // Construct_Object

    // Selected VDF Version, string key taken from the registry
    Procedure Set psVersion String sVersion
        Set psBufferVdfVersion of ghoWorkspaceHandlerEx to sVersion
    End_Procedure // Set psVersion

    // Selected VDF Version, string key taken from the registry
    Function psVersion Returns String
        String sVersion
        Get psBufferVdfVersion of ghoWorkspaceHandlerEx to sVersion
        Function_Return sVersion
    End_Function // psVersion

    // Selected VDF Version VDF7=70, VDF8=80, VDF8.2=82 and so on
    Function pnVersion Returns Number
        Number nVersion
        String sVersion
        Get psBufferVdfVersion of ghoWorkspaceHandlerEx to sVersion
        Get StringVdfVersionToNum of ghoWorkspaceHandlerEx sVersion to nVersion
        Function_Return nVersion
    End_Function // pnVersion

    Procedure Request_Cancel
        String sVersion
        String sVdfRoot
        // Restore the VDF version and DataFlex root dir as it wasn't selected
        // These properties are messed around with when changing the DataFlex version
        // and this panel depends on those changed values to function, so we restore
        // on closing if the new version isn't selected.
        Get psInvokingVersion    To sVersion
        Get psInvokingVdfRootDir to sVdfRoot
        Set psVersion To sVersion
        Set psVDFVersion of ghoWorkSpaceHandlerEx to sVersion
        Set psVdfRootDir Of ghoWorkspaceHandlerEx To sVdfRoot
        Send Close_Panel
    End_Procedure // Request_Cancel

    Procedure Popup_Group
        String sVersion
        String sVdfRoot
        Forward Send Popup_Group
        Get psVDFVersion Of ghoWorkSpaceHandlerEx To sVersion
        Get psVdfRootDir Of ghoWorkspaceHandlerEx To sVdfRoot
        Set psInvokingVersion    To sVersion
        Set psInvokingVdfRootDir To sVdfRoot
    End_Procedure

End_Class // cWorkspacePanel


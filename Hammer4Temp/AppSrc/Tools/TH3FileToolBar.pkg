// TH3FileToolBar.pkg (File Toolbar Pane)
// Created by Sergey V Natarov (senatc@postman.ru) on 09/03/2017 @ 11:40
//
Use cCJCommandBarSystem.pkg

Class cCJOpenFileByTypeMenuItem is a cCJMenuItem
    Procedure Construct_Object
        Forward Send Construct_Object
        Property Integer piTypeIndex 0
    End_Procedure
    Procedure OnExecute Variant vCommandBarControl
        Integer iType
        Handle hoClient
        Get Client_Id to hoClient
        Get piTypeIndex to iType
        Send CAOpenMultipleFilesByType to hoClient iType
    End_Procedure
End_Class

Class cCreateNewPopupItem is a cCJMenuItem
    Procedure Construct_Object
        Forward Send Construct_Object
        Set pbControlBeginGroup to True
        Set psCaption   to "Create New"
        Set psToolTip to "New source file"
        Set psDescription to "Create new source file"
        Set psShortcut to ""
        Set psImage to "ActionNew.ico"
    End_Procedure
    Procedure OnExecute Variant vCommandBarControl
        Handle hoClient
        Get Client_Id   to hoClient
        Send CANewFile  to hoClient
    End_Procedure
End_Class


Object oFileToolBar is a cCJToolbar
    Set psTitle to "File"

    Object oNewItem is a cCJMenuItem
        Set pbAddToDesignerMenu to True
        //Set peControlStyle to xtpButtonAutomatic
        Set psCaption   to "&New"
        Set psToolTip to "Create New File"
        Set psDescription to "Create a new file within the current workspace"
        Set psShortcut to "Ctrl+N"
        Set psImage to "THNew32.ico"
        Procedure OnExecute Variant vCommandBarControl
            Handle hoClient
            Get Client_Id   to hoClient
            Send CANewFile  to hoClient
        End_Procedure
        Function CreateComControl Handle hoControls Returns Variant
            Variant vControl
            Forward Get CreateComControl hoControls to vControl
            Send ComSetIconSize 32 32
            Function_Return vControl
        End_Function

    End_Object

    Object oOpenItem is a cCJMenuItem
        Property Handle[] phArrayOfWindows
        Set pbAddToDesignerMenu to True
        Set peControlType to xtpControlSplitButtonPopup //xtpControlPopup
        Set peControlStyle to xtpButtonIcon
        Set psCaption   to "Open"
        Set psToolTip to "Open file"
        Set psDescription to "Open source file"
        Set psShortcut to ""
        Set psImage to "THOpen32.ico"

        Procedure OnExecute Variant vCommandBarControl
            Handle hoClient
            Get Client_Id to hoClient
            Send CAOpenMultipleFiles to hoClient
        End_Procedure

        Procedure OnPopupInit Variant vCommandBarControl Handle hCommandBarControls
            Forward Send OnPopupInit vCommandBarControl hCommandBarControls
            Integer     iItem
            Integer     iMaxItems iWindows
            String      sType
            String      sDescription sDesc
            String      sTypes
            Handle[]    hArrayOfWindows
            Variant     vItem

            Move 15 to iMaxItems
            Get psFileTypes of ghoEditorProperties to sTypes
            // Delete Data
            Get phArrayOfWindows to hArrayOfWindows
            Move (SizeOfArray(hArrayOfWindows)) to iWindows
            For iItem from 0 to (iWindows-1)
                Send Destroy of hArrayOfWindows[iItem] // assume this removes all menu items of this action
            Loop
            Move (ResizeArray(hArrayOfWindows,0)) to hArrayOfWindows
            // Generate Menu
            Move 0  to iItem
            While (pos("|",sTypes)<>0 and iItem<iMaxItems)

                Move (Left(sTypes,pos("|",sTypes)-1)) to sDescription
                Move (Replace(sDescription+"|",sTypes,"")) to sTypes
                Move (Left(sTypes,pos("|",sTypes)-1)) to sType
                Move (Replace(sType+"|",sTypes,"")) to sTypes
                //
                Get Create U_cCJOpenFileByTypeMenuItem ;
                                        to hArrayOfWindows[iItem]
                Set psCaption           of hArrayOfWindows[iItem] to sDescription
                Set psShortcut          of hArrayOfWindows[iItem] to sType
                #IFDEF TH_TRANSLATION
                Move (Replace("%1", gILanguage[366], sDescription)) to sDesc
                #ELSE
                Move ("Open '" - sDescription - "' file.") to sDesc
                #ENDIF
                Set psDescription       of hArrayOfWindows[iItem] to sDesc
                Set piTypeIndex         of hArrayOfWindows[iItem] to (iItem+1)
                Get AddDynamicControl   of hArrayOfWindows[iItem] hCommandBarControls to vItem
                //
                Increment iItem
            Loop
            // Create new (final option)
            Get Create U_cCreateNewPopupItem ;
                                    to hArrayOfWindows[iItem]
            #IFDEF TH_TRANSLATION
            Set psCaption       of hArrayOfWindows[iItem] to gILanguage[356]
            Set psToolTip       of hArrayOfWindows[iItem] to gILanguage[357]
            Set psDescription   of hArrayOfWindows[iItem] to gILanguage[358]
            #ENDIF
            Get AddDynamicControl   of hArrayOfWindows[iItem] hCommandBarControls to vItem
            //
            Set phArrayOfWindows to hArrayOfWindows
            //
        End_Procedure

    End_Object

    Object oOpenPkgItem is a cCJMenuItem
        Set pbAddToDesignerMenu to True
        Set peControlStyle to xtpButtonIconAndCaption
        //Set psImage to "New16.bmp"
        Set psCaption   to "PKG"
        Set psToolTip to "Open PKG file"
        Set psDescription to "Open a source file from workspace packages folder (if defined)"
        Set psImage to "THOpenOther32.ico"
        Set psShortcut to ""
        Procedure OnExecute Variant vCommandBarControl
            Handle hoClient
            Get Client_Id to hoClient
            Send CAOpenFile_Pkg to hoClient
        End_Procedure
    End_Object

    Object oOpenDDItem is a cCJMenuItem
        Set pbAddToDesignerMenu to True
        //Set psImage to "New16.bmp"
        Set psCaption   to "DD"
        Set peControlStyle to xtpButtonIconAndCaption
        Set psToolTip to "Open DD file"
        Set psDescription to "Open a Data Dictionary source file"
        Set psImage to "THOpenOther32.ico"
        Set psShortcut to ""
        Procedure OnExecute Variant vCommandBarControl
            Handle hoClient
            Get Client_Id to hoClient
            Send CAOpenFile_DD to hoClient
        End_Procedure
    End_Object

    Object oOpenHtmlItem is a cCJMenuItem
        Set pbAddToDesignerMenu to True
        //Set psImage to "New16.bmp"
        Set psCaption   to "HTM"
        Set peControlStyle to xtpButtonIconAndCaption
        Set psToolTip to "Open HTML file"
        Set psDescription to "Open a HTML source file"
        Set psImage to "THOpenOther32.ico"
        Set psShortcut to ""
        Procedure OnExecute Variant vCommandBarControl
            Handle hoClient
            Get Client_Id to hoClient
            Send CAOpenFile_Html to hoClient
        End_Procedure
    End_Object

    Object oSaveItem is a cCJMenuItem
        Set pbAddToDesignerMenu to True
        Set psCaption   to "Save"
        Set peControlStyle to xtpButtonIcon
        Set psToolTip to "Save File"
        Set psDescription to "Saves currently opened file source file"
        Set psImage to "THSave32.ico"
        Set psShortcut to ""
        Procedure OnExecute Variant vCommandBarControl
            Handle hoClient
            Get Client_Id to hoClient
            Send CASaveFile to hoClient
        End_Procedure
    End_Object

    Object oSaveAllItem is a cCJMenuItem
        Set pbAddToDesignerMenu to True
        Set psCaption   to "Save All"
        Set peControlStyle to xtpButtonIcon
        Set psToolTip to "Save All"
        Set psDescription to "Saves all opened files"
        Set psImage to "THSaveAll32.ico"
        Set psShortcut to ""
        Procedure OnExecute Variant vCommandBarControl
            Handle hoClient
            Get Client_Id to hoClient
            Send CASaveAllFiles to hoClient
        End_Procedure
    End_Object

    Object oPrintItem is a cCJMenuItem
        Set pbAddToDesignerMenu to True
        Set psCaption   to "Print"
        Set peControlStyle to xtpButtonIcon
        Set psToolTip to "Print"
        Set psDescription to "Print current source file"
        Set psImage to "THPrint32.ico"
        Set psShortcut to ""
        Procedure OnExecute Variant vCommandBarControl
            Handle hoClient
            Get Client_Id to hoClient
            Send CAPrintFile to hoClient
        End_Procedure
    End_Object

    #IFDEF TH_TRANSLATION
    Procedure Translate
        Set psTitle to gILanguage[359]

        Set psCaption       of oNewItem to gILanguage[360]
        Set psToolTip       of oNewItem to gILanguage[361]
        Set psDescription   of oNewItem to gILanguage[362]

        Set psCaption       of oOpenItem to gILanguage[363]
        Set psToolTip       of oOpenItem to gILanguage[364]
        Set psDescription   of oOpenItem to gILanguage[365]

        Set psCaption       of oOpenPkgItem to gILanguage[367]
        Set psToolTip       of oOpenPkgItem to gILanguage[368]
        Set psDescription   of oOpenPkgItem to gILanguage[369]

        Set psCaption       of oOpenDDItem to gILanguage[370]
        Set psToolTip       of oOpenDDItem to gILanguage[371]
        Set psDescription   of oOpenDDItem to gILanguage[372]

        Set psCaption       of oOpenHtmlItem to gILanguage[373]
        Set psToolTip       of oOpenHtmlItem to gILanguage[374]
        Set psDescription   of oOpenHtmlItem to gILanguage[375]

        Set psCaption       of oSaveItem to gILanguage[376]
        Set psToolTip       of oSaveItem to gILanguage[377]
        Set psDescription   of oSaveItem to gILanguage[378]

        Set psCaption       of oSaveAllItem to gILanguage[379]
        Set psToolTip       of oSaveAllItem to gILanguage[380]
        Set psDescription   of oSaveAllItem to gILanguage[381]

        Set psCaption       of oPrintItem to gILanguage[382]
        Set psToolTip       of oPrintItem to gILanguage[383]
        Set psDescription   of oPrintItem to gILanguage[384]
    End_Procedure
    #ENDIF

End_Object
